;衣服相关的处理
;────────────────────────────────────
;调教者的衣服初期化。勝手に拝借したも的な的で作者先生に最高的敬意被

;和是うけどこれcsvにすべきよね。つーわけでcsvにしました
;────────────────────────────────────
@CLOTHES_SETUP(ARG)
;衣服未设定（デフォ全裸）的场合的みここで设定し增加
IF !(EQUIP:ARG:配饰+EQUIP:ARG:袜子+EQUIP:ARG:下半身内衣+EQUIP:ARG:上半身内衣+EQUIP:ARG:下半身外套+EQUIP:ARG:上半身外套+EQUIP:ARG:全身外套)
	;脚但あれば短袜
	SIF !TALENT:ARG:无足
		EQUIP:ARG:袜子 = 1
	;男人的场合、短裤・裤子・Ｔ衬衫・外套
	IF TALENT:ARG:男人
		EQUIP:ARG:下半身内衣 = 3
		EQUIP:ARG:上半身内衣 = 4
		EQUIP:ARG:下半身外套 = 3
		EQUIP:ARG:上半身外套 = 1
	;貧乳的场合、内裤・吊带背心・连衣裙
	ELSEIF TALENT:ARG:貧乳
		EQUIP:ARG:下半身内衣 = 2
		EQUIP:ARG:上半身内衣 = 2
		EQUIP:ARG:全身外套 = 3
	;それ以外的场合、内裤・胸罩・裙子・外套
	ELSE
		EQUIP:ARG:下半身内衣 = 2
		EQUIP:ARG:上半身内衣 = 1
		EQUIP:ARG:下半身外套 = 1
		EQUIP:ARG:上半身外套 = 1
	ENDIF
	;扶她的场合衬裤に（内裤に入らない気但做的で）
	SIF TALENT:ARG:扶她
		EQUIP:ARG:下半身内衣 = 1
ENDIF

;────────────────────────────────────
;商店菜单で的服装セッティング
;────────────────────────────────────
@CLOTHES_SETTING
PRINTFORM %CALLNAME:MASTER%的衣服：
SIF CFLAG:MASTER:衣服 == 0
	PRINTFORM \@ EQUIP:MASTER:全身外套 ? [%DRESS(MASTER)%] # \@\@ EQUIP:MASTER:上半身外套 ? [%OUTER_T(MASTER)%] # \@\@ EQUIP:MASTER:下半身外套 ? [%OUTER_B(MASTER)%] # \@
SELECTCASE CFLAG:MASTER:衣服
	CASE IS < 2
		PRINTFORM \@ EQUIP:MASTER:上半身内衣 ? [%INNER_T(MASTER)%] # \@\@ EQUIP:MASTER:下半身内衣 ? [%INNER_B(MASTER)%] # \@\@ EQUIP:MASTER:袜子 ? [%SOCKS(MASTER)%] # \@\@ EQUIP:MASTER:配饰 ? [%GLOVES(MASTER)%] # \@
	CASE 2
		PRINT [全裸]
	CASEELSE
		PRINTFORM \@ EQUIP:MASTER:COSPLAY ? [%COSPLAY(MASTER)%] # \@
ENDSELECT
PRINTL 

IF TARGET >= 0
	PRINTFORM %CALLNAME:TARGET%的衣服：
	SIF CFLAG:TARGET:衣服 == 0
		PRINTFORM \@ EQUIP:TARGET:全身外套 ? [%DRESS(TARGET)%] # \@\@ EQUIP:TARGET:上半身外套 ? [%OUTER_T(TARGET)%] # \@\@ EQUIP:TARGET:下半身外套 ? [%OUTER_B(TARGET)%] # \@
	SELECTCASE CFLAG:TARGET:衣服
		CASE IS < 2
			PRINTFORM \@ EQUIP:TARGET:上半身内衣 ? [%INNER_T(TARGET)%] # \@\@ EQUIP:TARGET:下半身内衣 ? [%INNER_B(TARGET)%] # \@\@ EQUIP:TARGET:袜子 ? [%SOCKS(TARGET)%] # \@\@ EQUIP:TARGET:配饰 ? [%GLOVES(TARGET)%] # \@
		CASE 2
			PRINT [全裸]
		CASEELSE
			PRINTFORM \@ EQUIP:TARGET:COSPLAY ? [%COSPLAY(TARGET)%] # \@
	ENDSELECT
	PRINTL 
ENDIF

DRAWLINE
PRINTL  [ 0] 返回
PRINTFORML  [ 1] 将%CALLNAME:MASTER%的服装设定为[通常]
PRINTFORML  [ 2] 将%CALLNAME:MASTER%的服装设定为[半裸]
PRINTFORML  [ 3] 将%CALLNAME:MASTER%的服装设定为[全裸]
IF TARGET >= 0
	PRINTFORML  [51] 将%CALLNAME:TARGET%的服装设定为[通常]
	PRINTFORML  [52] 将%CALLNAME:TARGET%的服装设定为[半裸]
	PRINTFORML  [53] 将%CALLNAME:TARGET%的服装设定为[全裸]
ENDIF

$INPUT_LOOPC
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF RESULT == 1
	IF CFLAG:MASTER:衣服 == 0
		PRINTFORMW %CALLNAME:MASTER%的衣服已被设定为[通常]了
		PRINTL 
		CALL KOJO_EVENT(101, 1000)
		GOTO INPUT_LOOPC
	ELSE
		CFLAG:MASTER:衣服 = 0
		PRINTFORMW 把%CALLNAME:MASTER%的衣服设定为[通常]了
		PRINTL 
		CALL KOJO_EVENT(101, 1001)
	ENDIF
	RESTART
ELSEIF RESULT == 2
	IF CFLAG:MASTER:衣服 == 1
		PRINTFORMW %CALLNAME:MASTER%的衣服已被设定为[半裸]了
		PRINTL 
		CALL KOJO_EVENT(101, 1010)
	ELSEIF TALENT:MASTER:压抑 * 3 - TALENT:MASTER:解放 * 2 + TALENT:MASTER:羞涩 * 2 - TALENT:MASTER:无耻 + 2 > CFLAG:MASTER:调教等级 / 2 + ABL:MASTER:欲望
		PRINTFORMW %CALLNAME:MASTER%由于害羞做不到[半裸]
		PRINTL 
		CALL KOJO_EVENT(101, 1012)
	ELSEIF TALENT:MASTER:反抗的 * 3 - TALENT:MASTER:坦率 * 2 + MARK:反抗刻印 - MARK:屈服刻印 + 2 > CFLAG:MASTER:调教等级 / 2 + ABL:MASTER:顺从 + ABL:MASTER:欲望
		PRINTFORMW %CALLNAME:MASTER%由于讨厌不答应[半裸]
		PRINTL 
		CALL KOJO_EVENT(101, 1013)
	ELSE
		CFLAG:MASTER:衣服 = 1
		PRINTFORMW 把%CALLNAME:MASTER%的衣服设定为[半裸]了
		PRINTL 
		CALL KOJO_EVENT(101, 1011)
	ENDIF
	RESTART
ELSEIF RESULT == 3
	IF CFLAG:MASTER:衣服 == 2
		PRINTFORMW %CALLNAME:MASTER%的衣服已被设定为[全裸]了
		PRINTL 
		CALL KOJO_EVENT(101, 1020)
	ELSEIF TALENT:MASTER:压抑 * 3 - TALENT:MASTER:解放 * 2 + TALENT:MASTER:羞涩 * 2 - TALENT:MASTER:无耻 + 5 > CFLAG:MASTER:调教等级 / 2 + ABL:MASTER:欲望
		PRINTFORMW %CALLNAME:MASTER%由于害羞做不到[全裸]
		PRINTL 
		CALL KOJO_EVENT(101, 1022)
	ELSEIF TALENT:MASTER:反抗的 * 3 - TALENT:MASTER:坦率 * 2 + MARK:反抗刻印 - MARK:屈服刻印 + 4 > CFLAG:MASTER:调教等级 / 2 + ABL:MASTER:顺从 + ABL:MASTER:欲望
		PRINTFORMW %CALLNAME:MASTER%由于讨厌不答应[全裸]
		PRINTL 
		CALL KOJO_EVENT(101, 1023)
	ELSE
		CFLAG:MASTER:衣服 = 2
		PRINTFORMW 把%CALLNAME:MASTER%的衣服设定为[全裸]了
		PRINTL 
		CALL KOJO_EVENT(101, 1021)
	ENDIF
	RESTART
ELSEIF RESULT == 51
	IF CFLAG:TARGET:衣服 == 0
		PRINTFORMW %CALLNAME:TARGET%的衣服已被设定为[通常]了
		PRINTL 
		CALL KOJO_EVENT(101, 0)
	ELSE
		CFLAG:TARGET:衣服 = 0
		PRINTFORMW 把%CALLNAME:TARGET%的衣服设定为[通常]了
		PRINTL 
		CALL KOJO_EVENT(101, 1)
	ENDIF
	RESTART
ELSEIF RESULT == 52
	IF CFLAG:TARGET:衣服 == 1
		PRINTFORMW %CALLNAME:TARGET%的衣服已被设定为[半裸]了
		PRINTL 
		CALL KOJO_EVENT(101, 10)
	ELSEIF TALENT:压抑 * 3 - TALENT:解放 * 2 + TALENT:羞涩 * 2 - TALENT:无耻 + 2 > CFLAG:调教等级 / 2 + ABL:欲望
		PRINTFORMW %CALLNAME:TARGET%由于害羞做不到[半裸]
		PRINTL 
		CALL KOJO_EVENT(101, 12)
	ELSE
		CFLAG:TARGET:衣服 = 1
		PRINTFORMW 把%CALLNAME:TARGET%的衣服设定为[半裸]了
		PRINTL 
		CALL KOJO_EVENT(101, 11)
	ENDIF
	RESTART
ELSEIF RESULT == 53
	IF CFLAG:TARGET:衣服 == 2
		PRINTFORMW %CALLNAME:TARGET%的衣服已被设定为[全裸]了
		PRINTL 
		CALL KOJO_EVENT(101, 20)
	ELSEIF TALENT:压抑 * 3 - TALENT:解放 * 2 + TALENT:羞涩 * 2 - TALENT:无耻 + 5 > CFLAG:调教等级 / 2 + ABL:欲望
		PRINTFORMW %CALLNAME:TARGET%由于害羞做不到[全裸]
		PRINTL 
		CALL KOJO_EVENT(101, 22)
	ELSE
		CFLAG:TARGET:衣服 = 2
		PRINTFORMW 把%CALLNAME:TARGET%的衣服设定为[全裸]了
		PRINTL 
		CALL KOJO_EVENT(101, 21)
	ENDIF
	RESTART
ENDIF
RESTART
