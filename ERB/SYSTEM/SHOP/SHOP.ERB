;────────────────────────────────────
;商店菜单的显示
;────────────────────────────────────
@SHOW_SHOP
CALL VERSION_UP

DRAWLINE
PRINTFORML {FLAG:年数+1}年{FLAG:日数+1}日目(%GET_TIME()%) %GET_SEASON()% %GET_DAY()%的行 　主人：%CALLNAME:1%
SIF TARGET >= 0
	PRINTFORML %CALLNAME:TARGET%正在调教%CALLNAME:MASTER%中
SIF ASSI:1 >= 0
	PRINTFORML （助手１：%CALLNAME:(ASSI:1)%）\@ ASSI:2 >= 0 ? （助手２：%CALLNAME:(ASSI:2)%） # \@
HTML_PRINT HTML_BASE("体力", MASTER, 0, 16) + HTML_BASE("气力", MASTER, 1, 16)
SIF TARGET >= 0
	PRINTFORML (%CALLNAME%的魔力　{CFLAG:魔力})
[SKIPSTART]
要らない和思う的で注释アウトしておき增加@/L
IF TARGET >= 0
	PRINTFORML 【依存度　test版】　(%CALLNAME%对%CALLNAME:MASTER%的依存度：{CFLAG:依存度}　%CALLNAME:MASTER%对%CALLNAME:(FLAG:主人)%们的依存度：{CFLAG:MASTER:依存度})
	PRINTL 　限定天数的数值、主人的名字・主人公的性別、尽量制定大致的游戏方针
	PRINTL 　如果能在帖子上报告的话就太好了。是用于结尾分支等的预定值。
ENDIF
[SKIPEND]
DRAWLINE
;调教开始
{
CALL HTMLPRINTL(HTMLAUTOBUTTON("[100] - 调教时间", "100", "调教被开始し增加", , , TARGET >= 0, 
	"[---] - 请决定调教者", "调教被开始做為に是、调教者被決定做必要但あり增加", 0xA0A0A0))
}
;能力显示
CALL HTMLPRINTL(HTMLBUTTON("[101] - 显示能力", "101", "调教者和调教对象的各种経験値等被确认し增加"))
;魔力消费
SIF TARGET >= 0
	CALL HTMLPRINTL(HTMLBUTTON("[102] - 消费魔力", "102", "魔力被使って事项や能力等的取得、新たな调教者的追加被行い增加"))
;调教者切り替え
SIF CHARANUM > 2
	CALL HTMLPRINTL(HTMLBUTTON("[110] - 调教者/助手的切り替え", "110", "现在的调教者/助手被他的调教者に变更し增加"))
;衣服设定
SIF TARGET > 0
	CALL HTMLPRINTL(HTMLBUTTON("[155] - 衣服设定", "155", "调教者和调教对象的衣服被设定し增加"))
;数据ベース
CALL HTMLPRINTL(HTMLBUTTON("[177] - 显示数据", "177", "各种调教数据被确认し增加"))
;配置
CALL HTMLPRINTL(HTMLBUTTON("[178] - 配置", "178", "ゲーム的设定被行い增加"))
;保存
CALL HTMLPRINTL(HTMLBUTTON("[200] - 保存", "200", "现在的数据被保存し增加"))
;加载
CALL HTMLPRINTL(HTMLBUTTON("[300] - 加载", "300", "保存した数据被读み込み增加"))
;特殊调教开关
CALL HTMLPRINTL(HTMLBUTTON("[400] - 特殊调教开关", "400", "可手动关闭一些调教内容"))

;EmueraDEBUG模式启动时
[IF_DEBUG]
{
	CALL HTMLPRINTL(HTMLBUTTON("[777] - DEBUG模式" + (FLAG:DEBUG ? "的终了" # ""), "777", 
		FLAG:DEBUG ? "通常画面に返回" # "バリアント的DEBUG模式被启动し增加"))
}
	IF FLAG:DEBUG
		PRINTFORML \n--DEBUG指令是为了简单地改变状况而制作的、\n以下的指令的异常不在操作保障范围内--\n
		;魔力増加
		SIF TARGET >= 0
			CALL HTMLPRINTL(HTMLBUTTON("[778] - 魔力増加", "778", "调教者的魔力被増やし增加"))
		;転生的儀式
		CALL HTMLPRINTL(HTMLBUTTON("[779] - 転生的儀式", "779", "PLAYER角色的再设定画面へ進み增加"))
		;标志チェック
		CALL HTMLPRINTL(HTMLBUTTON("[780] - 标志チェック", "780", "CFLAG和FLAG的状況被确认し增加"))
		;自动模式
		CALL HTMLPRINTL(HTMLBUTTON("[781] - 自动模式", "781", "调教对象的行动被自动で行う模式的设定画面被呼び出し增加"))
		;素質修正确认
		CALL HTMLPRINTL(HTMLBUTTON("[782] - 素質修正确认", "782", "各角色的指令选择傾向被确认し增加"))
	ENDIF
[ENDIF]

PRINTL 
CALL HTMLPRINTL(HTMLBUTTON("[888] - 口上显示设定", "888", "口上的显示/非显示被设定し增加"))
DRAWLINE


;────────────────────────────────────
;商店菜单
;────────────────────────────────────
@USERSHOP
IF RESULT == 100 && TARGET >= 0
	BEGIN TRAIN
	RETURN 1
ELSEIF RESULT == 101
	CALL SHOW_CHARADATA
ELSEIF RESULT == 102 && TARGET >= 0
	CALL BONUS_GAIN
	RETURN 1
ELSEIF RESULT == 110 && CHARANUM > 2
	CALL CHANGE_TARGET_ASSI
ELSEIF RESULT == 155 && TARGET >= 0
	CALL CLOTHES_SETTING
ELSEIF RESULT == 177
	CALL DATABASE_OUTPUT
ELSEIF RESULT == 178
	CALL CONFIGURE
ELSEIF RESULT == 200
	;SAVEGAME
	;→SYSTEM_DATAMENU.ERB
	CALL DATAMENU("SAVE")
ELSEIF RESULT == 300
	;LOADGAME
	;→SYSTEM_DATAMENU.ERB
	CALL DATAMENU("LOAD")
ELSEIF RESULT == 400
    ;特殊调教开关
    CALL SET_ACT_SWITCH
ELSEIF RESULT == 888
	CALL KOJO_SET
ENDIF
;EmueraDEBUG模式启动时
[IF_DEBUG]
	IF 0
	ELSEIF RESULT == 777
		PRINTFORML 显示DEBUG用的信息\@ FLAG:DEBUG ? 不做 # 做 \@已配置设定
		FLAG:DEBUG = !FLAG:DEBUG ? 1 # 0
		WAIT
	ELSEIF RESULT == 778 && FLAG:DEBUG && TARGET >= 0
		CFLAG:魔力 += 10000
		PRINTFORML %CALLNAME%的魔力+10000
		WAIT
	ELSEIF RESULT == 779 && FLAG:DEBUG
		PRINTW 可重新设定调教对象（主人公）
		CALL MASTER_CUSTOM
		CALL BASE_MASTER_SETUP
	ELSEIF RESULT == 780 && FLAG:DEBUG
		CALL FLAGCHECK
	ELSEIF RESULT == 781 && FLAG:DEBUG
		CALL SET_AUTO
	ELSEIF RESULT == 782 && FLAG:DEBUG
		CALL CHECK_TALENT_CRI
	ENDIF
[ENDIF]


@KOJO_SET
PRINTL 是否显示角色口上？
PRINTL [0] - 显示OFF
PRINTL [1] - 显示ON　※推荐
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0
		PRINTW 不显示角色口上
	CASE 1
		PRINTW 显示角色口上
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
ENDSELECT
FLAG:口上 = RESULT
PRINTL 

;────────────────────────────────────
;调教者/助手变更
;────────────────────────────────────
@CHANGE_TARGET_ASSI
#DIM OLD_T, 3

OLD_T = TARGET, (ASSI:1), (ASSI:2)

$CHANGE_LOOP
PRINTFORML 可以变更调教者/助手
CALL KOJO_COLOR(TARGET)
PRINTBUTTON @"[  0] 调教者 - \@ TARGET >= 0 ? [{TARGET,2}] %CALLNAME:TARGET% # 没有调教者 \@", 0
PRINTL 
RESETCOLOR
CALL KOJO_COLOR(ASSI:1)
PRINTBUTTON @"[  1] 助手1  - \@ ASSI:1 >= 0 ? [{ASSI:1,2}] %CALLNAME:(ASSI:1)% # 没有助手1 \@", 1
PRINTL 
IF CHARANUM > 3
	RESETCOLOR
	CALL KOJO_COLOR(ASSI:2)
	PRINTBUTTON @"[  2] 助手2  - \@ ASSI:2 >= 0 ? [{ASSI:2,2}] %CALLNAME:(ASSI:2)% # 没有助手2 \@", 2
	PRINTL 
ENDIF
IF TARGET < 0
	SETCOLOR 160, 160, 160
	PRINTFORML [---] 请决定调教者
	RESETCOLOR
ELSE
	RESETCOLOR
	PRINTFORML [  9] 变更终了
ENDIF
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0
		CALL CHANGE_TARGET
		GOTO CHANGE_LOOP
	CASE 1
		CALL CHANGE_ASSI(1)
		GOTO CHANGE_LOOP
	CASE 2
		IF CHARANUM > 3
			CALL CHANGE_ASSI(2)
			GOTO CHANGE_LOOP
		ENDIF
	CASE 9
		IF TARGET >= 0
			;助手1但空的场合、助手2被助手1和做
			SIF ASSI:1 == -1
				SWAP ASSI:1, ASSI:2
			CALL CHANGE_TARGET_ASSI_MESSAGE(OLD_T)
			RETURN RESULT
		ENDIF
ENDSELECT
CLEARLINE 1
GOTO INPUT_LOOP

@CHANGE_TARGET_ASSI_MESSAGE(OLD_T)
#DIM LCOUNT
#DIM REF OLD_T, 0
#DIM NEW_T, 3

NEW_T = TARGET, (ASSI:1), (ASSI:2)

;变更但全く行われていない场合
IF OLD_T:0 == NEW_T:0 && OLD_T:1 == NEW_T:1 && OLD_T:2 == NEW_T:2
	;调教者但主人的场合
	IF TALENT:TARGET:主人
		PRINTFORMW %CALLNAME:TARGET%就这样自己调教了%CALLNAME:MASTER%…
	;それ以外
	ELSE
		PRINTFORMW 就这样%CALLNAME:TARGET%调教了%CALLNAME:MASTER%…
	ENDIF
ENDIF
;调教者的变更但行われている场合
IF OLD_T:0 != NEW_T:0
	;口上的為に専用变数に交代元・先的登录番号被入れる
	SHOP_OLDTARGET = OLD_T:0
	SHOP_NEWTARGET = NEW_T:0
	;新调教者カットイン
	PRINTL
	CALL CHARA_CUTIN(CSTR:10, CSTR:11, CSTR:单词)
	PRINTL
	;暂时的にTARGET被旧调教者に戻す
	TARGET = OLD_T:0
	;旧调教者口上呼び出し
	CALL KOJO_EVENT(102)
	;主人→主人以外へ的变更
	IF TARGET >= 0 && TALENT:TARGET:主人
		PRINTFORMW %CALLNAME:TARGET%是%CALLNAME:SHOP_NEWTARGET%に%CALLNAME:MASTER%的调教被委託した…
	;主人以外→主人へ的变更
	ELSEIF TALENT:SHOP_NEWTARGET:主人
		PRINTFORMW %CALLNAME:SHOP_NEWTARGET%决定自己去调教%CALLNAME:MASTER%…
	;主人以外→主人以外へ的变更
	ELSE
		PRINTFORMW \@ TARGET >= 0 ? %CALLNAME:TARGET%代替、 # \@%CALLNAME:SHOP_NEWTARGET%调教了%CALLNAME:MASTER%…
	ENDIF
	;TARGET被新调教者に
	TARGET = NEW_T:0
	;新调教者口上呼び出し
	CALL KOJO_EVENT(102, 1)
ENDIF
;新助手全員被调査
FOR LCOUNT, 1, 3
	;助手LCOUNT但新規任命者的场合
	IF FINDLASTELEMENT(OLD_T, NEW_T:LCOUNT, 1) == -1
		;ASSIに新規任命者被指定
		ASSI = NEW_T:LCOUNT
		;助手任命口上呼び出し
		CALL KOJO_EVENT_ASSI(102)
		CALL VOIDLINE_IF(RESULT)
		;ASSI被元に戻す
		ASSI = ASSI:1
	ENDIF
NEXT

;调教者选择
@CHANGE_TARGET(ARG)
LOCALS = 
LOCALS += "<识別>CHANGE_TARGET"
LOCALS += "<上部>CHANGE_TARGET_SHOW"
LOCALS += "<条件>CHANGE_TARGET_REQU"
LOCALS += "<口上>调教者"
LOCALS += ARG ? "<取消>不可取消" # "<取消>返回"
LOCALS += "<项目>名前/体力・气力/経験値"
CALL SELECT_CHARA_LIST(LOCALS)
SIF RESULT != -1
	CALL SET_TARGET_ASSI(TARGET, 0, RESULT)

@CHANGE_TARGET_SHOW
PRINTFORML 现在 \@ TARGET >= 0 ? [{TARGET,2}] %CALLNAME:TARGET%调教中 # 没有调教者 \@
PRINTL 这回的调教者是谁？

@CHANGE_TARGET_REQU(ARG)
RETURN ARG == 0 ? 0 # 1

;助手选择
@CHANGE_ASSI(ARG)
LOCALS = 
LOCALS += "<识別>CHANGE_TARGET"
LOCALS += @"<上部>CHANGE_ASSI_SHOW_{ARG}"
LOCALS += "<条件>CHANGE_TARGET_REQU"
LOCALS += "<口上>调教者"
LOCALS += "<选择>CHANGE_ASSI_CHOI"
LOCALS += "<取消>返回"
LOCALS += "<项目>名前/体力・气力/経験値"
CALL SELECT_CHARA_LIST(LOCALS)
IF RESULT != -1
	SIF RESULT == -2
		RESULT = -1
	CALL SET_TARGET_ASSI(ASSI, ARG, RESULT)
ENDIF

@CHANGE_ASSI_SHOW_1
CALL CHANGE_ASSI_SHOW(1)
@CHANGE_ASSI_SHOW_2
CALL CHANGE_ASSI_SHOW(2)
@CHANGE_ASSI_SHOW(ARG)
IF ASSI:1 >= 0
	PRINTFORML 现在 [{ASSI:1,3}] %CALLNAME:(ASSI:1)%\@ ASSI:2 >= 0 ? 、[{ASSI:2,3}] %CALLNAME:(ASSI:2)% # \@助手
ELSE
	PRINTL 现在 没有助手
ENDIF
PRINTFORML 这回的助手{ARG}是谁？
PRINTFORML 　[0]「无」※不指定助手{ARG}

@CHANGE_ASSI_CHOI(ARGS)
IF ARGS == "0"
	RESULTS '= "-2"
	RETURN 0
ENDIF
RETURN 1

;调教者/助手的任命に应じて適宜异动・解任被行う
@SET_TARGET_ASSI(LREF, ARG, ARG:1)
#DIM REF LREF,0
;前任者和役割的交换被行う
LOCAL = LREF:ARG
IF ARG:1 != -1
	TARGET = TARGET == ARG:1 ? LOCAL # TARGET
	ASSI:1 = ASSI:1 == ARG:1 ? LOCAL # ASSI:1
	ASSI:2 = ASSI:2 == ARG:1 ? LOCAL # ASSI:2
ENDIF
;役割的任命被行う
LREF:ARG = ARG:1
;ASSI被设定
ASSI = ASSI:1

;────────────────────────────────────
;角色的属性显示
;────────────────────────────────────
@SHOW_CHARADATA(ARG)
LOCALS = 
LOCALS += "<识別>SHOW_CHARADATA"
LOCALS += "<口上>调教者"
LOCALS += "<取消>返回"
LOCALS += "<项目>名前/体力・气力/経験値"
CALL SELECT_CHARA_LIST(LOCALS)
;返回选择时脱出
SIF RESULT < 0
	RETURN 0
LOCAL = RESULT
;属性显示
CALL PRINT_STATUS(RESULT)
;再描画
RESTART

@SHOW_CHARADATA_PAD
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM L_SELECT
#DIM L_CHARA, 4
#DIM LINE
LINE = LINECOUNT
L_SELECT = 0
L_CHARA = MASTER, TARGET, ASSI:1, ASSI:2
$START
CLEARLINE LINECOUNT - LINE

DRAWLINE
FOR LOCAL, 0, 4
	SIF L_CHARA:LOCAL == -1
		CONTINUE
	SIF LOCAL == L_SELECT
		SETCOLOR 0x7070C0
	LOCALS = 
	LOCALS += @" %CALLNAME:LOCAL,16,LEFT% 体力({MAX(BASE:LOCAL:0,0),4}/{MAXBASE:LOCAL:0,4}) "
	LOCALS += @" [LV {CFLAG:LOCAL:0,2} NEXT {CFLAG:LOCAL:必要経験値 - CFLAG:LOCAL:1,6}]"
	LOCALS += AUTO_SPLIT("_调教者_助手１_助手２", "_", LOCAL)
	SIF TALENT:(L_CHARA:LOCAL):主人
		LOCALS += "<主人>"
	PRINTBUTTON LOCALS, UNICODE(10000 + LOCAL)
	RESETCOLOR
	PRINTL 
	SIF LOCAL == 0
		DRAWLINE
NEXT
DRAWLINE
PRINTBUTTON "[" + KEYMARK_B+ "] - 返回", KEY_B
ONEINPUTS
SELECTCASE RESULTS
	CASE KEY_A
		CALL PRINT_STATUS, L_CHARA:L_SELECT
	CASE KEY_B
		RETURN 0
	CASE KEY_UP
		DO 
			SIF --L_SELECT == -1
				L_SELECT = 3
		LOOP L_CHARA:L_SELECT == -1
	CASE KEY_DOWN
		L_SELECT++
		SIF L_CHARA:L_SELECT == -1
			L_SELECT = 0
ENDSELECT
GOTO START
@FLAGCHECK
PRINTL [0] - 返回
PRINTL [1] - FLAG
PRINTL [2] - CFLAG
PRINTL [3] - FLAG的转储
PRINTL [4] - CFLAG的转储
$INPUT_LOOP1
INPUT
SELECTCASE RESULT
	CASE 0
	CASE 1
		VARSIZE FLAG
		LOCAL = RESULT
		PRINTFORML 请输入FLAG番号(0～{LOCAL-1})。
		$INPUT_LOOP2
		INPUT
		IF RESULT < 0 || LOCAL <= RESULT
			CLEARLINE 1
			REUSELASTLINE FLAG的范围是(0～{LOCAL-1})です。
			GOTO INPUT_LOOP2
		ENDIF
		PRINTFORMW FLAG:{RESULT} = {FLAG:RESULT}
		RESTART
	CASE 2
		PRINTFORML 请输入对象角色的登录番号(0～{CHARANUM-1})。
		$INPUT_LOOP3
		INPUT
		IF RESULT < 0 || CHARANUM <= RESULT
			CLEARLINE 1
			REUSELASTLINE 存在しません。
			GOTO INPUT_LOOP3
		ENDIF
		LOCAL = RESULT
		VARSIZE CFLAG
		LOCAL:1 = RESULT
		PRINTFORML 选择了%CALLNAME:LOCAL%。
		PRINTFORML 请输入CFLAG番号(0～{LOCAL:1-1})。
		$INPUT_LOOP4
		INPUT
		IF RESULT < 0 || LOCAL:1 <= RESULT
			CLEARLINE 1
			REUSELASTLINE CFLAG的范围是(0～{LOCAL:1-1})です
			GOTO INPUT_LOOP4
		ENDIF
		PRINTFORMW %CALLNAME:LOCAL%的CFLAG:{RESULT} = {CFLAG:LOCAL:RESULT}
		RESTART
	CASE 3
		PRINTL FLAG一览(值为0的将被省略)
		FOR LOCAL, 0, VARSIZE("FLAG")
			SIF FLAG:LOCAL
				PRINTFORMLC [FLAG:{LOCAL,4}={FLAG:LOCAL,10}]
		NEXT
		WAIT
		RESTART
	CASE 4
		PRINTFORML 请输入对象角色的登录番号(0～{CHARANUM-1})。
		$INPUT_LOOP5
		INPUT
		IF RESULT < 0 || CHARANUM <= RESULT
			CLEARLINE 1
			REUSELASTLINE 存在しません。
			GOTO INPUT_LOOP5
		ENDIF
		PRINTFORML %CALLNAME:RESULT%的CFLAG一览(值为0的将被省略)
		FOR LOCAL, 0, VARSIZE("CFLAG")
			SIF CFLAG:RESULT:LOCAL
				PRINTFORMLC [CFLAG:{LOCAL,4}={CFLAG:RESULT:LOCAL,10}]
		NEXT
		WAIT
		RESTART
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP1
ENDSELECT

@SET_AUTO
#DIM SELECT
#DIM LINE
#DIMS 设置名, 5
#LOCALSSIZE 100
REDRAW 0
SELECT = -1
VARSET LOCALS
LINE = LINECOUNT
FOR LOCAL, 0, 5
	SIF !STRCOUNT(GLOBALS:@"自动模式登录{LOCAL}", "_")
		GLOBALS:@"自动模式登录{LOCAL}" = 登录{LOCAL}_%GLOBALS:@"自动模式登录{LOCAL}"%
	SPLIT GLOBALS:@"自动模式登录{LOCAL}", "_", RESULTS
	设置名:LOCAL = %RESULTS%
NEXT
SPLIT GLOBALS:@"自动模式登录{FLAG:选择设置}", "_", RESULTS
SPLIT RESULTS:1, "/", LOCALS
$START
;削除指令及び异常値的チェック
FOR LOCAL, 0, 100
	SIF LOCALS:LOCAL == ""
		CONTINUE
	SIF GETNUM(TRAINNAME, LOCALS:LOCAL) >= 0 || LOCALS:LOCAL == "随心所欲"
		CONTINUE
	ARRAYREMOVE LOCALS, LOCAL, 1
	LOCAL--
NEXT
;空行的整理
FOR LOCAL, 0, 100 - MATCH(LOCALS, "")
	SIF LOCALS:LOCAL != ""
		CONTINUE
	ARRAYREMOVE LOCALS, LOCAL, 1
	LOCAL--
NEXT
;新規追加及さ受到指令的チェック
FOR LOCAL, 0, 100
	SIF TRAINNAME:LOCAL == ""
		CONTINUE
	;仕大人上通常选择でき时问题但ある的でカットしておく
	SIF TRAINNAME:LOCAL == "随心所欲"
		CONTINUE
	SIF FINDELEMENT(LOCALS, TRAINNAME:LOCAL) >= 0
		CONTINUE
	LOCALS:(100 - MATCH(LOCALS, "")) = %TRAINNAME:LOCAL%
NEXT
CLEARLINE LINECOUNT - LINE
DRAWLINE
PRINTBUTTON @"自动模式<%\@FLAG:自动模式 > 0?ＯＮ#ＯＦＦ\@%>", 1000
IF FLAG:自动模式 > 0
	PRINT 　
	PRINTBUTTON @"经过回合数<{FLAG:自动模式}>", 1001
ENDIF
PRINTL 
DRAWLINE
PRINTFORML <%设置名:(FLAG:选择设置)%>
FOR LOCAL, 0, 100
	SIF LOCALS:LOCAL == ""
		BREAK
	SIF LOCAL == SELECT
		SETCOLOR 0xC07070
	PRINTBUTTON @"%"["+LOCALS:LOCAL+"]",24,LEFT% > ", LOCAL
	SIF LOCAL % 4 == 3
		PRINTL 
	RESETCOLOR
NEXT
PRINTL 
DRAWLINE
PRINTL <设置>
FOR LOCAL, 0, 5
	SIF LOCAL == FLAG:选择设置
		SETCOLOR 0xC07070
	PRINTBUTTON @"%"["+设置名:LOCAL+"]",25,LEFT%", 2000 + LOCAL
	RESETCOLOR
	SIF LOCAL % 3 == 2
		PRINTL 
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
PRINTL <设置操作>
PRINTBUTTON "[名称变更]　", 2010
PRINT 　
PRINTBUTTON "[登录]　　", 2011
PRINT 　
PRINTBUTTON "[设置]", 2012
PRINTL 
DRAWLINE
PRINTL <默认设置>
PRINTBUTTON "[番号顺]　　", 2020
PRINT 　
PRINTBUTTON "[顺从]　　", 2021
PRINT 　
PRINTBUTTON "[反抗的]　", 2022
PRINTL 
DRAWLINE
PRINTL <显示设定>
SIF FLAG:自动模式显示设定 == 0
	SETCOLOR 0xC07070
PRINTBUTTON "[通常]　　", 2030
RESETCOLOR
PRINT 　
SIF FLAG:自动模式显示设定 == 1
	SETCOLOR 0xC07070
PRINTBUTTON "[简略]　　", 2031
PRINT 　
RESETCOLOR
SIF FLAG:自动模式显示设定 == 2
	SETCOLOR 0xC07070
;PRINTBUTTON "[DEBUG非显示]", 2032
RESETCOLOR
PRINTL 
DRAWLINE
PRINTL <优先大满足奖励>
PRINTBUTTON "["+AUTO_SPLIT("平时原样/欺负肛门/惩罚/激烈/撒娇/直到坏掉/想要外出/想要怀孕/要求休息/被抱住/想要侍奉/宠爱/随机", "/", FLAG:优先大满足奖励 % 100)+"]", 3000
PRINTL 
PRINTBUTTON "["+AUTO_SPLIT("平时原样/欺负肛门/惩罚/激烈/撒娇/直到坏掉/想要外出/想要怀孕/要求休息/被抱住/想要侍奉/宠爱/随机", "/", FLAG:优先大满足奖励 / 100)+"]", 3001
PRINTL (备用)
DRAWLINE
PRINTBUTTON "[返回]", 999
INPUT
SELECTCASE RESULT
	CASE 2000 TO 2004
		IF FLAG:选择设置 != RESULT - 2000
			FLAG:选择设置 = RESULT - 2000
			SPLIT GLOBALS:@"自动模式登录{FLAG:选择设置}", "_", RESULTS
			SPLIT RESULTS:1, "/", LOCALS
		ENDIF
	CASE 2010
		PRINTL 请输入名称
		INPUTS
		SIF RESULTS == ""
			RESULTS = 登录{FLAG:选择设置}
		设置名:(FLAG:选择设置) = %RESULTS%
	CASE 2011, 999
		PRINTW 登录现在的设置
		GLOBALS:@"自动模式登录{FLAG:选择设置}" = %设置名:(FLAG:选择设置)%_
		FOR LOCAL, 0, 100 - MATCH(LOCALS, "")
			SIF LOCALS:LOCAL == ""
				BREAK
			GLOBALS:@"自动模式登录{FLAG:选择设置}" += LOCALS:LOCAL
			GLOBALS:@"自动模式登录{FLAG:选择设置}" += "/"
		NEXT
		;末尾的区切り记号被削る
		GLOBALS:@"自动模式登录{FLAG:选择设置}" = %SUBSTRING(GLOBALS:@"自动模式登录{FLAG:选择设置}", 0, STRLENS(GLOBALS:@"自动模式登录{FLAG:选择设置}") - 1)%
		SAVEGLOBAL
		IF RESULT == 999
			CALL AUTO_MODE, 1
			REDRAW 1
			RETURN 0
		ENDIF
	CASE 2012
		SPLIT GLOBALS:@"自动模式登录{FLAG:选择设置}", "_", RESULTS
		SPLIT RESULTS:1, "/", LOCALS
	CASE 2020
		VARSET LOCALS
	CASE 2021
		VARSET LOCALS
		LOCALS += "充满爱意的执行/摆动腰部/高潮！/射进里面！/积极的执行/接受快感/保持心情舒畅/友善的回应/更加强硬/"
		LOCALS += "消极的执行/请求宽恕/感到害羞/弱弱地回应/保持沉默/冷淡地回应/强势的回应/讽刺挖苦/忍受快感/"
		LOCALS += "发出悲鸣/忍受疼痛/开始自慰/讨厌/骂笨手笨脚/无所畏惧的笑/害怕/"
		LOCALS += "不要中出！/拒绝执行/发狂/移除道具"
		SPLIT LOCALS, "/", LOCALS
	CASE 2022
		VARSET LOCALS
		LOCALS += "发狂/移除道具/拒绝执行/不要中出！/"
		LOCALS += "骂笨手笨脚/讨厌/无所畏惧的笑/发出悲鸣/忍受疼痛/开始自慰/害怕/"
		LOCALS += "消极的执行/请求宽恕/感到害羞/弱弱地回应/保持沉默/冷淡地回应/强势的回应/讽刺挖苦/忍受快感/"
		LOCALS += "射进里面！/积极的执行/接受快感/保持心情舒畅/友善的回应/更加强硬/充满爱意的执行/摆动腰部/高潮！"
		SPLIT LOCALS, "/", LOCALS
	CASE 2030, 2031, 2032
		FLAG:自动模式显示设定 = RESULT - 2030
	CASE 3000
		IF FLAG:优先大满足奖励 % 100 == 12
			FLAG:优先大满足奖励 -= 12
		ELSE
			FLAG:优先大满足奖励++
		ENDIF
	CASE 3001
		IF FLAG:优先大满足奖励 / 100 == 12
			FLAG:优先大满足奖励 -= 900
		ELSEIF FLAG:优先大满足奖励 / 100 == 5
			FLAG:优先大满足奖励 += 300
		ELSE
			FLAG:优先大满足奖励 += 100
		ENDIF
		
	CASE 1001
		IF FLAG:自动模式 > 0
			CALL INPUT_MANY, 1, 100
			FLAG:自动模式 = RESULT
		ENDIF
	CASE 1000
		IF !FLAG:自动模式
			FLAG:自动模式 = 1
		ELSE
			FLAG:自动模式 *= -1
		ENDIF
	CASEELSE
		IF SELECT == -1
			SELECT = RESULT
		ELSE
			SWAP LOCALS:SELECT, LOCALS:RESULT
			SELECT = -1
		ENDIF
ENDSELECT
GOTO START


@SET_ACT_SWITCH
#DIM LINE
;ACT封印 用文字菜单，顺序需要和CODES保持一致
#DIMS NAMES = "三角木马,灌肠套装,拳交,肛门拳交,两穴拳交"
;ACT封印 用ACT代码，顺序需要和NAMES保持一致
#DIMS CODES = "67,68,70,71,72"
;ACT封印 记录数组长度
#DIM NAMES_LEN
#LOCALSSIZE 100
REDRAW 0
VARSET LOCALS
LINE = LINECOUNT
$START
;空行的整理
FOR LOCAL, 0, 100 - MATCH(LOCALS, "")
	SIF LOCALS:LOCAL != ""
		CONTINUE
	ARRAYREMOVE LOCALS, LOCAL, 1
	LOCAL--
NEXT
CLEARLINE LINECOUNT - LINE
DRAWLINE
PRINTFORML 特殊调教开关
DRAWLINE
;PRINTFORML 开关参数：%GLOBALS:@"特殊调教开关"%
SPLIT NAMES, ",", RESULTS, NAMES_LEN
FOR LOCAL:2, 0, NAMES_LEN
    LOCALS = %AUTO_SPLIT_INT(GLOBALS:@"特殊调教开关", ",",LOCAL:2)>= 0?"ＯＮ"#"ＯＦＦ"%
	PRINTBUTTON @"%RESULTS:(LOCAL:2)%<%LOCALS%>", 1000 + LOCAL:2
	PRINTL 
NEXT
DRAWLINE
PRINTBUTTON "[返回]", 999
INPUT
SELECTCASE RESULT
    ;按钮参数，预留20
	CASE 1000 TO 1020
		LOCAL = RESULT - 1000
		SPLIT GLOBALS:@"特殊调教开关", ",", LOCALS
		IF LOCALS:LOCAL == "-1"
			CALL SET_ACT_ABLE_PROBABILITY(AUTO_SPLIT_INT(CODES, ",",LOCAL),0)
			LOCALS:LOCAL= 1
		ELSE
			CALL SET_ACT_ABLE_PROBABILITY(AUTO_SPLIT_INT(CODES, ",",LOCAL),100)
			LOCALS:LOCAL = -1
		ENDIF
		;重新生成配置参数
		GLOBALS:@"特殊调教开关" = 
		FOR LOCAL, 0, NAMES_LEN
			GLOBALS:@"特殊调教开关" += LOCALS:LOCAL
			GLOBALS:@"特殊调教开关" += ","
		NEXT
		;删除最末尾多余的符号
		GLOBALS:@"特殊调教开关" = %SUBSTRING(GLOBALS:@"特殊调教开关", 0, STRLENS(GLOBALS:@"特殊调教开关") - 1)%
		SAVEGLOBAL
	CASE 999
	    CALL AUTO_MODE, 1
		REDRAW 1
		RETURN 0
ENDSELECT
GOTO START

@CHECK_TALENT_CRI
#LOCALSIZE 10
#LOCALSSIZE 200
#DIM LINE
#DIM LINENUM, 2
#DIM LCHARANUM
#DIM LMASTER
#DIM LTARGET
#DIM LTEMP
#DIMS LTEMPS

#DIM LVAR, 30
#DIM LSEX_CRI, 30
#DIM LMAIDEN_CRI, 30

#DIM LTALENT_CRI_EX, 1000
#DIM LTALENT_MENU_CRI, 1000, 20
#DIM LTALENT_CRI, 1000, 20, 30

#DIMS LSEX_OPTION
#DIM LFAVOR_OPTION
#DIM LMAIDEN_OPTION
#DIM LFUTANARI_OPTION

LCHARANUM = CHARANUM
LMASTER = MASTER
LTARGET = TARGET
LINENUM = 15, 35

LSEX_OPTION = 性別无視
LMAIDEN_OPTION = 1

VARSET LTALENT_MENU_CRI:0:0
VARSET LTALENT_CRI:0:0:0
FOR LOCAL, 0, VARSIZE("TALENT")
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	ADDVOIDCHARA
	TARGET = CHARANUM - 1
	TALENT:LOCAL = 1
	;变数被初期化
	CALL SET_CRI_VAR
	FOR LOCAL:1, 0, 11
		;休息被是じく
		;奴役是处理不做
		SIF LOCAL:1 == 8 || LOCAL:1 == 9
			CONTINUE
		CALL SET_CRI_VAR, MENU(LOCAL:1), 1
	NEXT
	CALL ACT_MENU_TALENT_T
	VARSET LVAR
	VARSET RESULT
	CALLF VARIABLE_OF_CRI, "GET", "固定累计"
	FOR LOCAL:1, 0, 20
		LTALENT_MENU_CRI:LOCAL:(LOCAL:1) += RESULT:(LOCAL:1) * 2
	NEXT
	VARSET RESULT
	CALLF VARIABLE_OF_CRI, "GET", "变动累计"
	FOR LOCAL:1, 0, 20
		LTALENT_MENU_CRI:LOCAL:(LOCAL:1) += RESULT:(LOCAL:1)
		SIF LTALENT_MENU_CRI:LOCAL:(LOCAL:1)
			LTALENT_CRI_EX:LOCAL = 1
	NEXT
	FOR LOCAL:1, 0, STRCOUNT(MENULIST(), "/") + 1
		CALL RES_CRI_VAR, 1
		CALL ACT_M_INIT(LOCAL:1)
		TRYCCALLFORM ACT_ACTM{LOCAL:1}_TALENT_T
		CATCH
			CONTINUE
		ENDCATCH
		VARSET LVAR
		VARSET RESULT
		CALLF VARIABLE_OF_CRI, "GET", "固定累计"
		FOR LOCAL:2, 0, 30
			LTALENT_CRI:LOCAL:(LOCAL:1):(LOCAL:2) += RESULT:(LOCAL:2) * 2
		NEXT
		CALLF VARIABLE_OF_CRI, "GET", "变动累计"
		FOR LOCAL:2, 0, 30
			LTALENT_CRI:LOCAL:(LOCAL:1):(LOCAL:2) += RESULT:(LOCAL:2)
			SIF LTALENT_CRI:LOCAL:(LOCAL:1):(LOCAL:2)
				LTALENT_CRI_EX:LOCAL = 1
		NEXT
	NEXT
	DELCHARA TARGET
NEXT
TARGET= LTARGET
$START
DRAWLINE
LOCALS = 素質解析/出场人物指定/终了
LOCAL = 0
WHILE AUTO_SPLIT(LOCALS, "/", LOCAL) != ""
	PRINTBUTTON "[" + AUTO_SPLIT(LOCALS, "/", LOCAL) +"]", AUTO_SPLIT(LOCALS, "/", LOCAL) 
	PRINTL 
	LOCAL++
WEND
CALL INPUT_SPLIT, LOCALS
SELECTCASE RESULTS
	CASE "素質解析"
		$START_TALENT
		LOCALS:1 = 
		LOCAL:1 = 0
		FOR LOCAL, 0, VARSIZE("TALENT") + 2
			IF LOCAL == VARSIZE("TALENT") + 1
				LOCALS:1 += "END"
				PRINTBUTTON @"%"[返回]", 16, LEFT%", "END"
			ELSEIF LOCAL == VARSIZE("TALENT")
				LOCALS:1 += "ALL/"
				PRINTBUTTON @"%"[全素質]", 16, LEFT%", "ALL"
			ELSE
				SIF !LTALENT_CRI_EX:LOCAL
					CONTINUE
				LOCALS:1 += TALENTNAME:LOCAL + "/"
				PRINTBUTTON @"%"[" + TALENTNAME:LOCAL +"]", 16, LEFT%", TALENTNAME:LOCAL
			ENDIF
			SIF LOCAL:1 % 6 == 0
				PRINTL 
			LOCAL:1++
		NEXT
		SIF !LINEISEMPTY()
			PRINTL 
		CALL INPUT_SPLIT, LOCALS:1
		SIF RESULTS == "END"
			GOTO START
		FOR LOCAL, 0, VARSIZE("TALENT")
			IF RESULTS == "ALL"
				SIF !LTALENT_CRI_EX:LOCAL
					CONTINUE
			ELSE
				SIF TALENTNAME:LOCAL != RESULTS
					CONTINUE
			ENDIF
			DRAWLINE
			PRINTFORML %TALENTNAME:LOCAL%
			FOR LOCAL:1, 0, 20
				LTEMP = LTALENT_MENU_CRI:LOCAL:(LOCAL:1)
				SIF !LTEMP
					CONTINUE
				PRINTFORM %MENU(LOCAL:1), 24, LEFT%
				IF LTEMP > 0
					SETCOLOR 0x7070C0
				ELSE
					SETCOLOR 0xC07070
				ENDIF
				PRINTFORML {LTEMP,4}
				RESETCOLOR
			NEXT
			FOR LOCAL:1, 0, 20
				FOR LOCAL:2, 0, 30
					LTEMP = LTALENT_CRI:LOCAL:(LOCAL:1):(LOCAL:2)
					SIF !LTEMP
						CONTINUE
					PRINTFORM %GET_ACTABBNAME(ACTM_NUM(LOCAL:1, LOCAL:2)) + "(" + MENU(LOCAL:1) + ")", 24, LEFT%
					IF LTEMP > 0
						SETCOLOR 0x7070C0
					ELSE
						SETCOLOR 0xC07070
					ENDIF
					PRINTFORML {LTEMP,4}
					RESETCOLOR
				NEXT
			NEXT
		NEXT
		WAIT
		GOTO START_TALENT
	CASE "出场人物指定"
		$START_CHARA
		LOCALS:1 = END/把调教者变为扶她/性別无視/调教对象但男/调教对象但女/调教对象但扶她/初体验无視/调教对象但处女/好感少/好感中/好感多
		DRAWLINE
		FOR LOCAL, 1, FLAG:Cha总数 + 1
			LOCALS:1 += "/" + CSVCALLNAME(LOCAL, 0)
			PRINTBUTTON @"%"[" + CSVCALLNAME(LOCAL, 0) +"]", 24, LEFT%", CSVCALLNAME(LOCAL, 0)
			SIF LOCAL % 4 == 0
				PRINTL 
		NEXT
		SIF !LINEISEMPTY()
			PRINTL 
		DRAWLINE
		FOR LOCAL, 0 ,4
			LTEMPS = %AUTO_SPLIT("性別无視/调教对象但男/调教对象但女/调教对象但扶她", "/", LOCAL)%
			SIF LTEMPS == LSEX_OPTION
				SETCOLOR 0x70C070
			PRINTBUTTON @"%"[" + LTEMPS + "]", 24, LEFT%", LTEMPS
			RESETCOLOR
		NEXT
		SIF LFUTANARI_OPTION
			SETCOLOR 0x70C070
		PRINTBUTTON @"%"[把调教者变为扶她]", 24, LEFT%", "把调教者变为扶她"
		RESETCOLOR
		PRINTL 
		DRAWLINE
		FOR LOCAL, 0 ,2
			LTEMPS = %AUTO_SPLIT("初体验无視/调教对象但处女", "/", LOCAL)%
			SIF GETBIT(LMAIDEN_OPTION, LOCAL)
				SETCOLOR 0x70C070
			PRINTBUTTON @"%"[" + LTEMPS + "]", 24, LEFT%", LTEMPS
			RESETCOLOR
		NEXT
		PRINTL 
		FOR LOCAL, 0 ,3
			LTEMPS = %AUTO_SPLIT("好感少/好感中/好感多", "/", LOCAL)%
			SIF LFAVOR_OPTION == LOCAL
				SETCOLOR 0x70C070
			PRINTBUTTON @"%"[" + LTEMPS + "]", 24, LEFT%", LTEMPS
			RESETCOLOR
		NEXT
		PRINTL 
		DRAWLINE
		PRINTBUTTON @"%"[返回]", 24, LEFT%", "END"
		PRINTL 
		CALL INPUT_SPLIT, LOCALS:1
		SIF RESULTS == "END"
			GOTO START
		IF GROUPMATCH(RESULTS, "性別无視", "调教对象但男", "调教对象但女", "调教对象但扶她")
			LSEX_OPTION = %RESULTS%
			GOTO START_CHARA
		ENDIF
		IF GROUPMATCH(RESULTS, "把调教者变为扶她", "初体验无視", "调教对象但处女", "好感少", "好感中", "好感多")
			SIF RESULTS == "把调教者变为扶她"
				INVERTBIT LFUTANARI_OPTION, 0
			SIF RESULTS == "初体验无視"
				INVERTBIT LMAIDEN_OPTION, 0
			SIF RESULTS == "调教对象但处女"
				INVERTBIT LMAIDEN_OPTION, 1
			SIF RESULTS == "好感少"
				LFAVOR_OPTION = 0
			SIF RESULTS == "好感中"
				LFAVOR_OPTION = 1
			SIF RESULTS == "好感多"
				LFAVOR_OPTION = 2
			GOTO START_CHARA
		ENDIF
		FOR LOCAL, 0, FLAG:Cha总数 + 1
			SIF RESULTS != CSVCALLNAME(LOCAL, 0)
				CONTINUE
			ADDCHARA LOCAL
			BREAK
		NEXT
		TARGET = CHARANUM - 1
		ADDVOIDCHARA
		MASTER = CHARANUM - 1
		SIF LSEX_OPTION == "调教对象但男"
			TALENT:MASTER:男人 = 1
		SIF LSEX_OPTION == "调教对象但扶她"
			TALENT:MASTER:扶她 = 1
		SIF LFUTANARI_OPTION
			TALENT:扶她 = 1
		SIF GETBIT(LMAIDEN_OPTION, 1)
			TALENT:MASTER:处女 = 1
		CFLAG:好感 = LFAVOR_OPTION * 1000
		DRAWLINE
		PRINTFORML 　%CALLNAME:TARGET%
		;变数被初期化
		CALL SET_CRI_VAR
		FOR LOCAL:1, 0, 11
			;休息被是じく
			;奴役是处理不做
			SIF LOCAL:1 == 8 || LOCAL:1 == 9
				CONTINUE
			CALL SET_CRI_VAR, MENU(LOCAL:1), 1
		NEXT
		LINE = LINECOUNT
		DRAWLINE
		PRINTFORML 　菜单选择
		DRAWLINE
		VARSET LVAR
		VARSET RESULT
		VARSET LSEX_CRI
		VARSET LMAIDEN_CRI
		IF LSEX_OPTION != "性別无視"
			CALL CHA_CRI_VAR, "性別"
			CALL ACT_MENU_SEX
			CALLF VARIABLE_OF_CRI, "GET", "固定累计"
			FOR LOCAL:1, 0, 30
				LSEX_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
			NEXT
			CALLF VARIABLE_OF_CRI, "GET", "变动累计"
			FOR LOCAL:1, 0, 30
				LSEX_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
			NEXT
		ENDIF
		IF !GETBIT(LMAIDEN_OPTION, 0)
			VARSET RESULT
			CALL CHA_CRI_VAR, "调教者但处女"
			CALL ACT_MENU_MAIDEN_T
			CALLF VARIABLE_OF_CRI, "GET", "固定累计"
			FOR LOCAL:1, 0, 30
				LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
			NEXT
			CALLF VARIABLE_OF_CRI, "GET", "变动累计"
			FOR LOCAL:1, 0, 30
				LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
			NEXT
			VARSET RESULT
			CALL CHA_CRI_VAR, "调教对象但处女"
			CALL ACT_MENU_MAIDEN_M
			CALLF VARIABLE_OF_CRI, "GET", "固定累计"
			FOR LOCAL:1, 0, 30
				LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
			NEXT
			CALLF VARIABLE_OF_CRI, "GET", "变动累计"
			FOR LOCAL:1, 0, 30
				LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
			NEXT
		ENDIF
		CALL CHA_CRI_VAR, "素質"
		VARSET RESULT
		CALL ACT_MENU_TALENT_T
		CALLF VARIABLE_OF_CRI, "GET", "固定累计"
		FOR LOCAL:1, 0, 30
			LVAR:(LOCAL:1) += RESULT:(LOCAL:1) * 2
		NEXT
		CALLF VARIABLE_OF_CRI, "GET", "变动累计"
		FOR LOCAL:1, 0, 30
			LVAR:(LOCAL:1) += RESULT:(LOCAL:1)
		NEXT
		FOR LOCAL:1, 0, STRCOUNT(MENULIST(), "/") + 1
			SETCOLOR 0xC07070
			PRINTFORM %UNICODE(0x25AE) * ABS(MIN(LVAR:(LOCAL:1), 0)),50,RIGHT%
			RESETCOLOR
			PRINTFORM  %MENU(LOCAL:1)%({LVAR:(LOCAL:1), 4}) 
			SETCOLOR 0x7070C0
			PRINTFORM %UNICODE(0x25AE) * MAX(LVAR:(LOCAL:1), 0),50,LEFT%
			PRINTFORML 
		NEXT
		WHILE LINECOUNT < LINE + LINENUM
			PRINTFORML 
		WEND
		RESETCOLOR
		LOCAL:3 = 0
		FOR LOCAL:1, 0, VARSIZE("TALENT")
			SIF !TALENT:(LOCAL:1)
				CONTINUE
			LTEMPS = 
			FOR LOCAL:2, 0, STRCOUNT(MENULIST(), "/") + 1
				IF LTALENT_MENU_CRI:(LOCAL:1):(LOCAL:2)
					SIF LTEMPS != ""
						LTEMPS += "/"
					LTEMPS += MENU(LOCAL:2) + "_" + TOSTR(LTALENT_MENU_CRI:(LOCAL:1):(LOCAL:2))
				ENDIF
			NEXT
			IF LTEMPS != ""
				SIF LOCAL:3++ == 0
					DRAWLINE
				PRINTFORM %TALENTNAME:(LOCAL:1), 16, LEFT%
				SPLIT LTEMPS, "/", LOCALS
				FOR LOCAL:2, 0, RESULT
					LTEMP = TOINT(AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 1))
					SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
					PRINTFORM <%AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 0)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
					RESETCOLOR
				NEXT
				PRINTL 
			ENDIF
		NEXT
		IF MATCH(LMAIDEN_CRI, 0) != VARSIZE("LMAIDEN_CRI")
			SIF LOCAL:3++ == 0
				DRAWLINE
			PRINTFORM %"初体验修正", 16, LEFT%
			FOR LOCAL:2, 0, 30
				SIF !LMAIDEN_CRI:(LOCAL:2)
					CONTINUE
				LTEMP = LMAIDEN_CRI:(LOCAL:2)
				SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
				PRINTFORM <%MENU(LOCAL:2)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
				RESETCOLOR
			NEXT
			PRINTL 
		ENDIF
		IF MATCH(LSEX_CRI, 0) != VARSIZE("LSEX_CRI")
			SIF LOCAL:3++ == 0
				DRAWLINE
			PRINTFORM %"性別修正", 16, LEFT%
			FOR LOCAL:2, 0, 30
				SIF !LSEX_CRI:(LOCAL:2)
					CONTINUE
				LTEMP = LSEX_CRI:(LOCAL:2)
				SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
				PRINTFORM <%MENU(LOCAL:2)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
				RESETCOLOR
			NEXT
			PRINTL 
		ENDIF
		LOCAL:3 = 0
		FOR LOCAL:1, 0, STRCOUNT(MENULIST(), "/") + 1
			LTEMPS = 
			FOR LOCAL:2, 0, VARSIZE("TALENT")
				SIF !TALENT:(LOCAL:2)
					CONTINUE
				LTEMP = LTALENT_MENU_CRI:(LOCAL:2):(LOCAL:1)
				SIF !LTEMP
					CONTINUE
				LTEMPS += "/" + TALENTNAME:(LOCAL:2) + "_" + TOSTR(LTEMP)
			NEXT
			SIF LMAIDEN_CRI:(LOCAL:1)
				LTEMPS += "/" + "初体验修正" + "_" + TOSTR(LMAIDEN_CRI:(LOCAL:1))
			SIF LSEX_CRI:(LOCAL:1)
				LTEMPS += "/" + "性別修正" + "_" + TOSTR(LSEX_CRI:(LOCAL:1))
			SIF LTEMPS == ""
				CONTINUE
			SIF LOCAL:3++ == 0
				DRAWLINE
			PRINTFORM %MENU(LOCAL:1), 16, LEFT%
			SPLIT LTEMPS, "/", LOCALS
			FOR LOCAL:2, 1, RESULT
				LTEMP = TOINT(AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 1))
				SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
				PRINTFORM <%AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 0)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
				RESETCOLOR
			NEXT
			PRINTL 
		NEXT
		WHILE LINECOUNT < LINE + LINENUM:1
			PRINTFORML 
		WEND
		WAIT
		FOR LOCAL, 0, STRCOUNT(MENULIST(), "/") + 1
			CALL RES_CRI_VAR, 1
			CALL ACT_M_INIT(LOCAL)
			VARSET LVAR
			VARSET LSEX_CRI
			VARSET LMAIDEN_CRI
			VARSET RESULT
			IF !GETBIT(LMAIDEN_OPTION, 0)
				CALL CHA_CRI_VAR, "调教对象但处女"
				TRYCCALLFORM ACT_ACTM{LOCAL}_MAIDEN_M
					CALLF VARIABLE_OF_CRI, "GET", "固定累计"
					FOR LOCAL:1, 0, 30
						LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
					NEXT
					CALLF VARIABLE_OF_CRI, "GET", "变动累计"
					FOR LOCAL:1, 0, 30
						LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
					NEXT
				CATCH
				ENDCATCH
				CALL CHA_CRI_VAR, "调教者但处女"
				TRYCCALLFORM ACT_ACTM{LOCAL}_MAIDEN_T
					CALLF VARIABLE_OF_CRI, "GET", "固定累计"
					FOR LOCAL:1, 0, 30
						LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
					NEXT
					CALLF VARIABLE_OF_CRI, "GET", "变动累计"
					FOR LOCAL:1, 0, 30
						LMAIDEN_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
					NEXT
				CATCH
				ENDCATCH
			ENDIF
			IF LSEX_OPTION != "性別无視"
				CALL CHA_CRI_VAR, "性別"
				TRYCCALLFORM ACT_ACTM{LOCAL}_GENDER_T
					CALLF VARIABLE_OF_CRI, "GET", "固定累计"
					FOR LOCAL:1, 0, 30
						LSEX_CRI:(LOCAL:1) += RESULT:(LOCAL:1) * 2
					NEXT
					CALLF VARIABLE_OF_CRI, "GET", "变动累计"
					FOR LOCAL:1, 0, 30
						LSEX_CRI:(LOCAL:1) += RESULT:(LOCAL:1)
					NEXT
				CATCH
				ENDCATCH
			ENDIF
			CALL CHA_CRI_VAR, "素質"
			TRYCCALLFORM ACT_ACTM{LOCAL}_TALENT_T
			CATCH
			ENDCATCH
			CALLF VARIABLE_OF_CRI, "GET", "固定累计"
			FOR LOCAL:1, 0, 30
				LVAR:(LOCAL:1) += RESULT:(LOCAL:1) * 2
			NEXT
			CALLF VARIABLE_OF_CRI, "GET", "变动累计"
			FOR LOCAL:1, 0, 30
				LVAR:(LOCAL:1) += RESULT:(LOCAL:1)
			NEXT
			LINE = LINECOUNT
			DRAWLINE
			PRINTFORML 　%MENU(LOCAL)%
			DRAWLINE
			FOR LOCAL:1, 0, ACTM_NUM(LOCAL)
				SETCOLOR 0xC07070
				PRINTFORM %UNICODE(0x25AE) * ABS(MIN(LVAR:(LOCAL:1), 0)),50,RIGHT%
				RESETCOLOR
				PRINTFORM  %GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:1))%({LVAR:(LOCAL:1), 4}) 
				SETCOLOR 0x7070C0
				PRINTFORM %UNICODE(0x25AE) * MAX(LVAR:(LOCAL:1), 0),50,LEFT%
				PRINTFORML 
			NEXT
			WHILE LINECOUNT < LINE + LINENUM
				PRINTFORML 
			WEND
			RESETCOLOR
			LOCAL:3 = 0
			FOR LOCAL:1, 0, VARSIZE("TALENT")
				SIF !TALENT:(LOCAL:1)
					CONTINUE
				LTEMPS = 
				FOR LOCAL:2, 0, ACTM_NUM(LOCAL)
					IF LTALENT_CRI:(LOCAL:1):LOCAL:(LOCAL:2)
						SIF LTEMPS != ""
							LTEMPS += "/"
						LTEMPS += GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:2)) + "_" + TOSTR(LTALENT_CRI:(LOCAL:1):LOCAL:(LOCAL:2))
					ENDIF
				NEXT
				IF LTEMPS != ""
					SIF LOCAL:3++ == 0
						DRAWLINE
					PRINTFORM %TALENTNAME:(LOCAL:1), 16, LEFT%
					SPLIT LTEMPS, "/", LOCALS
					FOR LOCAL:2, 0, RESULT
						LTEMP = TOINT(AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 1))
						SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
						PRINTFORM <%AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 0)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
						RESETCOLOR
					NEXT
					PRINTL 
				ENDIF
			NEXT
			IF MATCH(LMAIDEN_CRI, 0) != VARSIZE("LMAIDEN_CRI")
				SIF LOCAL:3++ == 0
					DRAWLINE
				PRINTFORM %"初体验修正", 16, LEFT%
				FOR LOCAL:2, 0, 30
					SIF !LMAIDEN_CRI:(LOCAL:2)
						CONTINUE
					LTEMP = LMAIDEN_CRI:(LOCAL:2)
					SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
					PRINTFORM <%GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:2))% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
					RESETCOLOR
				NEXT
				PRINTL 
			ENDIF
			IF MATCH(LSEX_CRI, 0) != VARSIZE("LSEX_CRI")
				SIF LOCAL:3++ == 0
					DRAWLINE
				PRINTFORM %"性別修正", 16, LEFT%
				FOR LOCAL:2, 0, 30
					SIF !LSEX_CRI:(LOCAL:2)
						CONTINUE
					LTEMP = LSEX_CRI:(LOCAL:2)
					SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
					PRINTFORM <%GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:2))% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
					RESETCOLOR
				NEXT
				PRINTL 
			ENDIF
			DRAWLINE
			FOR LOCAL:1, 0, ACTM_NUM(LOCAL)
				LTEMPS = 
				FOR LOCAL:2, 0, VARSIZE("TALENT")
					SIF !TALENT:(LOCAL:2)
						CONTINUE
					LTEMP = LTALENT_CRI:(LOCAL:2):LOCAL:(LOCAL:1)
					SIF !LTEMP
						CONTINUE
					LTEMPS += "/" + TALENTNAME:(LOCAL:2) + "_" + TOSTR(LTEMP)
				NEXT
				SIF LMAIDEN_CRI:(LOCAL:1)
					LTEMPS += "/" + "初体验修正" + "_" + TOSTR(LMAIDEN_CRI:(LOCAL:1))
				SIF LSEX_CRI:(LOCAL:1)
					LTEMPS += "/" + "性別修正" + "_" + TOSTR(LSEX_CRI:(LOCAL:1))
				SIF LTEMPS == ""
					CONTINUE
				PRINTFORM %GET_ACTABBNAME(ACTM_NUM(LOCAL, LOCAL:1)), 16, LEFT%
				SPLIT LTEMPS, "/", LOCALS
				FOR LOCAL:2, 1, RESULT
					LTEMP = TOINT(AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 1))
					SETCOLOR LTEMP > 0 ? 0x7070C0 # 0xC07070
					PRINTFORM <%AUTO_SPLIT(LOCALS:(LOCAL:2), "_", 0)% \@ LTEMP > 0 ? + # -\@{ABS(LTEMP)}> 
					RESETCOLOR
				NEXT
				PRINTL 
			NEXT
			WHILE LINECOUNT < LINE + LINENUM:1
				PRINTFORML 
			WEND
			WAIT
		NEXT
		WHILE CHARANUM > LCHARANUM
			DELCHARA CHARANUM -  1
		WEND
		GOTO START_CHARA
	CASE "终了"
		TARGET = LTARGET
		MASTER = LMASTER
		WHILE CHARANUM > LCHARANUM
			DELCHARA CHARANUM -  1
		WEND
		RETURN 1
ENDSELECT
;────────────────────────────────────
;保存インフォ		→SYSTEM_DATAMENU.ERBにて代替
;────────────────────────────────────
;@SAVEINFO
;PUTFORM {DAY+1}日目%GET_TIME()% \@ TARGET >= 0 ? %CALLNAME:TARGET%但%CALLNAME:MASTER%被调教中 # \@


;────────────────────────────────────
;バージョン上升
;────────────────────────────────────
@VERSION_UP

