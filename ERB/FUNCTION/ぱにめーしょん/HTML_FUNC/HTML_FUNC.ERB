;===============================================================================
;
;	HTML_PRINT相关関数群
;
;===============================================================================
;	例：LOCALS = %HTML_ADDBOLDTAG()%
;		LOCALS = %HTML_ADDFONTTAG(LOCALS,0xFFC0C0)%
;		LOCALS = %HTML_ADDBUTTONTAG(LOCALS,"UNKO")%
;		LOCALS = %HTML_SETTAGTEXT("うんこうんこ",LOCALS)%
;		→ <button value = 'UNKO'><font color = '#ffc0c0' ><b>うんこうんこ</b></font></button>
;-------------------------------------------------------------------------------
;
;	■变数使い想要！和かいちいち手打ち但めんどくさい俺み想要な人向け
;
;	■本文置换用【HTML】是HTML_FUNC.ERHにて、HTML_SUBSTITUTEで
;	　CONST指定してあん的で、気に食わない那么各自で置き换えましょう
;
;	■複数回やっちゃったらそ的分ネストされちゃうよ → <button><button></button></button>
;	　どういう不具合但起きるか是知らないし、回避处理是STRFIND但重い的でやらないよ
;
;	■当然ライセンスフリー
;
;===============================================================================


;-------------------------------------------------------------------------------
;	HTML_TOTAGTEXT(式中関数)
;-------------------------------------------------------------------------------
;	例：LOCALS:0 = <button value = 'hoge'><font color = '#8080c0'>本文だよー</font></button>
;		LOCALS:1 = %HTML_TOTAGTEXT(LOCALS:0)%
;		→  <button value = 'hoge'><font color = '#8080c0'>【HTML】</font></button>
;-------------------------------------------------------------------------------
;	基本命令的 HTML_TOPLAINTEXT 的逆、タグ部分只被取得し、无い那么何も返さない
;	本文只差し替えたり做用。还有レイヤ和か。
;===============================================================================
@HTML_TOTAGTEXT( ARGS )
#FUNCTIONS
#LOCALSSIZE 1
LOCALS '= REPLACE( ARGS, ESCAPE(HTML_TOPLAINTEXT(ARGS)), HTML_SUBSTITUTE)
IF LOCALS != HTML_SUBSTITUTE
	RETURNF LOCALS
ELSE
	RETURNF
ENDIF


;-------------------------------------------------------------------------------
;	HTML_SETTAGTEXT(式中関数)
;-------------------------------------------------------------------------------
;	例：LOCALS:0 = <button value = 'hoge'><font color = '#8080c0'>【HTML】</font></button>
;		LOCALS:1 = %HTML_SETTAGTEXT("本文だよー",LOCALS:0)%
;		→  <button value = 'hoge'><font color = '#8080c0'>本文だよー</font></button>
;-------------------------------------------------------------------------------
;	HTML_TOTAGTEXTで避けたタグ部分被差し戻す
;===============================================================================
@HTML_SETTAGTEXT( ARGS , ARGS:1 )
#FUNCTIONS
RETURNF REPLACE( ARGS:1, HTML_SUBSTITUTE, ARGS )


;-------------------------------------------------------------------------------
;	HTML_SETTAG(式中関数)
;-------------------------------------------------------------------------------
;	例：LOCALS:0 = <button value = 'hoge'><font color = '#8080c0'>【HTML】</font></button>
;		LOCALS:1 = %HTML_SETTAGTEXT(LOCALS:0,"本文だよー")%
;		→  <button value = 'hoge'><font color = '#8080c0'>本文だよー</font></button>
;-------------------------------------------------------------------------------
;	HTML_TOTAGTEXTで避けたタグ部分被差し戻す
;===============================================================================
@HTML_SETTAG( ARGS , ARGS:1 )
#FUNCTIONS
RETURNF REPLACE( ARGS, HTML_SUBSTITUTE, ARGS:1 )

;-------------------------------------------------------------------------------
;	HTML_GETTAG(式中関数)
;-------------------------------------------------------------------------------
;	例：LOCALS:0 = <button value = 'hoge'><font color = '#8080c0'>【HTML】</font></button>
;		LOCALS:1 = %HTML_GETTAG(LOCALS:0,"color")%
;		→  0x8080c0
;
;	例：LOCALS:0 = <button value = 'hoge'><font color = '#8080c0'>【HTML】</font></button>
;		LOCALS:1 = %HTML_GETTAG(LOCALS:0,"BUTTON")%
;		→  hoge
;-------------------------------------------------------------------------------
;	・HTML_PRINT的仕大人によって格納されている字符串から
;	　align, color, bcolor, face, button的各VALUE値被取得做
;	　commentで<!--  -->的中身も取得できる
;
;	・注释タグ的取得的仕大人(comment)
;	　構文上的(ホワイトスペース(意味的ない空白)な的か、
;	　字符串变数内部的意味的ある半角スペースな的か被ERB上から判断做こ和但できないため
;	　取得した字符串から半角スペース是取り除かれ增加
;	　( <!--HOGE--> <!-- HOGE --> <!-- H O G E  -->是すべてHOGE和帰る )
;
;	・→变更　(旧仕大人：COLOR和BCOLOR的二つ是、#ZZZZZZ → 0xZZZZZZに变换做ため、直でTOINTに投げ込める)
;	　【第三引数ARG但1的场合に限り】 #ZZZZZZ → 0xZZZZZZに变换被試みる
;	　COLORBYNAME指定的可能性但あるこ和被すっかりうっかり无視してた
;
;	・かなり力技な实装してるため、速度但大事なも的に是ちょい厳しい
;	　STRFIND使うしか无い的にSTRFIND重いねん・・・
;	　一应上で書いた多重ネスト回避处理是これ被使えば可能
;===============================================================================
@HTML_GETTAG( ARGS = "＠", ARGS:1 = "＠" , ARG )
#FUNCTIONS
#LOCALSIZE 5
#LOCALSSIZE 20
#DIMS TAG,1
#DIMS VALUE,1
#DIM SPLITNUM,1
VARSET LOCAL
; 引数是大字符でも小字符でもよい但、内部是小字符で回ってるため小字符推荐
TAG '= TOLOWER(ARGS:1)
VALUE =
; 引数被それぞれ的仕大人和あわせる
SELECTCASE TAG
	; ネストでもなければVALUE型でもない独特すぎる注释タグ是専用处理
	CASE "comment","flag"
		LOCAL:0 = STRFINDU( ARGS, ESCAPE("<!--") )
		IF LOCAL:0 != -1
			LOCAL:1 = STRFINDU( ARGS, ESCAPE("-->") )
			IF LOCAL:1 != -1
				LOCAL:0 += 4
				RETURNF REPLACE( SUBSTRINGU( ARGS , LOCAL:0 , (LOCAL:1 - LOCAL:0) ) , " ", "" )
			ENDIF
		ENDIF
		RETURNF
	CASE "align"
		TAG   = <p
		VALUE = align
	CASE "color"
		TAG   = <font
		VALUE = color
		LOCAL:4 = 1
	CASE "bcolor"
		TAG   = <font
		VALUE = bcolor
		LOCAL:4 = 1
	CASE "face"
		TAG   = <font
		VALUE = face
	CASE "button"
		TAG   = <button
		VALUE = value
	CASE "tooltip","title"
		TAG   = <button
		VALUE = title
	CASE "＠"
		SPLIT ARGS, ">", LOCALS, SPLITNUM
		RETURNF HTML_TOPLAINTEXT(ARGS)
	CASEELSE
		THROW %ARGS:1%但异常です
ENDSELECT
; タグ被ばらばらにSPLIT SPLIT再使用可能
SIF ARGS != "＠"
	SPLIT ARGS, ">", LOCALS, SPLITNUM
; SPLITした和ころから上で指定さ受到TAG被探す
LOCAL = FINDELEMENT( LOCALS ,ESCAPE(TAG) , 0 , SPLITNUM + 1 )
; 见つかった
IF LOCAL != -1
	; 上で指定さ受到VALUE被探す
	LOCAL:1 = STRFINDU( LOCALS:LOCAL , ESCAPE(VALUE) , 0)
	; 见つかった
	IF LOCAL:1 != -1
		; VALUE的1つ次的 ' 的字符位置和、VALUE的2つ次的 ' (つまりネスト)直到的字符位置被探す
		LOCAL:2 = STRFINDU( LOCALS:LOCAL , "'" , LOCAL:1 ) + 1
		LOCAL:3 = STRFINDU( LOCALS:LOCAL , "'" , LOCAL:2 + 1 ) - LOCAL:2
			; 见つからないわきゃ无い和思う但
			SIF LOCAL:2 == -1 || LOCAL:3 == -1
				THROW %ARGS%的ネスト但异常です
		; 前后カットで必要な部分被抽出
		VALUE '= SUBSTRINGU( LOCALS:LOCAL , LOCAL:2 , LOCAL:3 )
		; 16進数だよ标志但ある那么#被0xに变换做
		IF LOCAL:4 == 1 && ARG == 1
			RETURNF REPLACE( VALUE , "#" , "0x" )
		ELSE
			RETURNF VALUE
		ENDIF
	ENDIF
ENDIF
RETURNF

;-------------------------------------------------------------------------------
;	HTML_DELTAG(式中関数)
;-------------------------------------------------------------------------------
;	例：LOCALS:0 = <button value = 'hoge'><font color = '#8080c0'>【HTML】</font></button>
;		LOCALS:1 = %HTML_DELTAG(LOCALS:0,"color")%
;		→  0x8080c0
;
;	例：LOCALS:0 = <button value = 'hoge'><font color = '#8080c0'>【HTML】</font></button>
;		LOCALS:1 = %HTML_GETTAG(LOCALS:0,"BUTTON")%
;		→  hoge
;-------------------------------------------------------------------------------
@HTML_DELTAG( ARGS = HTML_SUBSTITUTE, ARGS:1 )
#FUNCTIONS
#LOCALSIZE 5
#LOCALSSIZE 3
VARSET LOCAL
VARSET LOCALS
; 引数是大字符でも小字符でもよい但、内部是小字符で回ってるため小字符推荐
LOCALS '= ARGS
ARGS:1 '= TOLOWER(ARGS:1)
; 引数被それぞれ的仕大人和あわせる
; 是<p align='～'><nobr><br><button value='～'><font face='～' color='#～' bcolor='#～'><b><i><u><s>です
SELECTCASE ARGS:1
	CASE "p"
		LOCALS:1 = <p
		LOCALS:2 = </p>
	CASE "nobr"
		LOCALS:1 = <nobr
		LOCALS:2 = </nobr>
	CASE "br"
		LOCALS:1 = <br
		LOCALS:2 =
	CASE "button"
		LOCALS:1 = <button
		LOCALS:2 = </button>
	CASE "font"
		LOCALS:1 = <font
		LOCALS:2 = </font>
	CASE "comment","flag"
		LOCALS:1 '= "<!-- "
		LOCALS:2 '= " -->"
	CASE "b","bold"
		LOCALS:1 = <b
		LOCALS:2 = </b>
	CASE "i","italic"
		LOCALS:1 = <i
		LOCALS:2 = </i>
	CASE "u","under","underline"
		LOCALS:1 = <u
		LOCALS:2 = </u>
	CASE "s","strike"
		LOCALS:1 = <s
		LOCALS:2 = </s>
	CASEELSE
		THROW %ARGS:1%但异常です
ENDSELECT
; "<x"被探す
LOCAL:1 = STRFINDU( LOCALS, LOCALS:1 , 0 )
; 见つかった
IF LOCAL:1 != -1
	; <x直后的">"被探す
	LOCAL:2 = STRFINDU( LOCALS , ">" , LOCAL:1 )
	; 见つかった
	IF LOCAL:2 != -1
		LOCALS '= SUBSTRINGU( LOCALS,  0, LOCAL:1 ) + SUBSTRINGU( LOCALS, LOCAL:2 + 1 )
		; </xxx>被削除
		LOCALS '= REPLACE( ESCAPE(LOCALS), ESCAPE(LOCALS:2), "" )
	ENDIF
ENDIF
RETURNF LOCALS


;-------------------------------------------------------------------------------
;	HTML_ADDFONTTAG(式中関数)
;-------------------------------------------------------------------------------
;	例：LOCALS:0 = %HTML_ADDFONTTAG("わ是ー",0xFF8080)%
;		→  <font color = '#FF8080'>わ是ー</font>
;-------------------------------------------------------------------------------
;	ARGSにfontタグ被ネスト侍奉
;	引数是本文, 色(int), マウスオーバー色(int), フォント指定(str)
;===============================================================================
@HTML_ADDFONTTAG( ARGS = HTML_SUBSTITUTE , COLOR , BCOLOR , FACE )
#FUNCTIONS
#DIMS COLOR,1
#DIMS BCOLOR,1
#DIMS FACE,1
#LOCALSIZE 1
#LOCALSSIZE 1
LOCALS =
; color = '#c08080'和か的色设定
IF COLOR != ""
	IF ISNUMERIC(COLOR) && INRANGE( TOINT(COLOR),0,0xFFFFFF )
		LOCALS += "color = '#" + CONVERT(TOINT(COLOR),16) + "' "
	ELSE
		LOCALS += "color = '" + COLOR + "' "
	ENDIF
ENDIF
; bcolor = '#ffff00'和か的色设定
IF BCOLOR != ""
	IF ISNUMERIC(BCOLOR) && INRANGE( TOINT(BCOLOR),0,0xFFFFFF )
		LOCALS += "bcolor = '#" + CONVERT(TOINT(BCOLOR),16) + "' "
	ELSE
		LOCALS += "bcolor = '" + BCOLOR + "' "
	ENDIF
ENDIF
; face = 'ＭＳ 明朝'和か的フォント设定
IF FACE != ""
	SIF CHKFONT(FACE) == 1
		LOCALS += "face = '" + FACE + "' "
ENDIF
; ここ直到来てfontタグに何か追加做も的但あった
IF LOCALS != ""
	RETURNF "<font " + LOCALS + ">" + ARGS + "</font>"
ELSE
	RETURNF ARGS
ENDIF

;-------------------------------------------------------------------------------
;	HTML_ADDBUTTONTAG(式中関数)
;-------------------------------------------------------------------------------
;	例：LOCALS:0 = %HTML_ADDBUTTONTAG("わ是ー","HOGE")%
;		→  <button value = 'HOGE'>わ是ー</button>
;-------------------------------------------------------------------------------
;	ARGSにbuttonタグ被ネスト侍奉
;	引数是本文, 按键ラベル(str)
;===============================================================================
@HTML_ADDBUTTONTAG(ARGS = HTML_SUBSTITUTE , ARGS:1 , ARGS:2)
#FUNCTIONS
#DIMS VALUE,1
#DIMS TITLE,1
VARSET LOCALS
SIF ARGS:1 != ""
	LOCALS += "value = '" + ARGS:1 + "' "
SIF ARGS:2 != ""
	LOCALS += "title = '" + ARGS:2 + "' "
IF LOCALS != ""
	RETURNF "<button " + LOCALS + ">" + ARGS + "</button>"
ELSE
	RETURNF ARGS
ENDIF


;-------------------------------------------------------------------------------
;	HTML_ADDCOMMENTTAG(式中関数)
;-------------------------------------------------------------------------------
;	例：LOCALS:0 = %HTML_ADDCOMMENTTAG("わ是ー","HOGE")%
;		→  わ是ー<!-- HOGE -->
;
;	例：LOCALS:0 = わ是ー<!-- HOGE -->
;		LOCALS:0 = %HTML_ADDCOMMENTTAG(LOCALS:0,"HAGE")%
;		→  わ是ー<!-- HOGE/HAGE -->

;-------------------------------------------------------------------------------
;	ARGSに<!--ARGS:1-->被付加做
;	すでに注释タグ但存在している场合ARGS:2被区切り记号和して(初期値"/")
;	既存的も的に追加做形で动作做
;	(中身的分割なんか被SPLITでやるこ和被想定して的仕大人)
;===============================================================================
@HTML_ADDCOMMENTTAG(ARGS = HTML_SUBSTITUTE , ARGS:1 , ARGS:2 = "/")
#FUNCTIONS
#LOCALSSIZE 2
VARSET LOCALS
LOCALS:1 '= HTML_GETTAG(ARGS,"COMMENT")
IF LOCALS:1 != ""
	LOCALS:1 += ARGS:2 + ARGS:1
	LOCALS:0 '= HTML_DELTAG(ARGS,"COMMENT")
ELSE
	LOCALS:1 '= ARGS:1
	LOCALS:0 '= ARGS
ENDIF
RETURNF LOCALS:0 + "<!-- " + LOCALS:1 + " -->"

;-------------------------------------------------------------------------------
;	HTML_ADDOTHERTAG(式中関数)
;-------------------------------------------------------------------------------
;	例：LOCALS:0 = %HTML_ADDOTHERTAG("わ是ー","b")%
;		→  <b>わ是ー</b>
;-------------------------------------------------------------------------------
;	见たまんまシリーズ被一つでやる
;===============================================================================
@HTML_ADDOTHERTAG( ARGS = HTML_SUBSTITUTE, ARGS:1 )
#FUNCTIONS
#LOCALSSIZE 2
VARSET LOCALS
ARGS:1 '= TOLOWER(ARGS:1)
SELECTCASE ARGS:1
	CASE "nobr"
		LOCALS:0 = <nobr>
		LOCALS:1 = </nobr>
	CASE "br"
		LOCALS:0 = <br>
		LOCALS:1 =
	CASE "b","bold"
		LOCALS:0  = <b>
		LOCALS:1 = </b>
	CASE "i","italic"
		LOCALS:0  = <i>
		LOCALS:1 = </i>
	CASE "u","under","underline"
		LOCALS:0  = <u>
		LOCALS:1 = </u>
	CASE "s","strike"
		LOCALS:0  = <s>
		LOCALS:1 = </s>
	CASEELSE
		THROW %ARGS:1%但异常です (<nobr> <br> <b> <i> <u> <s>的み对应してい增加)
ENDSELECT
RETURNF LOCALS:0 + ARGS + LOCALS:1

;-------------------------------------------------------------------------------
;	HTML_ADDNOBRTAG(式中関数)
;-------------------------------------------------------------------------------
;	まぁ见たまんま
;===============================================================================
@HTML_ADDNOBRTAG( ARGS = HTML_SUBSTITUTE )
#FUNCTIONS
RETURNF "<nobr>" + ARGS + "</nobr>"

;-------------------------------------------------------------------------------
;	HTML_ADDBOLDTAG(式中関数)
;-------------------------------------------------------------------------------
;	ｒｙ
;===============================================================================
@HTML_ADDBOLDTAG( ARGS = HTML_SUBSTITUTE )
#FUNCTIONS
RETURNF "<b>" + ARGS + "</b>"

;-------------------------------------------------------------------------------
;	HTML_ADDITALICTAG(式中関数)
;-------------------------------------------------------------------------------
;	ｒｙ
;===============================================================================
@HTML_ADDITALICTAG( ARGS = HTML_SUBSTITUTE )
#FUNCTIONS
RETURNF "<i>" + ARGS + "</i>"

;-------------------------------------------------------------------------------
;	HTML_ADDUNDERTAG(式中関数)
;-------------------------------------------------------------------------------
;	ｒｙ
;===============================================================================
@HTML_ADDUNDERTAG( ARGS = HTML_SUBSTITUTE )
#FUNCTIONS
RETURNF "<u>" + ARGS + "</u>"

;-------------------------------------------------------------------------------
;	HTML_ADDSTRIKETAG(式中関数)
;-------------------------------------------------------------------------------
;	ｒｙ
;===============================================================================
@HTML_ADDSTRIKETAG( ARGS = HTML_SUBSTITUTE )
#FUNCTIONS
RETURNF "<s>" + ARGS + "</s>"

;-------------------------------------------------------------------------------
;	HTML_ADDALIGNTAG(式中関数)
;-------------------------------------------------------------------------------
;	Pタグ是最前/最后尾不是和ダメらしい的で、最后に使おう
;===============================================================================
@HTML_ADDALIGNTAG( ARGS = HTML_SUBSTITUTE , VALUE )
#FUNCTIONS
#DIMS VALUE,1
RETURNF "<p align = '" + VALUE + "'>" + ARGS + "</p>"

; <br>
; さす但に手动で入れてちょ
