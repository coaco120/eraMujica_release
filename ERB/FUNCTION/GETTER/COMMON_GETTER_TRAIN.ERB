;==================================================
;
;	通用関数置き场＠参照編
;		参照に用い、特定的数値や名称など被返すも的
;		あるい是、状況に应じた接頭語など被返すも的
;
;		口上向け的も的もあったりなかったり。
;
;==================================================

;==================================================
;
;	ACT・COM相关的参照
;
;==================================================

;-------------------------------------------------
;名称・番号对应系(旧名称で的呼び出しも可能)
;
;○○LIST        对应関係目录被返す
;GET_○○NAME    番号から名称被返す
;GET_○○NUM     名称から番号被返す
;NOW○○NAME     现在的名称被返す
;IS_NOW○○NAME  现在的名称和合致すれば真被返す
;PREV○○NAME    上次的名称被返す
;IS_PREV○○NAME 上次的名称和合致すれば真被返す
;-------------------------------------------------

;-------------------------------------------------
;MENU(调教类別)
;-------------------------------------------------

;对应関係目录被返す
@MENULIST()
#FUNCTIONS
RETURNF "会话/爱抚/道具/性交/羞耻/奉仕/施虐/异常/奴役/休息/性奉"

;番号から名称被返す
@GET_MENUNAME(ARG)
#FUNCTIONS
RETURNF GET_NAME_LIST(ARG, MENULIST())

@MENU(ARG)
#FUNCTIONS
RETURNF GET_MENUNAME(ARG)

;名称から番号被返す
@GET_MENUNUM(ARGS)
#FUNCTION
RETURNF GET_NUM_LIST(ARGS, MENULIST())

@MENUNUM(ARGS)
#FUNCTION
RETURNF GET_MENUNUM(ARGS)

;现在的名称被返す
@NOWMENUNAME()
#FUNCTIONS
RETURNF GET_NAME_LIST(TFLAG:ACT分类, MENULIST())

;现在的名称和合致すれば真被返す
@IS_NOWMENUNAME(ARGS)
#FUNCTION
RETURNF IS_LIST(ARGS, MENULIST(), TFLAG:ACT分类)

;上次的名称被返す
@PREVMENUNAME()
#FUNCTIONS
RETURNF GET_NAME_LIST(TFLAG:前ACT分类, MENULIST())

;上次的名称和合致すれば真被返す
@IS_PREVMENUNAME(ARGS)
#FUNCTION
RETURNF IS_LIST(ARGS, MENULIST(), TFLAG:前ACT分类)

;-------------------------------------------------
;ACT(调教者行动)
;-------------------------------------------------

;对应関係目录被返す
@ACTLIST()
#FUNCTIONS
IF LOCALS == ""
	FOR LOCAL, 0, 800
		LOCALS += STR:LOCAL
		LOCALS += "/"
	NEXT
ENDIF
RETURNF LOCALS

;番号から名称被返す
@GET_ACTNAME(ARG)
#FUNCTIONS
RETURNF GET_NAME_LIST(ARG, ACTLIST())

;名称から番号被返す
@GET_ACTNUM(ARGS)
#FUNCTION
RETURNF GET_NUM_LIST(ARGS, ACTLIST())

;现在的名称被返す
@NOWACTNAME()
#FUNCTIONS
RETURNF GET_NAME_LIST(TFLAG:ACT, ACTLIST())

;现在的名称和合致すれば真被返す
@IS_NOWACTNAME(ARGS)
#FUNCTION
RETURNF IS_LIST(ARGS, ACTLIST(), TFLAG:ACT)

;上次的名称被返す
@PREVACTNAME()
#FUNCTIONS
RETURNF GET_NAME_LIST(TFLAG:前ACT, ACTLIST())

;上次的名称和合致すれば真被返す
@IS_PREVACTNAME(ARGS)
#FUNCTION
RETURNF IS_LIST(ARGS, ACTLIST(), TFLAG:前ACT)

;-------------------------------------------------
;ACTABB(调教者行动略称)
;※ACT略称是全角二字符(半角四字符)に統一した@これみ
;-------------------------------------------------

;对应関係目录被返す
@ACTABBLIST()
#FUNCTIONS
IF LOCALS == ""
	LOCALS += "心情/好感/知识/安慰/威胁/衣服/合意/惩罚/大喊/嗤笑/"
	LOCALS += "用手/胸部/用口/Ａ手/Ａ口/接吻/Ｃ擦/Ｂ擦/用脚/Ｖ手/"
	LOCALS += "跳蛋/振动/A振动/A拉珠/Ｃ爱/乳头/Ｐ帯/推倒///"
	LOCALS += "正常/后背/骑乘/对面/背面/ＡＳ/////"
	LOCALS += "自慰/张开/看到/大镜/野外/放尿/COS////"
	LOCALS += "用手/用口/用胸/素股//足舐/高潮/胸部///"
	LOCALS += "平手/木马/针/绳/眼罩/口枷/辱骂/木马/灌肠//"
	LOCALS += "Fist/ＡFi/両Fi/放置/公衆/Ｐ装/////"
	LOCALS += "触手//////////"
	LOCALS += "休息/服药/治疗///Ｍ正/Ｍ后/Ｍ对/Ｍ背/ＭAS/"
	LOCALS += "///Ｍ骑"
ENDIF
RETURNF LOCALS

;番号から名称被返す
@GET_ACTABBNAME(ARG)
#FUNCTIONS
RETURNF GET_NAME_LIST(ARG, ACTABBLIST())

;名称から番号被返す
@GET_ACTABBNUM(ARGS)
#FUNCTION
RETURNF GET_NUM_LIST(ARGS, ACTABBLIST())

;现在的名称被返す
@NOWACTABBNAME()
#FUNCTIONS
RETURNF GET_NAME_LIST(TFLAG:ACT, ACTABBLIST())

;现在的名称和合致すれば真被返す
@IS_NOWACTABBNAME(ARGS)
#FUNCTION
RETURNF IS_LIST(ARGS, ACTABBLIST(), TFLAG:ACT)

;上次的名称被返す
@PREVACTABBNAME()
#FUNCTIONS
RETURNF GET_NAME_LIST(TFLAG:前ACT, ACTABBLIST())

;上次的名称和合致すれば真被返す
@IS_PREVACTABBNAME(ARGS)
#FUNCTION
RETURNF IS_LIST(ARGS, ACTABBLIST(), TFLAG:前ACT)

;-------------------------------------------------
;ASSIMENU(助手方针)
;-------------------------------------------------

;对应関係目录被返す
@ASSIMENULIST()
#FUNCTIONS
RETURNF "コンビネーション/Ｃ爱抚/Ａ爱抚/Ｂ爱抚/Ｖ爱抚/Ｍ爱抚/羞耻/施虐/其他"

;番号から名称被返す
@GET_ASSIMENUNAME(ARG)
#FUNCTIONS
RETURNF GET_NAME_LIST(ARG, ASSIMENULIST())

;名称から番号被返す
@GET_ASSIMENUNUM(ARGS)
#FUNCTION
RETURNF GET_NUM_LIST(ARGS, ASSIMENULIST())

;ARGで助手番号被指定
;引数被省略した场合是ASSI被见る…
;但まぁこっち是基本的に引数いれてつかってください

;现在的名称被返す
@NOWASSIMENUNAME(ARG = -99)
#FUNCTIONS
ARG = ASSI_ARG_SET(ARG, "NOWASSIMENUNAME")
SIF ARG < 0
	RETURNF ""
RETURNF GET_NAME_LIST(TCVAR:ARG:助手ACT, ASSIMENULIST())

;现在的名称和合致すれば真被返す
@IS_NOWASSIMENUNAME(ARG = -99, ARGS)
#FUNCTION
ARG = ASSI_ARG_SET(ARG, "IS_NOWASSIMENUNAME")
SIF ARG < 0
	RETURNF 0
RETURNF IS_LIST(ARGS, ASSIMENULIST(), TCVAR:ARG:助手ACT)

;上次的名称被返す
@PREVASSIMENUNAME(ARG = -99)
#FUNCTIONS
ARG = ASSI_ARG_SET(ARG, "PREVASSIMENUNAME")
SIF ARG < 0
	RETURNF ""
RETURNF GET_NAME_LIST(TCVAR:ARG:助手上次ACT, ASSIMENULIST())

;上次的名称和合致すれば真被返す
@IS_PREVASSIMENUNAME(ARG = -99, ARGS)
#FUNCTION
ARG = ASSI_ARG_SET(ARG, "IS_PREVASSIMENUNAME")
SIF ARG < 0
	RETURNF 0
RETURNF IS_LIST(ARGS, ASSIMENULIST(), TCVAR:ARG:助手上次ACT)

;-------------------------------------------------
;ASSIACT(助手行动)
;-------------------------------------------------

;对应関係目录被返す
@ASSIACTLIST()
#FUNCTIONS
IF LOCALS == ""
	FOR LOCAL, 800, 1000
		LOCALS += STR:LOCAL
		LOCALS += "/"
	NEXT
ENDIF
RETURNF LOCALS

;番号から名称被返す
@GET_ASSIACTNAME(ARG)
#FUNCTIONS
RETURNF GET_NAME_LIST(ARG, ASSIACTLIST())

;名称から番号被返す
@GET_ASSIACTNUM(ARGS)
#FUNCTION
RETURNF GET_NUM_LIST(ARGS, ASSIACTLIST())

;ARGで助手番号被指定
;引数被省略した场合是ASSI被见る…
;但まぁこっち是基本的に引数いれてつかってください

;现在的名称被返す
@NOWASSIACTNAME(ARG = -99)
#FUNCTIONS
ARG = ASSI_ARG_SET(ARG, "NOWASSIACTNAME")
SIF ARG < 0
	RETURNF ""
RETURNF GET_NAME_LIST(TCVAR:ARG:助手ACT, ASSIACTLIST())

;现在的名称和合致すれば真被返す
@IS_NOWASSIACTNAME(ARG = -99, ARGS)
#FUNCTION
ARG = ASSI_ARG_SET(ARG, "IS_NOWASSIACTNAME")
SIF ARG < 0
	RETURNF 0
RETURNF IS_LIST(ARGS, ASSIACTLIST(), TCVAR:ARG:助手ACT)

;上次的名称被返す
@PREVASSIACTNAME(ARG = -99)
#FUNCTIONS
ARG = ASSI_ARG_SET(ARG, "PREVASSIACTNAME")
SIF ARG < 0
	RETURNF ""
RETURNF GET_NAME_LIST(TCVAR:ARG:助手上次ACT, ASSIACTLIST())

;上次的名称和合致すれば真被返す
@IS_PREVASSIACTNAME(ARG = -99, ARGS)
#FUNCTION
ARG = ASSI_ARG_SET(ARG, "IS_PREVASSIACTNAME")
SIF ARG < 0
	RETURNF 0
RETURNF IS_LIST(ARGS, ASSIACTLIST(), TCVAR:ARG:助手上次ACT)

;-------------------------------------------------
;COMGRO(调教对象行动分类)
;-------------------------------------------------

;对应関係目录被返す
@COMGROLIST()
#FUNCTIONS
RETURNF "无/消极的服从/淡然的服从/积极的服从/接受/请求宽恕/发狂/拒绝/逃跑"

;番号から名称被返す
@GET_COMGRONAME(ARG)
#FUNCTIONS
RETURNF GET_NAME_LIST(ARG, COMGROLIST())

@COMGRONAME(ARG)
#FUNCTIONS
RETURNF GET_COMGRONAME(ARG)

;名称から番号被返す
@GET_COMGRONUM(ARGS)
#FUNCTION
RETURNF GET_NUM_LIST(ARGS, COMGROLIST())

@COMGRONUM(ARGS)
#FUNCTION
RETURNF GET_COMGRONUM(ARGS)

;现在的名称被返す
@NOWCOMGRONAME()
#FUNCTIONS
RETURNF GET_NAME_LIST(TFLAG:REACT分类, COMGROLIST())

;现在的名称和合致すれば真被返す
@IS_NOWCOMGRONAME(ARGS)
#FUNCTION
RETURNF IS_LIST(ARGS, COMGROLIST(), TFLAG:REACT分类)

@IS_COMGRONAME(ARGS)
#FUNCTION
RETURNF IS_NOWCOMGRONAME(ARGS)

;上次的名称被返す
@PREVCOMGRONAME()
#FUNCTIONS
RETURNF GET_NAME_LIST(TFLAG:前REACT分类, COMGROLIST())

;上次的名称和合致すれば真被返す
@IS_PREVCOMGRONAME(ARGS)
#FUNCTION
RETURNF IS_LIST(ARGS, COMGROLIST(), TFLAG:前REACT分类)

;-------------------------------------------------
;COMIMP(REACT印象)
;-------------------------------------------------

;对应関係目录被返す
@COMIMPLIST()
#FUNCTIONS
RETURNF "影响无/好印象大/好印象小/恶印象小/恶印象大/0から遠ざかる/0接近"

;番号から名称被返す
@GET_COMIMPNAME(ARG)
#FUNCTIONS
RETURNF GET_NAME_LIST(ARG, COMIMPLIST())

@COMIMPNAME(ARG)
#FUNCTIONS
RETURNF GET_COMIMPNAME(ARG)

;名称から番号被返す
@GET_COMIMPNUM(ARGS)
#FUNCTION
RETURNF GET_NUM_LIST(ARGS, COMIMPLIST())

@COMIMPNUM(ARGS)
#FUNCTION
RETURNF GET_COMIMPNUM(ARGS)

;现在的名称被返す
@NOWCOMIMPNAME()
#FUNCTIONS
RETURNF GET_NAME_LIST(TFLAG:REACT印象, COMIMPLIST())

;现在的名称和合致すれば真被返す
@IS_NOWCOMIMPNAME(ARGS)
#FUNCTION
RETURNF IS_LIST(ARGS, COMIMPLIST(), TFLAG:REACT印象)

@IS_COMIMPNAME(ARGS)
#FUNCTION
RETURNF IS_NOWCOMIMPNAME(ARGS)

;-------------------------------------------------
;COM(调教对象行动)
;-------------------------------------------------

;对应関係目录被返す
@COMLIST()
#FUNCTIONS
IF LOCALS == ""
	FOR LOCAL, 0, 100
		LOCALS += TRAINNAME:LOCAL
		LOCALS += "/"
	NEXT
ENDIF
RETURNF LOCALS

;番号から名称被返す
@GET_COMNAME(ARG)
#FUNCTIONS
RETURNF GET_NAME_LIST(ARG, COMLIST())

;名称から番号被返す
@GET_COMNUM(ARGS)
#FUNCTION
RETURNF GET_NUM_LIST(ARGS, COMLIST())

;现在的名称被返す
;TRAINNAME:SELECTCOMでも可能
@NOWCOMNAME()
#FUNCTIONS
RETURNF GET_NAME_LIST(SELECTCOM, COMLIST())

;现在的名称和合致すれば真被返す
@IS_NOWCOMNAME(ARGS)
#FUNCTION
RETURNF IS_LIST(ARGS, COMLIST(), SELECTCOM)

;上次的名称被返す
;TRAINNAME:PREVCOMでも可能
@PREVCOMNAME()
#FUNCTIONS
RETURNF GET_NAME_LIST(PREVCOM, COMLIST())

;上次的名称和合致すれば真被返す
@IS_PREVCOMNAME(ARGS)
#FUNCTION
RETURNF IS_LIST(ARGS, COMLIST(), PREVCOM)

;-------------------------------------------------
;COMCOR(COM修正値)
;-------------------------------------------------

;对应関係目录被返す
@COMCORLIST()
#FUNCTIONS
RETURNF "积极/反抗/活力/精力/好感/友善/堕落"

;番号から名称被返す
@GET_COMCORNAME(ARG)
#FUNCTIONS
RETURNF GET_NAME_LIST(ARG, COMCORLIST())

;名称から番号被返す
@GET_COMCORNUM(ARGS)
#FUNCTION
RETURNF GET_NUM_LIST(ARGS, COMCORLIST())

;-------------------------------------------------
;共通处理
;-------------------------------------------------
@ASSI_ARG_SET(ARG, ARGS)
#FUNCTION
ARG = (ARG == -99 ? ASSI # ARG)
RETURNF ARG

;-------------------------------------------------
;関数名:GET_NAME_LIST
;概　要:LIST被使用し番号から名称被取得做関数
;引　数:ARG…番号, ARGS…LIST
;戻り値:名称
;-------------------------------------------------
@GET_NAME_LIST(ARG, ARGS)
#FUNCTIONS
#LOCALSSIZE 1000
;目录被分解
SPLIT ARGS, "/", LOCALS, LOCAL
;番号但范围内であるこ和被确认
IF ARG < 0 || ARG >= LOCAL
	;CALLF PRINT_ERRORMESSAGE(@"@GET_NAME_LIST に范围外的番号({ARG})但渡されました")
	RETURNF 
ENDIF
;名称被返す
RETURNF LOCALS:ARG

;-------------------------------------------------
;関数名:GET_NUM_LIST
;概　要:LIST被使用し名称から番号被取得做関数
;引　数:ARGS…名称, ARGS:1…LIST
;戻り値:番号
;-------------------------------------------------
@GET_NUM_LIST(ARGS, ARGS:1)
#FUNCTION
#LOCALSSIZE 1000
;目录被分解
SPLIT ARGS:1, "/", LOCALS, LOCAL
;名称但存在做こ和被确认
LOCAL = FINDELEMENT(LOCALS, ARGS, 0, LOCAL, 1)
SIF LOCAL == -1
	CALLF PRINT_ERRORMESSAGE(@"@GET_NUM_LIST に不正な名称(%ARGS%)但渡されました")
;番号被返す
RETURNF LOCAL

;-------------------------------------------------
;関数名:IS_LIST
;概　要:调査目录に名称和合致做も的但あれば真被返す
;       同时に名称目录に合致しなければ错误被吐く
;引　数:ARGS…调査目录(/区切りで複数代入但可能です)
;       ARGS:1…名称目录, ARGS:2…名称
;戻り値:真伪値(1/0)
;-------------------------------------------------
@IS_LIST(ARGS, ARGS:1, ARG)
#FUNCTION
#LOCALSSIZE 1000
#DIMS LTEMPS, 10
;调査目录被分解
SPLIT ARGS, "/", LTEMPS, LOCAL:1
;名称目录被分解
SPLIT ARGS:1, "/", LOCALS, LOCAL:2
;调査目录被顺に见ていく
FOR LOCAL, 0, LOCAL:1
	;名称目录から番号被取得
	SELECTCASE FINDELEMENT(LOCALS, LTEMPS:LOCAL, 0, LOCAL:2, 1)
		;名称但存在做こ和被确认
		CASE -1
			CALLF PRINT_ERRORMESSAGE(@"@IS_LIST に不正な名称(%LTEMPS:LOCAL%)但渡されました")
			RETURNF -1
		;適合すれば真被返す
		CASE ARG
			RETURNF 1
	ENDSELECT
NEXT
RETURNF 0

;-------------------------------------------------
;関数名:MENUMATCH
;概　要:ARGで指定したACT番号但ARGSで指定したACT分类に含まれているか被返す
;引　数:ARG…ACT番号、ARGS…ACT分类
;戻り値:含まれれば1、含まれなければ0
;-------------------------------------------------
@MENUMATCH(ARG, ARGS)
#FUNCTION
#LOCALSIZE 1
#DIM LCOUNT, 1
LOCAL = MENUNUM(ARGS)
FOR LCOUNT, 0, ACTM_NUM(LOCAL)
	SIF ACTM_NUM(LOCAL, LCOUNT) == GET_NORMALACTNUM(ARG)
		RETURNF 1
NEXT
RETURNF 0

;-------------------------------------------------
;関数名:ACTM_NUM
;概　要:系列番号から系列所属ACT番号被返す関数
;引　数:ARG:0…对象和なる系列番号
;       ARG:1…-1であれば所属ACT数、0以降所属ACT番号被顺番に返す
;戻り値:所属ACT数、ACT番号
;-------------------------------------------------
@ACTM_NUM(ARG:0, ARG:1 = -1)
#FUNCTION
#LOCALSIZE 20
;LOCAL被-1に初期化
VARSET LOCAL, -1
SELECTCASE ARG:0
	CASE 0
		LOCAL = 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 66
	CASE 1
		LOCAL = 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20, 27
	CASE 2
		LOCAL = 20, 21, 22, 23, 24, 25, 26, 68
	CASE 3
		LOCAL = 30, 31, 32, 33, 34, 35
	CASE 4
		LOCAL = 40, 41, 42, 43, 44, 45, 46, 73
	CASE 5
		LOCAL = 50, 51, 52, 53, 55, 56, 57
	CASE 6
		LOCAL = 60, 61, 62, 63, 64, 65, 66, 67, 68, 56
	CASE 7
		LOCAL = 70, 71, 72, 73, 74, 75, 9, 18, 55, 67
	CASE 9
		LOCAL = 90, 91, 92
	CASE 10
		LOCAL = 95, 96, 103, 97, 98, 99
ENDSELECT
;ARG:1但负那么所属ACT数
SIF ARG:1 < 0
	RETURNF FINDELEMENT(LOCAL, -1)
;ARG:1但正那么ACT番号
SIF ARG:1 < VARSIZE("LOCAL")
	RETURNF LOCAL:(ARG:1)
;どちらにも合わなければ-1被返す
RETURNF -1

;-------------------------------------------------
;関数名:ACTMNAME_NUM
;概　要:系列名称から系列所属ACT番号被返す関数
;引　数:ARGS:0…对象和なる系列名称、あるい是系列番号被表す字符串
;        ARG:0…-1であれば所属ACT数、0以降所属ACT番号被顺番に返す
;戻り値:所属ACT数、ACT番号
;-------------------------------------------------
@ACTMNAME_NUM(ARGS:0, ARG:0 = -1)
#FUNCTION
RETURNF ACTM_NUM(ISNUMERIC(ARGS:0) ? TOINT(ARGS:0) # MENUNUM(ARGS:0), ARG:0)

;-------------------------------------------------
;関数名:GET_ACTM
;概　要:ACT番号からACT分类番号被取得做関数
;引　数:ARG…ACT番号
;戻り値:ACT分类番号
;備　考:未登录的番号だ和-1但返る
;-------------------------------------------------
@GET_ACTM, ARG
#FUNCTION
#DIM LCOUNT, 2
ARG = GET_NORMALACTNUM(ARG)
FOR LCOUNT, 0, 11
	FOR LCOUNT:1, 0, ACTM_NUM(LCOUNT, -1)
		SIF ARG == ACTM_NUM(LCOUNT, LCOUNT:1)
			RETURNF LCOUNT
	NEXT
NEXT
RETURNF -1

;-------------------------------------------------
;関数名:GET_COMBIACTNUM
;概　要:ACT番号からコンビネーションACT番号あるい是そ的逆被取得做関数
;引　数:ARG…ACT番号
;戻り値:ACT番号
;備　考:派生条件被满たしてない场合是-1被返す
;	    また、こ的関数自体に派生被侍奉处理是ない的にも注意
;-------------------------------------------------
@GET_COMBIACTNUM, ARG
#FUNCTION
SELECTCASE ARG
	;Ｗ手淫/用手爱抚
	CASE 10
		SIF PENIS(MASTER)
			RETURNF 300
	CASE 300
		RETURNF 10
	;Ｗ口交/用口爱抚
	CASE 12
		SIF PENIS(MASTER)
			RETURNF 301
	CASE 301
		RETURNF 12
	;Ｗ接吻/接吻
	CASE 15
		RETURNF 302
	CASE 302
		RETURNF 15
	;Ｗ素股/摩擦性器
	CASE 16
		SIF PENIS(MASTER)
			RETURNF 303
	CASE 303
		RETURNF 16
	;Ｗ乳交/进行乳交
	CASE 17
		SIF PENIS(MASTER) && !GET_TALENT(ASSI:1, "男人")
			RETURNF 304
	CASE 304
		RETURNF 17
	;Ｗ足交/进行足交
	CASE 18
		SIF PENIS(MASTER)
			RETURNF 305
	CASE 305
		RETURNF 18
	;骑乘二穴进攻/骑乘位
	CASE 32
		SIF PENIS(ASSI:1) && !TALENT:MASTER:男人 && ABL:MASTER:Ａ感觉 > 1
			RETURNF 306
	CASE 306
		RETURNF 32
	;对面二穴进攻/对面座位
	CASE 33
		SIF PENIS(ASSI:1) && !TALENT:MASTER:男人 && ABL:MASTER:Ａ感觉 > 1
			RETURNF 307
	CASE 307
		RETURNF 33
	;特别版牡丹乱舞/背面座位
	CASE 34
		RETURNF 308
	CASE 308
		RETURNF 34
	;肛门二穴进攻/肛交
	CASE 35
		SIF PENIS(ASSI:1) && !TALENT:MASTER:男人 && !TALENT:MASTER:处女
			RETURNF 309
	CASE 309
		RETURNF 35
	;Ｗ逆强奸/逆强奸
	CASE 103
		SIF PENIS(MASTER) && !GET_TALENT(ASSI:1, "男人")
			RETURNF 310
	CASE 310
		RETURNF 103
	;谷间强渡/正常位
	CASE 95
		SIF PENIS(MASTER) && !GET_TALENT(ASSI:1, "男人")
			RETURNF 311
	CASE 311
		RETURNF 95
	;强制双龙出海/强制口爱抚
	CASE 51
		SIF PENIS(TARGET) && PENIS(ASSI:1)
			RETURNF 312
	CASE 312
		RETURNF 51
ENDSELECT
RETURNF -1

;-------------------------------------------------
;関数名:GET_COMBIACTNAME
;概　要:ACT名称からコンビネーションACT名称あるい是そ的逆被取得做関数
;引　数:ARGS…ACT名称
;戻り値:ACT名称
;備　考:派生条件被满たしてない场合是 "ＮＯＮＥ"被返す
;	    また、こ的関数自体に派生被侍奉处理是ない的にも注意
;-------------------------------------------------
@GET_COMBIACTNAME, ARGS
#FUNCTIONS
RETURNF GET_ACTNAME(GET_COMBIACTNUM(GET_ACTNUM(ARGS)))

;-------------------------------------------------
;関数名:GET_SPACTNUM
;概　要:ACT番号からSP活动ション番号あるい是そ的逆被取得做関数
;引　数:ARG…ACT番号
;戻り値:ACT番号
;備　考:派生条件被满たしてない场合是-1被返す
;	    また、こ的関数自体に派生被侍奉处理是ない的にも注意
;-------------------------------------------------
@GET_SPACTNUM, ARG
#FUNCTION
SELECTCASE ARG
	;针/信仰之针
	CASE 62
		SIF NO:TARGET == 1 && CFLAG:调教等级 > 0
			RETURNF 200
	CASE 200
		RETURNF 62
	;绳子紧绑/八方鬼縛阵
	CASE 63
		SIF NO:TARGET == 1 && ABL:技巧 > 2
			RETURNF 201
	CASE 201
		RETURNF 63
	;令人毛骨悚然に微笑む/狂气之瞳
	CASE 9
		SIF NO:TARGET == 28 && ABL:异常 > 1
			RETURNF 202
	CASE 202
		RETURNF 9
ENDSELECT
RETURNF -1

;-------------------------------------------------
;関数名:GET_SPACTNAME
;概　要:ACT名称からコンビネーションACT名称あるい是そ的逆被取得做関数
;引　数:ARGS…ACT名称
;戻り値:ACT名称
;備　考:派生条件被满たしてない场合是 "ＮＯＮＥ"被返す
;	    また、こ的関数自体に派生被侍奉处理是ない的にも注意
;-------------------------------------------------
@GET_SPACTNAME, ARGS
#FUNCTIONS
RETURNF GET_ACTNAME(GET_SPACTNUM(GET_ACTNUM(ARGS)))

;-------------------------------------------------
;関数名:GET_NORMALACTNUM
;概　要:コンビネーション、ＳＰ活动ション番号から派生前ACT番号被取得做関数
;引　数:ARG…ACT番号
;戻り値:ACT番号
;備　考:变化したACT番号被元に戻すも的。も和も和派生していない场合是そ的まま返す的で判定に使う
;　　　派生元但不明なACT番号是そ的まま返す
;-------------------------------------------------
@GET_NORMALACTNUM, ARG
#FUNCTION
;300番台, 助手和的コンビネーション
IF ARG/100 == 3 && GET_COMBIACTNUM(ARG) >= 0
	RETURNF GET_COMBIACTNUM(ARG)
;200番台, ＳＰ活动ション
ELSEIF ARG/100 == 2 && GET_SPACTNUM(ARG) >= 0
	RETURNF GET_SPACTNUM(ARG)
ENDIF
RETURNF ARG

;-------------------------------------------------
;関数名:GET_COMCOR
;概　要:修正种別的COM修正値被取得（＆保存）
;引　数:ARGS…COM修正种名
;　　　 ARG …记入した场合、そ的修正种的数値被记录
;戻り値:指定したCOM修正种的修正値
;-------------------------------------------------
@GET_COMCOR(ARGS, ARG = -65535)
#FUNCTION
IF GET_COMCORNUM(ARGS) < 0
	CALLF PRINT_ERRORMESSAGE(@"@GET_COMCOR に无效なCOM修正値名(%ARGS%)但渡されました")
	RETURNF -1
ENDIF
SIF ARG != -65535
	LOCAL:GET_COMCORNUM(ARGS) = ARG
RETURNF LOCAL:GET_COMCORNUM(ARGS)

;-------------------------------------------------
;関数名:GET_COMCORTYPE
;概　要:COM好感計算タイプ被取得
;引　数:ARG …ACT番号。省略做和现在的ACT
;戻り値:指定したACT的好感計算タイプ（字符串）
;-------------------------------------------------
@GET_COMCORTYPE(ARG = -1)
#FUNCTIONS
ARG = ARG != -1 ? ARG # TFLAG:ACT
IF GET_ACTNAME(GET_NORMALACTNUM(ARG)) == "骑乘位"
	RETURNF "行為"
ELSEIF GET_ACTNAME(GET_NORMALACTNUM(ARG)) == "逆强奸"
	RETURNF "忍耐"
ELSEIF MENUMATCH(ARG, "性交") 
	RETURNF "忍耐"
ELSEIF MENUMATCH(ARG, "性奉") 
	RETURNF "行為"
ELSEIF GET_ACTNAME(ARG) == "自慰"
	RETURNF "忍耐"
ELSEIF GET_ACTNAME(ARG) == "胸爱抚" && TFLAG:ACT派生 == 2
	RETURNF "行為"
ELSEIF MENUMATCH(ARG, "爱抚")
	RETURNF "忍耐"
ELSEIF MENUMATCH(ARG, "道具")
	RETURNF "忍耐"
ELSEIF MENUMATCH(ARG, "奉仕")
	RETURNF "行為"
ENDIF
RETURNF "屈服"

;-------------------------------------------------
;関数名:COMCOR_POSI
;概　要:主に消极的服从系COM的气力消费但大きい際に、COM修正値但好感的かどうか被判断
;　　　 「嫌々やっている」的か「やり想要的被忍耐している」的か参照做ために用いる
;引　数:ARGS…行為(爱抚强制→消极的执行等)、忍耐(爱抚→忍受快感等)、屈服(征求同意→软弱く答える等)
;　　　 省略做和现在的ACT分类から自动生成做
;戻り値:行為那么积极+友善＞反抗*2　忍耐那么好感+堕落＞反抗+活力　屈服那么好感+友善＞反抗+精力であれば1。違う那么0
;-------------------------------------------------
@COMCOR_POSI(ARGS)
#FUNCTION
ARGS = \@ ARGS == "" ? %GET_COMCORTYPE()% # %ARGS% \@
SELECTCASE ARGS
	CASE "行為"
		RETURNF GET_COMCOR("积极") + GET_COMCOR("友善") > GET_COMCOR("反抗") * 2
	CASE "忍耐"
		RETURNF GET_COMCOR("好感") + GET_COMCOR("堕落") > GET_COMCOR("反抗") + GET_COMCOR("活力")
	CASE "屈服"
		RETURNF GET_COMCOR("好感") + GET_COMCOR("友善") > GET_COMCOR("反抗") + GET_COMCOR("精力")
ENDSELECT
RETURNF 0

;-------------------------------------------------
;関数名:GET_COMEFFORT
;引　数:ARGS …COM名称
;戻り値:指定したCOM的REACT负荷
;-------------------------------------------------
@GET_COMEFFORT(ARGS)
#FUNCTION
RETURNF !COM_VITALITY("顺位低", ARGS) ? 0 # (COM_VITALITY(, ARGS) >= 100)+1

;-------------------------------------------------
;関数名:GET_ASSIACTMATCH
;概　要:助手ACT名称からそ的助手ACT被实行している助手番号被取得做関数
;引　数:ARGS…助手ACT名称
;戻り値:助手番号（0～3）　そ的行动被した助手但居なかった场合是0
;備　考:ASSI:1的1助手番号。本来ASSI:0是行动中的助手被指す但、
;	　　ここで的戻り値0是指定した行动被和った助手但いないこ和被表す
;	　　同じ行动被和った助手但複数いる场合是、番号的若い方被优先做
;-------------------------------------------------
@GET_ASSIACTMATCH, ARGS
#FUNCTION
#DIM LCOUNT, 1
;ひ和まず助手的数是３人直到和做
FOR LCOUNT, 1, 4
	SIF ASSI:LCOUNT >= 0 && TCVAR:(ASSI:LCOUNT):助手ACT == GET_ASSIACTNUM(ARGS)
		RETURNF LCOUNT
NEXT
;ヒットしなかったら0で返回
RETURNF 0

;-------------------------------------------------
;性交主導権取得関数@SEX_INITIATIVE
;式中で使用做関数です。现在的性交で主に腰被振っている角色的登录番号（性交被发生してない那么-1）被返し增加。
;引数に角色登录番号被与えた场合、そ的角色に主導権但あれば真になり增加。
;-------------------------------------------------
@SEX_INITIATIVE(ARG = -2)
#FUNCTION
#DIM LCOUNT, 1
LOCAL = -1
IF 0
;ACT根据判定
ELSEIF GET_ACTNAME(GET_NORMALACTNUM(TFLAG:ACT)) == "骑乘位"
	LOCAL = MASTER
ELSEIF GET_ACTNAME(GET_NORMALACTNUM(TFLAG:ACT)) == "逆强奸"
	LOCAL = TARGET
ELSEIF MENUMATCH(TFLAG:ACT, "性交") 
	LOCAL = TARGET
ELSEIF MENUMATCH(TFLAG:ACT, "性奉") 
	LOCAL = MASTER
;TEQUIP根据判定
ELSEIF TEQUIP:性交中 == 3
	LOCAL = MASTER
ELSEIF TEQUIP:性交奉仕中 == 3
	LOCAL = TARGET
ELSEIF TEQUIP:性交中
	LOCAL = TARGET
ELSEIF TEQUIP:性交奉仕中
	LOCAL = MASTER
ENDIF
;现在助手和的性交是、助手侧但进攻（通常性交・逆强奸）でしか发生不做。体位的概念もない
IF LOCAL == -1
	FOR LCOUNT, 1, 3
		IF ASSI:LCOUNT >= 0
			;助手ACT根据判定
			IF GROUPMATCH(TCVAR:(ASSI:LCOUNT):助手ACT, GET_ASSIACTNUM("性交"), GET_ASSIACTNUM("逆强奸"))
				LOCAL = ASSI:LCOUNT
			;TEQUIP根据判定
			ELSEIF TEQUIP:助手性交中 == ASSI:LCOUNT || TEQUIP:助手性交奉仕中 == ASSI:LCOUNT
				LOCAL = ASSI:LCOUNT
			ENDIF
		ENDIF
	NEXT
ENDIF
RETURNF ARG == -2 ? LOCAL # ARG == LOCAL

;-------------------------------------------------
;体位判定関数@POSITION
;式中で使用做関数です。引数被TFLAG:90(ACT)和した场合的体位被TEQUIP:性交中/TEQUIP:性交奉仕中準拠で返し增加
;-------------------------------------------------
@POSITION(ARG)
#FUNCTION
SELECTCASE ARG
	;正常位/后背位/骑乘位/对面座位/背面座位/肛交
	CASE 30 TO 35
		RETURNF ARG - 29
	;逆强奸
	CASE 103
		RETURNF 3
	;正常位侍奉/后背位侍奉
	CASE 95, 96
		RETURNF ARG - 94
	;对面座位侍奉/背面座位侍奉/肛交侍奉
	CASE 97 TO 99
		RETURNF ARG - 93
ENDSELECT
RETURNF 0

;-------------------------------------------------
;Ｖ插入判定関数@V_SEX
;式中で使用做関数です。引数的角色但Ｖに插入中那么真（返り値是体位値）になり增加。
;现在性交中・性交奉仕中的参照只な的で、TARGET和MASTER以外是弹き增加
;省略做和TARGET的插入、つまり性交中被参照し增加。
;-------------------------------------------------
@V_SEX(ARG = -2)
#FUNCTION
ARG = ARG == MASTER ? MASTER # TARGET
SIF ARG == TARGET
	RETURNF INRANGE(TEQUIP:性交中, 1, 5) ? TEQUIP:性交中 # 0
SIF ARG == MASTER
	RETURNF INRANGE(TEQUIP:性交奉仕中, 1, 5) ? TEQUIP:性交奉仕中 # 0
RETURNF 0

;-------------------------------------------------
;Ａ插入判定関数@A_SEX
;式中で使用做関数です。引数的角色但Ａに插入中那么真（返り値是体位値）になり增加。
;现在性交中・性交奉仕中的参照只な的で、TARGET和MASTER以外是弹き增加
;省略做和TARGET的插入、つまり性交中被参照し增加。
;-------------------------------------------------
@A_SEX(ARG = -2)
#FUNCTION
ARG = ARG == MASTER ? MASTER # TARGET
SIF ARG == TARGET
	RETURNF TEQUIP:性交中 == 6 ? TEQUIP:性交中 # 0
SIF ARG == MASTER
	RETURNF TEQUIP:性交奉仕中 == 6 ? TEQUIP:性交奉仕中 # 0
RETURNF 0

;-------------------------------------------------
;上次合Ｖ插入判定関数@PREV_V_SEX
;式中で使用做関数です。引数的角色但上次合でＶに插入中那么真（返り値是体位値）になり增加。
;上次合的性交中・性交奉仕中的参照只な的で、TARGET和MASTER以外是弹き增加
;省略做和TARGET的插入、つまり上次合性交被参照し增加。
;-------------------------------------------------
@PREV_V_SEX(ARG = -2)
#FUNCTION
ARG = ARG == MASTER ? MASTER # TARGET
SIF ARG == TARGET
	RETURNF INRANGE(TFLAG:上次合性交, 1, 5) ? TFLAG:上次合性交 # 0
SIF ARG == MASTER
	RETURNF INRANGE(TFLAG:上次合性交奉仕, 1, 5) ? TFLAG:上次合性交奉仕 # 0
RETURNF 0

;-------------------------------------------------
;上次合Ａ插入判定関数@PREV_A_SEX
;式中で使用做関数です。引数的角色但上次合でＡに插入中那么真（返り値是体位値）になり增加。
;上次合的性交中・性交奉仕中的参照只な的で、TARGET和MASTER以外是弹き增加
;省略做和TARGET的插入、つまり上次合性交被参照し增加。
;-------------------------------------------------
@PREV_A_SEX(ARG = -2)
#FUNCTION
ARG = ARG == MASTER ? MASTER # TARGET
SIF ARG == TARGET
	RETURNF TFLAG:上次合性交 == 6 ? TFLAG:上次合性交 # 0
SIF ARG == MASTER
	RETURNF TFLAG:上次合性交奉仕 == 6 ? TFLAG:上次合性交奉仕 # 0
RETURNF 0

;-------------------------------------------------
;関数名:PLEASURE_CHECK
;概　要:快感チェック関数
;引　数:ARG…真那么MASTER的技量
;            伪那么TARGET的技量(省略时TARGET)
;戻り値:真那么爱抚で全然感じなかったこ和被表す
;備  考:式中関数
;爱抚的巧拙被判定做関数的つもり
;COM12被参考にしている但、独自的改造被施した的で
;何かおかしいかもしれない。参考程度に…
;-------------------------------------------------
@PLEASURE_CHECK(ARG)
#FUNCTION
#LOCALSIZE 2
;MASTER但TARGETに与える快感被チェック
IF ARG
	;TARGET的抵抗度合い
	LOCAL:0 = 20 + TALENT:自尊心高 * 5 + TALENT:刚毅 * 5 - (CFLAG:好感 / 200)
	;TARGET的受けた快感
	LOCAL:1 = SOURCE:快Ｃ * (1 + TALENT:Ｃ敏感) + SOURCE:快Ｖ * (1 + TALENT:Ｖ敏感) + SOURCE:快Ａ * (1 + TALENT:Ａ敏感) + SOURCE:快Ｂ * (1 + TALENT:Ｂ敏感)
	LOCAL:0 -= LOCAL:1 / (500 - TALENT:承认快感 * 100 + TALENT:否定快感 * 50)
;TARGET但MASTERに与える快感被チェック
ELSE
	;MASTER的抵抗度合い
	LOCAL:0 = 20 + TALENT:MASTER:自尊心高 * 5 + TALENT:MASTER:刚毅 * 5 + MARK:反抗刻印 + PALAM:MASTER:反抗 / 500 - GET_ABL(MASTER, "顺从") * 3 / 10 - CFLAG:MASTER:调教等级
	;MASTER的受けた快感
	LOCAL:1 = SOURCE:MASTER:快Ｃ * (1 + TALENT:MASTER:Ｃ敏感) + SOURCE:MASTER:快Ｖ * (1 + TALENT:MASTER:Ｖ敏感) + SOURCE:MASTER:快Ａ * (1 + TALENT:MASTER:Ａ敏感) + SOURCE:MASTER:快Ｂ * (1 + TALENT:MASTER:Ｂ敏感)
	LOCAL:0 -= LOCAL:1 / (500 - TALENT:MASTER:承认快感 * 100 + TALENT:MASTER:否定快感 * 50)
ENDIF
RETURNF LOCAL:0 > 0

;-------------------------------------------------
;関数名:CARESS_SPOT
;概　要:爱抚个所関数
;引　数:ARG:0…角色登录番号。省略做和TARGET
;戻り値:そ的角色但MASTER的どこ被爱抚了ているか
;       (0.してない/1.Ｃ/2.Ｖ/3.Ａ/4.Ｂ/5.口)
;備  考:式中関数
;指定した角色但どこ被爱抚了ているか被显示。自慰对应
;-------------------------------------------------
@CARESS_SPOT(ARG = -2)
#FUNCTION
ARG = ARG != -2 ? ARG # TARGET

SELECTCASE ARG
	;调教者的场合
	CASE TARGET
		SIF MC_PLAYER() == 2 || GROUPMATCH(TFLAG:90, 62, 67)
			RETURNF 1
		SIF MV_PLAYER() == 2
			RETURNF 2
		SIF MA_PLAYER() == 2 || GROUPMATCH(TFLAG:90, 60, 61, 68)
			RETURNF 3
		SIF MB_PLAYER() == 2 || GROUPMATCH(TFLAG:90, 63)
			RETURNF 4
		SIF GROUPMATCH(TFLAG:90, 15, 51, 55, 56, 64, 65) || TEQUIP:顔面骑乘 || TEQUIP:顔面骑乘肛门
			RETURNF 5
		RETURNF 0
	;调教对象的场合
	CASE MASTER
		SIF MC_PLAYER() == 1
			RETURNF 1
		SIF MV_PLAYER() == 1
			RETURNF 2
		SIF MA_PLAYER() == 1
			RETURNF 3
		SIF MB_PLAYER() == 1
			RETURNF 4
		RETURNF 0
	;助手1的场合
	CASE ASSI:1
		SELECTCASE TCVAR:(ASSI:1):助手ACT
			CASE 1, 3, 4, 5, 6, 7
				RETURNF 1
			CASE 30, 31, 32
				RETURNF 2
			CASE 10, 11, 12, 13, 14
				RETURNF 3
			CASE 20, 21, 22
				RETURNF 4
			CASE 40, 41, 42, 60
				RETURNF 5
			CASEELSE
				RETURNF 0
		ENDSELECT
	;助手2的场合
	CASE ASSI:2
		SELECTCASE TCVAR:(ASSI:2):助手ACT
			CASE 1, 3, 4, 5, 6, 7
				RETURNF 1
			CASE 30, 31, 32
				RETURNF 2
			CASE 10, 11, 12, 13, 14
				RETURNF 3
			CASE 20, 21, 22
				RETURNF 4
			CASE 40, 41, 42, 60
				RETURNF 5
			CASEELSE
				RETURNF 0
		ENDSELECT
ENDSELECT


;-------------------------------------------------
;関数名:MC_PLAYER
;概　要:Ｃ爱抚者/爱抚物判定関数
;引　数:ARG…真那么爱抚している物但何であるか被返す
;            伪那么爱抚している人但誰な的か被返す。複数存在做场合是调教者优先
;戻り値:物的场合→0.无/1.口/2.手/3.阴茎/4.小穴/5.肛门/6.胸/7.足/8.小穴口
;                 /90.触手
;                 /100.跳蛋/112.阴蒂夹/114.飞机杯
;       人的场合→0.无/1.调教对象自身/2.调教者/4.助手1/8.助手2/16.助手３
;                 /64.道具/128.便所使用者
;備  考:式中関数
;仕大人被整理して作り直した、新しい爱抚者/爱抚物判定関数
;反应口上や绝顶口上で的使用被想定している
;-------------------------------------------------
@MC_PLAYER(ARG)
#FUNCTION
#LOCALSIZE 1
;TEQUIP根据判定1(优先度高)
;肛交侍奉
SIF TEQUIP:性交奉仕中 == 6
	RETURNF ARG ? 5 # 2
;性交奉仕中(肛交以外)
SIF TEQUIP:性交奉仕中
	RETURNF ARG ? 4 # 2
;阴蒂夹
SIF TEQUIP:阴蒂夹 && ARG
	RETURNF 112
;飞机杯
SIF TEQUIP:飞机杯 && ARG
	RETURNF 114
;假阴茎
SIF TEQUIP:假阴茎 && ARG
	RETURNF 103

;调教者活动ション根据判定1(优先度高)
SELECTCASE TFLAG:ACT
	;用手爱抚/对面座位/背面座位/打开小穴
	CASE 10, 33, 34, 41
		RETURNF ARG ? 2 # 2
	;用口爱抚
	CASE 12
		RETURNF ARG ? 1 # 2
	;摩擦性器
	CASE 16
		RETURNF ARG ? 8 # 2
	;进行乳交
	CASE 17
		RETURNF ARG ? 6 # 2
	;进行足交
	CASE 18
		RETURNF ARG ? 7 # 2
	;跳蛋
	CASE 20
		RETURNF ARG ? 100 # 2
	;素股
	CASE 53
		;命令に从った场合的み
		SIF !IS_COMGRONAME("拒绝")
			RETURNF ARG ? 3 # 2
ENDSELECT

;継续手淫
SIF TEQUIP:调教对象Ｃ使用 == 1
	RETURNF ARG ? 2 # 2
;継续口交
SIF TEQUIP:调教对象Ｃ使用 == 2
	RETURNF ARG ? 1 # 2
;助手活动ション根据判定
FOR LOCAL, 1, 4
	;助手但いない场合是判定不做
	SIF ASSI:LOCAL < 0
		CONTINUE
	SELECTCASE TCVAR:(ASSI:LOCAL):助手ACT
		;用口爱抚
		CASE 1
			RETURNF ARG ? 1 # (2 << LOCAL)
		;摩擦性器
		CASE 2
			RETURNF ARG ? 8 # (2 << LOCAL)
		;进行乳交
		CASE 3
			RETURNF ARG ? 6 # (2 << LOCAL)
		;进行足交
		CASE 4
			RETURNF ARG ? 7 # (2 << LOCAL)
		;道具で爱抚
		CASE 5
			;爱抚者判定
			SIF !ARG
				RETURNF (2 << LOCAL)
			;爱抚物判定
			RETURNF PENIS(MASTER) ? 114 # 100
		;逆强奸
		CASE 6
			RETURNF ARG ? 3 # (2 << LOCAL)
		;用手爱抚
		CASE 7
			RETURNF ARG ? 2 # (2 << LOCAL)
	ENDSELECT
NEXT

;调教者活动ション根据判定2(优先度低)
SELECTCASE TFLAG:ACT
	;Ｃ道具爱抚
	CASE 24
		;爱抚者判定
		SIF !ARG
			RETURNF 2
		;爱抚物判定
		RETURNF PENIS(MASTER) ? 114 # 112
	;公共肉便器PLAY
	CASE 74
		;便器和して使用さ受到场合
		SIF TFLAG:ACT派生 > 1
			RETURNF ARG ? 4 # 128
	;性交奉仕(肛交以外)(抵抗さ受到)
	CASE 103, 95 TO 98
		;快Ｃ被得ている
		SIF SOURCE:MASTER:快Ｃ > 0
			RETURNF ARG ? 4 # 2
	;肛交侍奉(抵抗さ受到)
	CASE 99
		;快Ｃ被得ている
		SIF SOURCE:MASTER:快Ｃ > 0
			RETURNF ARG ? 5 # 2
ENDSELECT

;COM根据判定
;移除道具で主动でＣ関係道具被解除している
SIF SELECTCOM == GETNUM(TRAINNAME, "移除道具") && TFLAG:REACT派生 == GETNUM(TEQUIP, "阴蒂夹")
	RETURNF ARG ? 112 # 1
SIF SELECTCOM == GETNUM(TRAINNAME, "移除道具") && TFLAG:REACT派生 == GETNUM(TEQUIP, "飞机杯")
	RETURNF ARG ? 114 # 1
SIF SELECTCOM == GETNUM(TRAINNAME, "移除道具") && TFLAG:REACT派生 == GETNUM(TEQUIP, "假阴茎")
	RETURNF ARG ? 103 # 1

;TEQUIP根据判定2(优先度低)
;调教对象自慰中(快Ｃ被得ている)
SIF GETBIT(TEQUIP:自慰中, 0) && SOURCE:MASTER:快Ｃ > 0
	RETURNF ARG ? 2 # 1

;-------------------------------------------------
;関数名:ME_PLAYER
;概　要:调教对象射精对象人物/物体判定関数
;引　数:ARG…真那么射精对象物体但何であるか被返す
;            伪那么射精对象人物但誰な的か被返す。複数存在做场合是调教者优先
;戻り値:物的场合→0.无/1.口/2.手/3.阴茎/4.小穴/5.肛门/6.胸/7.足/8.小穴口/9.顔
;                /90.触手
;                /100.跳蛋/112.阴蒂夹/114.飞机杯
;       人的场合→0.无/1.调教对象自身/2.调教者/4.助手1/8.助手2/16.助手３
;                /64.道具/128.便所使用者
;備  考:式中関数
;
;自慰顔射射精时など爱抚物和射精对象物体但异なる场合被想定して新设
;MX_PLAYER全部に共通做こ和和して对象但複数存在做场合手も足も出ない的で
;そ的うち字符串で複数对象被全部返す関数被作ってもいいかもしれない
;何にせよ急いで做こ和でもないんじゃないかな@これみ
;-------------------------------------------------
@ME_PLAYER(ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
;非射精时0被返す
SIF !NOWEX:MASTER:射精
	RETURNF 0
;自慰射精、积极的服从、高潮、焦虑终了/无し时
IF (IS_NOWACTNAME("自慰") || GETBIT(TEQUIP:自慰中, 0)) && IS_COMGRONAME("积极的服从") && TCVAR:MASTER:高潮 && TCVAR:MASTER:焦虑度 < 2
	;1/2的确率で自慰顔射射精
	IF LOCALS != SAME_TURN()
		LOCALS = SAME_TURN()
		LOCAL = RAND:2
	ENDIF
	;自慰顔射射精时以外是特別な处理被行わない
	;自慰中でも、奉仕・爱抚・性交・性交奉仕・施虐・道具ACT的際是顔射に无理但ある的で行わない
	SIF LOCAL && !GROUPMATCH(GET_ACTM(TFLAG:ACT), 1, 2, 3, 5, 6, 10)
		RETURNF ARG ? 9 # 2
ENDIF
;其他的场合是Ｃ爱抚者/爱抚物判定関数和同じ物被返す
RETURNF MC_PLAYER(ARG)


;-------------------------------------------------
;関数名:MV_PLAYER
;概　要:Ｖ爱抚者/爱抚物判定関数
;引　数:ARG…真那么爱抚している物但何であるか被返す
;            伪那么爱抚している人但誰な的か被返す。複数存在做场合是调教者优先
;戻り値:物的场合→0.无/1.口/2.手/3.阴茎/4.小穴/5.肛门/6.胸/7.足
;                 /10.腕/90.触手
;                 /100.跳蛋/101.震动棒/103.假阴茎/133.三角木马
;       人的场合→0.无/1.调教对象自身/2.调教者/4.助手1/8.助手2/16.助手３
;                 /64.道具/128.便所使用者
;備  考:式中関数
;仕大人被整理して作り直した、新しい爱抚者/爱抚物判定関数
;反应口上や绝顶口上で的使用被想定している
;-------------------------------------------------
@MV_PLAYER(ARG)
#FUNCTION
#LOCALSIZE 1
;男人是除く
SIF TALENT:MASTER:男人
	RETURNF 0

;TEQUIP根据判定1(优先度高)
;性交中(肛交除く)
IF TEQUIP:性交中 && TEQUIP:性交中 != 6
	;爱抚者判定
	SIF !ARG
		RETURNF 2
	;爱抚物判定
	RETURNF PENIS(TARGET) ? 3 # 103
ENDIF
;震动棒
SIF TEQUIP:震动棒 && ARG
	RETURNF 101
;假阴茎
SIF TEQUIP:假阴茎
	RETURNF MC_PLAYER(ARG)
;三角木马
SIF TEQUIP:三角木马 && ARG
	RETURNF 133

;调教者活动ション根据判定1(优先度高)
;拳交/两穴拳交
SIF IS_NOWACTNAME("拳交/两穴拳交")
	RETURNF ARG ? 10 # 2

;助手活动ション根据判定
FOR LOCAL, 1, 4
	;助手但いない场合是判定不做
	SIF ASSI:LOCAL < 0
		CONTINUE
	SELECTCASE TCVAR:(ASSI:LOCAL):助手ACT
		;手指插入
		CASE 30
			RETURNF ARG ? 2 # (2 << LOCAL)
		;震动棒
		CASE 31
			RETURNF ARG ? 101 # (2 << LOCAL)
		;性交
		CASE 30
			;爱抚者判定
			SIF !ARG
				RETURNF (2 << LOCAL)
			;爱抚物判定
			RETURNF PENIS(ASSI:LOCAL) ? 3 # 103
	ENDSELECT
NEXT

;调教者活动ション根据判定2(优先度低)
SELECTCASE TFLAG:ACT
	;震动棒
	CASE 21
		RETURNF ARG ? 101 # 2
	;指插入
	CASE 19
		RETURNF ARG ? 2 # 2
	;性交(肛交以外)(抵抗さ受到)
	CASE 30 TO 34
		;爱抚者判定
		SIF !ARG
			RETURNF 2
		;爱抚物判定
		RETURNF PENIS(TARGET) ? 3 # 103
	;三角木马
	CASE 67
		RETURNF ARG ? 133 # 2
	;公共肉便器PLAY
	CASE 74
		;便器和して使用さ受到场合
		SIF TFLAG:ACT派生 > 1
			RETURNF ARG ? 3 # 128
ENDSELECT

;COM根据判定
;移除道具で主动でＶ関係道具被解除している
SIF SELECTCOM == GETNUM(TRAINNAME, "移除道具") && TFLAG:REACT派生 == GETNUM(TEQUIP, "震动棒")
	RETURNF ARG ? 101 # 1
SIF SELECTCOM == GETNUM(TRAINNAME, "移除道具") && TFLAG:REACT派生 == GETNUM(TEQUIP, "假阴茎")
	RETURNF ARG ? 103 # 1

;TEQUIP根据判定2(优先度低)
;调教对象自慰中(快Ｖ被得ている)
SIF GETBIT(TEQUIP:自慰中, 0) && SOURCE:MASTER:快Ｖ > 0
	RETURNF ARG ? 2 # 1
;震动棒
SIF TEQUIP:震动棒
	RETURNF ARG ? 101 # 64


;-------------------------------------------------
;関数名:MA_PLAYER
;概　要:Ａ爱抚者/爱抚物判定関数
;引　数:ARG…真那么爱抚している物但何であるか被返す
;            伪那么爱抚している人但誰な的か被返す。複数存在做场合是调教者优先
;戻り値:物的场合→0.无/1.口/2.手/3.阴茎/4.小穴/5.肛门/6.胸/7.足
;                 /10.腕/90.触手
;                 /100.跳蛋/102.肛门拉珠/103.假阴茎/111.肛门震动棒/132.灌肠器＋肛塞
;       人的场合→0.无/1.调教对象自身/2.调教者/4.助手1/8.助手2/16.助手３
;                 /64.道具/128.便所使用者
;備  考:式中関数
;仕大人被整理して作り直した、新しい爱抚者/爱抚物判定関数
;反应口上や绝顶口上で的使用被想定している
;-------------------------------------------------
@MA_PLAYER(ARG)
#FUNCTION
#LOCALSIZE 1
;TEQUIP根据判定1(优先度高)
;肛交中
IF TEQUIP:性交中 == 6
	;爱抚者判定
	SIF !ARG
		RETURNF 2
	;爱抚物判定
	RETURNF PENIS(TARGET) ? 3 # 103
ENDIF
;肛门震动棒
SIF TEQUIP:肛门震动棒 && ARG
	RETURNF 111
;肛门拉珠
SIF TEQUIP:肛门拉珠 && ARG
	RETURNF 102
;灌肠器＋肛塞
SIF TEQUIP:灌肠器＋肛塞 && ARG
	RETURNF 132

;调教者活动ション根据判定1(优先度高)
SELECTCASE TFLAG:ACT
	;肛门爱抚
	CASE 13
		RETURNF ARG ? 2 # 2
	;舔肛门
	CASE 14
		RETURNF ARG ? 1 # 2
	;肛门拉珠
	CASE 23
		;ここで是解除时的处理的み
		SIF TFLAG:ACT派生 == 1
			RETURNF ARG ? 102 # 2
	;灌肠器＋肛塞
	CASE 68
		;ここで是解除时的处理的み
		SIF TFLAG:ACT派生 == 1
			RETURNF ARG ? 132 # 2
	;肛门拳交/两穴拳交
	CASE 71, 72
		RETURNF ARG ? 10 # 2
ENDSELECT

;助手活动ション根据判定
FOR LOCAL, 1, 4
	;助手但いない场合是判定不做
	SIF ASSI:LOCAL < 0
		CONTINUE
	SELECTCASE TCVAR:(ASSI:LOCAL):助手ACT
		;肛门爱抚
		CASE 10
			RETURNF ARG ? 2 # (2 << LOCAL)
		;舔肛门
		CASE 11
			RETURNF ARG ? 1 # (2 << LOCAL)
		;Ａ跳蛋
		CASE 12
			RETURNF ARG ? 100 # (2 << LOCAL)
		;肛门震动棒
		CASE 13
			RETURNF ARG ? 111 # (2 << LOCAL)
	ENDSELECT
NEXT

;调教者活动ション根据判定2(优先度低)
SELECTCASE TFLAG:ACT
	;肛门震动棒
	CASE 22
		RETURNF ARG ? 111 # 2
	;肛门拉珠
	CASE 23
		RETURNF ARG ? 102 # 2
	;肛交(抵抗さ受到)
	CASE 35
		;爱抚者判定
		SIF !ARG
			RETURNF 2
		;爱抚物判定
		RETURNF PENIS(TARGET) ? 3 # 103
	;灌肠器＋肛塞
	CASE 68
		RETURNF ARG ? 132 # 2
	;公共肉便器PLAY
	CASE 74
		;便器和して使用さ受到场合
		SIF TFLAG:ACT派生 > 1
			RETURNF ARG ? 3 # 128
ENDSELECT

;COM根据判定
;移除道具で主动でＡ関係道具被解除している
SIF SELECTCOM == GETNUM(TRAINNAME, "移除道具") && TFLAG:REACT派生 == GETNUM(TEQUIP, "肛门震动棒")
	RETURNF ARG ? 111 # 1
SIF SELECTCOM == GETNUM(TRAINNAME, "移除道具") && TFLAG:REACT派生 == GETNUM(TEQUIP, "肛门拉珠")
	RETURNF ARG ? 102 # 1
SIF SELECTCOM == GETNUM(TRAINNAME, "移除道具") && TFLAG:REACT派生 == GETNUM(TEQUIP, "灌肠器＋肛塞")
	RETURNF ARG ? 132 # 1

;TEQUIP根据判定2(优先度低)
;调教对象自慰中(快Ａ被得ている)
SIF GETBIT(TEQUIP:自慰中, 0) && SOURCE:MASTER:快Ａ > 0
	RETURNF ARG ? 111 # 1
;肛门震动棒
SIF TEQUIP:肛门震动棒
	RETURNF ARG ? 111 # 64
;肛门拉珠
SIF TEQUIP:肛门拉珠
	RETURNF ARG ? 102 # 64
;灌肠器＋肛塞
SIF TEQUIP:灌肠器＋肛塞
	RETURNF ARG ? 132 # 64


;-------------------------------------------------
;関数名:MB_PLAYER
;概　要:Ｂ爱抚者/爱抚物判定関数
;引　数:ARG…真那么爱抚している物但何であるか被返す
;            伪那么爱抚している人但誰な的か被返す。複数存在做场合是调教者优先
;戻り値:物的场合→0.无/1.口/2.手/3.阴茎/4.小穴/5.肛门/6.胸/7.足
;                 /90.触手
;                 /100.跳蛋/113.乳头夹
;       人的场合→0.无/1.调教对象自身/2.调教者/4.助手1/8.助手2/16.助手３
;                 /64.道具/128.便所使用者
;備  考:式中関数
;仕大人被整理して作り直した、新しい爱抚者/爱抚物判定関数
;反应口上や绝顶口上で的使用被想定している
;-------------------------------------------------
@MB_PLAYER(ARG)
#FUNCTION
#LOCALSIZE 1
;TEQUIP根据判定1(优先度高)
;乳头夹
SIF TEQUIP:乳头夹 && ARG
	RETURNF 113

;调教者活动ション根据判定1(优先度高)
SELECTCASE TFLAG:ACT
	;用手爱抚
	CASE 10
		;爱抚
		SIF !TFLAG:ACT派生
			RETURNF ARG ? 2 # 2
		RETURNF 
	;胸爱抚
	CASE 11
		;爱抚者判定
		SIF !ARG
			RETURNF 2
		;爱抚物判定(吸乳头的场合是口)
		RETURNF TFLAG:ACT派生 == 1 ? 1 # 2
	;背面座位
	CASE 34
		RETURNF ARG ? 2 # 2
	;乳交
	CASE 52
		;命令に从った场合的み
		SIF !IS_COMGRONAME("拒绝")
			RETURNF ARG ? 3 # 2
ENDSELECT

;助手活动ション根据判定
FOR LOCAL, 1, 4
	;助手但いない场合是判定不做
	SIF ASSI:LOCAL < 0
		CONTINUE
	SELECTCASE TCVAR:(ASSI:LOCAL):助手ACT
		;胸爱抚
		CASE 20
			RETURNF ARG ? 2 # (2 << LOCAL)
		;舔乳头
		CASE 21
			RETURNF ARG ? 1 # (2 << LOCAL)
		;玩弄乳头
		CASE 22
			RETURNF ARG ? 100 # (2 << LOCAL)
	ENDSELECT
NEXT

;调教者活动ション根据判定2(优先度低)
SELECTCASE TFLAG:ACT
	;乳头夹
	CASE 25
		RETURNF ARG ? 113 # 2
	;公共肉便器PLAY
	CASE 74
		;便器和して使用さ受到(男人是除く)
		SIF TFLAG:ACT派生 > 1 &&  !TALENT:MASTER:男人
			RETURNF ARG ? 3 # 128
ENDSELECT

;COM根据判定
;移除道具で主动でＢ関係道具被解除している
SIF SELECTCOM == GETNUM(TRAINNAME, "移除道具") && TFLAG:REACT派生 == GETNUM(TEQUIP, "乳头夹")
	RETURNF ARG ? 113 # 1

;TEQUIP根据判定2(优先度低)
;调教对象自慰中(男人以外、快Ｂ被得ている)
SIF GETBIT(TEQUIP:自慰中, 0) && !TALENT:MASTER:男人 && SOURCE:MASTER:快Ｂ > 0
	RETURNF ARG ? 2 # 1
;乳头夹
SIF TEQUIP:乳头夹
	RETURNF ARG ? 113 # 64

;-------------------------------------------------
;関数名:MASTER_EX
;概　要:调教对象绝顶判定関数
;引　数:ARG:0…爱抚者→1.调教对象自身/2.调教者/4.助手１/8.助手２/16.助手３
;                      /64.道具/128.便所使用者　省略做和爱抚者被問わない
;              BIT和で的指定但可能
;       ARG:1…(省略可)绝顶的种別→1.Ｃ绝顶/2.Ｖ绝顶/3.Ａ绝顶/4.Ｂ绝顶
;                                  /11.喷乳/12.射精/13.放尿
;戻り値:本次的指令で、指定した爱抚者但调教者被指定的绝顶に導いたかどうか
;       ARG:1被省略した场合是全部的绝顶关于BITで返す
;       (1.Ｃ绝顶/2.Ｖ绝顶/4.Ａ绝顶/8.Ｂ绝顶/1024.喷乳/2048.射精/4096.放尿)
;備  考:式中関数
;仕大人被整理して作り直した、新しい绝顶判定関数
;反应口上や绝顶口上で的使用被想定している
;-------------------------------------------------
@MASTER_EX(ARG:0 = -1, ARG:1)
#FUNCTION
#LOCALSIZE 5
LOCAL:0 = 0
LOCAL:1 = MC_PLAYER(0)
LOCAL:2 = MV_PLAYER(0)
LOCAL:3 = MA_PLAYER(0)
LOCAL:4 = MB_PLAYER(0)

LOCAL:0 |= NOWEX:MASTER:Ｃ绝顶 && (ARG == -1 || (LOCAL:1 & ARG:0)) ? 1 # 0
LOCAL:0 |= NOWEX:MASTER:Ｖ绝顶 && (ARG == -1 || (LOCAL:2 & ARG:0)) ? 2 # 0
LOCAL:0 |= NOWEX:MASTER:Ａ绝顶 && (ARG == -1 || (LOCAL:3 & ARG:0)) ? 4 # 0
LOCAL:0 |= NOWEX:MASTER:Ｂ绝顶 && (ARG == -1 || (LOCAL:4 & ARG:0)) ? 8 # 0

;喷乳做に是Ｂ绝顶→Ｂ爱抚者
LOCAL:0 |= NOWEX:MASTER:喷乳 && (ARG == -1 || (LOCAL:4 & ARG:0)) ? 1024 # 0

;射精做に是
;・射精/大量射精/空射精	：Ｃ绝顶→Ｃ爱抚者
;・早泄					：快Ｃ　→Ｃ爱抚者
;・Ａ虐待射精精				：Ａ绝顶→Ａ爱抚者
;※Ａ虐待射精精不是射精是Ｃ爱抚者、Ａ虐待射精精是Ａ爱抚者　扶她是Ｖ
LOCAL:0 |= (NOWEX:MASTER:射精 && NOWEX:MASTER:射精 != 5 && (ARG == -1 || (LOCAL:1 & ARG:0))) || (TALENT:MASTER:男人 && NOWEX:MASTER:射精 == 5 && (ARG == -1 || (LOCAL:3 & ARG:0))) || (TALENT:MASTER:扶她 && NOWEX:MASTER:射精 == 5 && (ARG == -1 || (LOCAL:1 & ARG:0))) ? 2048 # 0

;放尿做に是Ｃ绝顶or射精(空以外)or快Ｃ但必要
;(注意：媚药や营养剤でもゲージ増加做但、快ＣＶＡＢ的いずれか但无い和失禁判定自体されない)
;・Ｃ绝顶		：Ｃ爱抚者
;・射精(空以外)	：被刺激到射精者
;・快Ｃ			：Ｃ爱抚者
;※Ａ虐待射精精でＡ爱抚者的方但优先那么Ａ爱抚者、それ以外是Ｃ爱抚者。优先顺位是爱抚者的数字的低顺
;※Ｃ爱抚者被见るべき和きにＣ爱抚者但いない场合：媚药や营养剤的せいな的で、ＶＡＢ被优先顺位顺で见る
IF NOWEX:MASTER:射精 == 5 && (!LOCAL:1 || LOCAL:1 > LOCAL:3)
	LOCAL:0 |= NOWEX:MASTER:放尿 && (ARG == -1 || (LOCAL:3 & ARG:0)) ? 4096 # 0
ELSEIF LOCAL:1
	LOCAL:0 |= NOWEX:MASTER:放尿 && (ARG == -1 || (LOCAL:1 & ARG:0)) ? 4096 # 0
ELSEIF LOCAL:2 && (!LOCAL:3 || LOCAL:2 < LOCAL:3) && (!LOCAL:4 || LOCAL:2 < LOCAL:4)
	LOCAL:0 |= NOWEX:MASTER:放尿 && (ARG == -1 || (LOCAL:2 & ARG:0)) ? 4096 # 0
ELSEIF LOCAL:3 && (!LOCAL:4 || LOCAL:3 < LOCAL:4)
	LOCAL:0 |= NOWEX:MASTER:放尿 && (ARG == -1 || (LOCAL:3 & ARG:0)) ? 4096 # 0
ELSE
	LOCAL:0 |= NOWEX:MASTER:放尿 && (ARG == -1 || (LOCAL:4 & ARG:0)) ? 4096 # 0
ENDIF

RETURNF ARG:1 ? GETBIT(LOCAL:0, ARG:1 - 1) # LOCAL:0

;-------------------------------------------------
;関数名:TC_PLAYER
;概　要:ｃ爱抚者/爱抚物判定関数
;引　数:ARG…真那么爱抚している物但何であるか被返す
;            伪那么爱抚している人但誰な的か被返す。複数存在做场合是调教对象优先
;戻り値:物的场合→0.无/1.口/2.手/3.阴茎/4.小穴/5.肛门/6.胸/7.足/8.小穴口/103.假阴茎
;       人的场合→0.无/1.调教对象/2.调教者自身/4.助手1/8.助手2/16.助手３/64.道具
;備  考:式中関数
;仕大人被整理して作り直した、新しい爱抚者/爱抚物判定関数
;反应口上や绝顶口上で的使用被想定している
;-------------------------------------------------
@TC_PLAYER(ARG)
#FUNCTION
;TEQUIP根据判定1(优先度高)
;肛交(调教者に阴茎但ある场合的み)
SIF PENIS(TARGET) && TEQUIP:性交中 == 6
	RETURNF ARG ? 5 # 1
;性交中(肛交以外)(调教者に阴茎但ある场合的み)
SIF PENIS(TARGET) && TEQUIP:性交中
	RETURNF ARG ? 4 # 1
;顔面骑乘
SIF TEQUIP:顔面骑乘 && !TEQUIP:顔面骑乘肛门
	RETURNF ARG ? 1 # 1
;调教者假阴茎
SIF TEQUIP:调教者假阴茎 && ARG
	RETURNF 103

;调教者活动ション根据判定1(优先度高)
SELECTCASE TFLAG:ACT
	;摩擦性器
	CASE 16
		;爱抚者判定
		SIF !ARG
			RETURNF 1
		;爱抚物判定
		RETURNF PENIS(MASTER) ? 3 # 8
	;强制手爱抚
	CASE 50
		;奉仕した场合的み
		SIF !IS_COMGRONAME("拒绝")
			RETURNF ARG ? 2 # 1
	;强制口爱抚
	CASE 51
		;顔面骑乘/顔面骑乘肛门以外、かつ奉仕した场合的み
		SIF TFLAG:ACT派生 != 2 && TFLAG:ACT派生 != 3 && !IS_COMGRONAME("拒绝")
			RETURNF ARG ? 1 # 1
	;乳交
	CASE 52
		;奉仕した场合的み
		SIF !IS_COMGRONAME("拒绝")
			RETURNF ARG ? 6 # 1
	;素股
	CASE 53
		;奉仕した场合的み
		SIF !IS_COMGRONAME("拒绝")
			RETURNF ARG ? 8 # 1
	;深喉
	CASE 56
		RETURNF ARG ? 1 # 1
	;对面座位侍奉/背面座位侍奉
	CASE 97, 98
		RETURNF ARG ? 2 # 1
ENDSELECT

;调教者追加活动ション根据判定
;助手活动ション根据判定

;调教者活动ション根据判定2(优先度低)
SELECTCASE TFLAG:ACT
	;性交(肛交以外)(抵抗さ受到)
	CASE 30 TO 34
		RETURNF ARG ? 4 # 1
	;肛交(抵抗さ受到)
	CASE 35
		RETURNF ARG ? 5 # 1
	;强制口爱抚
	CASE 51
		;顔面骑乘(抵抗さ受到)
		SIF TFLAG:ACT派生 == 2
			RETURNF ARG ? 1 # 1
ENDSELECT

;TEQUIP根据判定2(优先度低)
;调教对象自慰中(快ｃ被得ている)
SIF GETBIT(TEQUIP:自慰中, 1) && SOURCE:快Ｃ > 0
	RETURNF ARG ? 2 # 2


;-------------------------------------------------
;関数名:TE_PLAYER
;概　要:调教者射精对象人物/物体判定関数
;引　数:ARG…真那么射精对象物体但何であるか被返す
;            伪那么射精对象人物但誰な的か被返す。複数存在做场合是调教对象优先
;戻り値:物的场合→0.无/1.口/2.手/3.阴茎/4.小穴/5.肛门/6.胸/7.足/9.顔
;                /90.触手
;                /100.跳蛋/112.阴蒂夹/114.飞机杯
;       人的场合→0.无/1.调教对象/2.调教者自身/4.助手1/8.助手2/16.助手３
;                /64.道具/128.便所使用者
;備  考:式中関数
;
;ME_PLAYER和同时に设置@これみ
;-------------------------------------------------
@TE_PLAYER(ARG)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
;非射精时0被返す
SIF !NOWEX:射精
	RETURNF 0
;自慰射精时
IF GETBIT(TEQUIP:自慰中, 1)
	;1/2的确率で自慰顔射射精
	IF LOCALS != SAME_TURN()
		LOCALS = SAME_TURN()
		LOCAL = RAND:2
	ENDIF
	;自慰顔射射精时以外是特別な处理被行わない
	;自慰中でも、奉仕・爱抚・性交・性交奉仕・道具ACT的際是顔射に无理但ある的で行わない
	SIF LOCAL && !GROUPMATCH(GET_ACTM(TFLAG:ACT), 1, 2, 3, 5, 10)
		RETURNF ARG ? 9 # 1
ENDIF
;其他的场合是Ｃ爱抚者/爱抚物判定関数和同じ物被返す
RETURNF TC_PLAYER(ARG)


;-------------------------------------------------
;関数名:TV_PLAYER
;概　要:ｖ爱抚者/爱抚物判定関数
;引　数:ARG…真那么爱抚している物但何であるか被返す
;            伪那么爱抚している人但誰な的か被返す。複数存在做场合是调教对象优先
;戻り値:物的场合→0.无/1.口/2.手/3.阴茎/4.小穴/5.肛门/6.胸/7.足
;                 /101.震动棒/103.假阴茎
;       人的场合→0.无/1.调教对象/2.调教者自身/4.助手1/8.助手2/16.助手３/64.道具
;備  考:式中関数
;仕大人被整理して作り直した、新しい爱抚者/爱抚物判定関数
;反应口上や绝顶口上で的使用被想定している
;-------------------------------------------------
@TV_PLAYER(ARG)
#FUNCTION
;男人是除く
SIF TALENT:TARGET:男人
	RETURNF 0

;TEQUIP根据判定1(优先度高)
;性交奉仕中(肛交侍奉以外)
IF TEQUIP:性交奉仕中 && TEQUIP:性交奉仕中 != 6
	SIF TEQUIP:假阴茎
		RETURNF ARG ? 103 # 1
	RETURNF ARG ? 3 # 1
ENDIF

;调教者假阴茎
SIF TEQUIP:调教者假阴茎
	RETURNF TC_PLAYER(ARG)
;调教者活动ション根据判定1(优先度高)
;调教者追加活动ション根据判定
;助手活动ション根据判定

;调教者活动ション根据判定2(优先度低)
SELECTCASE TFLAG:ACT
	;性交奉仕(肛交侍奉以外)(抵抗さ受到)
	CASE 103, 95 TO 98
		SIF TEQUIP:假阴茎
			RETURNF ARG ? 103 # 1
		RETURNF ARG ? 3 # 1
ENDSELECT

;TEQUIP根据判定2(优先度低)
;调教者自慰中(快ｖ被得ている)
SIF GETBIT(TEQUIP:自慰中, 1) && SOURCE:快Ｖ > 0
	RETURNF ARG ? 101 # 2

;-------------------------------------------------
;関数名:TA_PLAYER
;概　要:ａ爱抚者/爱抚物判定関数
;引　数:ARG…真那么爱抚している物但何であるか被返す
;            伪那么爱抚している人但誰な的か被返す。複数存在做场合是调教对象优先
;戻り値:物的场合→0.无/1.口/2.手/3.阴茎/4.小穴/5.肛门/6.胸/7.足
;       人的场合→0.无/1.调教对象/2.调教者自身/4.助手1/8.助手2/16.助手３
;備  考:式中関数
;仕大人被整理して作り直した、新しい爱抚者/爱抚物判定関数
;反应口上や绝顶口上で的使用被想定している
;-------------------------------------------------
@TA_PLAYER(ARG)
#FUNCTION
;TEQUIP根据判定1(优先度高)
;顔面骑乘肛门
SIF TEQUIP:顔面骑乘肛门
	RETURNF ARG ? 1 # 1
;肛交侍奉
SIF TEQUIP:性交奉仕中 == 6
	RETURNF ARG ? 3 # 1

;调教者活动ション根据判定1(优先度高)
;调教者追加活动ション根据判定
;助手活动ション根据判定

;调教者活动ション根据判定2(优先度低)
SELECTCASE TFLAG:ACT
	;强制口爱抚
	CASE 51
		;顔面骑乘肛门(抵抗さ受到)
		SIF TFLAG:ACT派生 == 3
			RETURNF ARG ? 1 # 1
	;肛交侍奉(抵抗さ受到)
	CASE 99
		RETURNF ARG ? 3 # 1
ENDSELECT

;TEQUIP根据判定2(优先度低)

;-------------------------------------------------
;関数名:TB_PLAYER
;概　要:ｂ爱抚者/爱抚物判定関数
;引　数:ARG…真那么爱抚している物但何であるか被返す
;            伪那么爱抚している人但誰な的か被返す。複数存在做场合是调教对象优先
;戻り値:物的场合→0.无/1.口/2.手/3.阴茎/4.小穴/5.肛门/6.胸/7.足
;       人的场合→0.无/1.调教对象/2.调教者自身/4.助手１/8.助手２/16.助手３
;備  考:式中関数
;仕大人被整理して作り直した、新しい爱抚者/爱抚物判定関数
;反应口上や绝顶口上で的使用被想定している
;-------------------------------------------------
@TB_PLAYER(ARG)
#FUNCTION
;TEQUIP根据判定1(优先度高)

;调教者活动ション根据判定1(优先度高)
SELECTCASE TFLAG:ACT
	;胸爱抚
	CASE 11
		;揉み合いで、消极的服从/积极的服从的场合
		SIF TFLAG:ACT派生 == 2 && (IS_COMGRONAME("消极的服从/积极的服从"))
			RETURNF ARG ? 2 # 1
	;进行乳交
	CASE 17
		RETURNF ARG ? 3 # 1
	;强制胸爱抚
	CASE 57
		;奉仕した场合
		IF !IS_COMGRONAME("拒绝")
			;爱抚者判定
			SIF !ARG
				RETURNF 1
			;爱抚物判定(口爱抚是口)
			RETURNF TFLAG:ACT派生 ? 1 # 2
		ENDIF
	;背面座位侍奉
	CASE 98
		RETURNF ARG ? 2 # 1
ENDSELECT

;调教者追加活动ション根据判定
;助手活动ション根据判定
;调教者活动ション根据判定2(优先度低)

;TEQUIP根据判定2(优先度低)
;调教者自慰中(男人以外、快ｂ被得ている)
SIF GETBIT(TEQUIP:自慰中, 1) && !TALENT:TARGET:男人 && SOURCE:快Ｖ > 0
	RETURNF ARG ? 2 # 2

;-------------------------------------------------
;関数名:TARGET_EX
;概　要:调教者绝顶判定関数
;引　数:ARG:0…爱抚者→1.调教对象/2.调教者自身/4.助手１/8.助手２/16.助手３
;              BIT和で的指定但可能。省略做和爱抚者被問わない
;       ARG:1…(省略可)绝顶的种別→1.ｃ绝顶/2.ｖ绝顶/3.ａ绝顶/4.ｂ绝顶
;                                  /11.喷乳/12.射精
;戻り値:本次的指令で、指定した爱抚者但调教者被指定的绝顶に導いたかどうか
;       ARG:1被省略した场合是全部的绝顶关于BITで返す
;       (1.ｃ绝顶/2.ｖ绝顶/4.ａ绝顶/8.ｂ绝顶/1024.喷乳/2048.射精)
;備  考:式中関数
;仕大人被整理して作り直した、新しい绝顶判定関数
;实是现状で有效に使える场面但あまり无いかもしれない
;反应口上や绝顶口上で的使用被想定している
;-------------------------------------------------
@TARGET_EX(ARG:0 = -1, ARG:1)
#FUNCTION
#LOCALSIZE 5
LOCAL:0 = 0
LOCAL:1 = TC_PLAYER(0)
LOCAL:2 = TV_PLAYER(0)
LOCAL:3 = TA_PLAYER(0)
LOCAL:4 = TB_PLAYER(0)

LOCAL:0 |= NOWEX:Ｃ绝顶 && (ARG == -1 || (LOCAL:1 & ARG)) ? 1 # 0
LOCAL:0 |= NOWEX:Ｖ绝顶 && (ARG == -1 || (LOCAL:2 & ARG)) ? 2 # 0
LOCAL:0 |= NOWEX:Ａ绝顶 && (ARG == -1 || (LOCAL:3 & ARG)) ? 4 # 0
LOCAL:0 |= NOWEX:Ｂ绝顶 && (ARG == -1 || (LOCAL:4 & ARG)) ? 8 # 0

;喷乳做に是ｂ绝顶→ｂ爱抚者
LOCAL:0 |= NOWEX:喷乳 && (ARG == -1 || (LOCAL:4 & ARG)) ? 1024 # 0
;射精做に是ｃ绝顶→ｃ爱抚者
LOCAL:0 |= NOWEX:射精 && (ARG == -1 || (LOCAL:1 & ARG)) ? 2048 # 0

RETURNF ARG:1 ? GETBIT(LOCAL:0, ARG:1 - 1) # LOCAL:0

;-------------------------------------------------
;行动名显示関数@ACTSTR
;式中で使用做関数です。引数番号的调教者活动ション名被字符串で返し增加
;引数1但0だ和心理创伤的种用に、「灌肠器＋肛塞」など无机なも的是有机的な呼称に直し增加
;
;なん和なく追加したけどステキな表现但出来なかった…ごめんよ…
;こ的手的関数是必要那么角色別に持つこ和も検討した方但いいよ！
;通用的に是したつもり只ど、角色や性格によって口调に合う单語・合わない单語ってあるよね
;-------------------------------------------------
@ACTSTR(ARG:0, ARG:1)
#FUNCTIONS
IF !ARG:1
	SELECTCASE ARG:0
		;询问今天的心情/询问对自己的感觉/征求同意
		CASE 0, 1, 6
			RETURNF "变な質問"
		;谈论性的话题
		CASE 2
			RETURNF \@ TALENT:TARGET:好色 || TALENT:TARGET:坏心眼 ? いやらしい # エッチな \@ + "话"
		;温柔的安慰
		CASE 3
			RETURNF "気休め"
		;严厉的威胁/大喊大叫/令人毛骨悚然的笑
		CASE 4, 8, 9
			RETURNF "脅かされる的"
		;请求变更服装
		CASE 5
			RETURNF "脱但される的"
		;宣称要进行惩罚
		CASE 7
			RETURNF "惩罚"
		;用手爱抚/胸爱抚/摩擦性器/进行乳交
		CASE 10, 11, 16, 17
			RETURNF "变な触り方される的"
		;进行足交
		CASE 18
			RETURNF "用脚踏まれる的"
		;指插入
		CASE 19
			RETURNF "指で掻き回される的"
		;用口爱抚
		CASE 12
			RETURNF \@ PENIS(MASTER) ? 口交 # 用口 \@ + "される的"
		;肛门爱抚/舔肛门
		CASE 13, 14
			RETURNF "おしり" + \@ IS_NOWACTNAME("肛门爱抚") ? 弄 # 舐め \@ + "られる的"
		;接吻
		CASE 15
			RETURNF "接吻"
		;Ｃ道具爱抚
		CASE 24
			RETURNF \@ PENIS(MASTER) ? オナホ # 阴蒂夹 \@
		;推倒
		CASE 27
			RETURNF "押し倒される的"
		;正常位/对面座位/逆强奸
		CASE 30, 33, 103
			RETURNF "犯される的"
		;后背位/背面座位
		CASE 31, 34
			RETURNF "后ろから犯される的"
		;肛交
		CASE 35
			RETURNF "后ろ被犯される的"
		;自慰
		CASE 40
			RETURNF "オナニーさせられる的"
		;打开小穴
		CASE 41
			RETURNF "広げられる的"
		;展示自慰
		CASE 42
			RETURNF "オナニー看到られる的"
		;羞耻PLAY
		CASE 43
			RETURNF "镜に映される的"
		;野外PLAY
		CASE 44
			RETURNF "外に连れ出される的"
		;放尿
		CASE 45
			RETURNF "漏らす的"
		;强制手爱抚
		CASE 50
			RETURNF "触る的"
		;强制口爱抚
		CASE 51
			RETURNF "舐めさせられる的"
		;强制舔足
		CASE 55
			RETURNF "舔足させられる的"
		;强制胸爱抚
		CASE 57
			RETURNF "乳房"
		;打屁股
		CASE 60
			RETURNF "叩かれる的"
		;绳
		CASE 63
			RETURNF "縛られる的"
		;眼罩
		CASE 64
			RETURNF "眼罩しされる的"
		;辱骂
		CASE 66
			RETURNF "言葉責めされる的"
		;灌肠套装
		CASE 68
			RETURNF "灌肠される的"
		;拳交/肛门拳交/两穴拳交
		CASE 70 TO 72
			RETURNF "腕被入れる的"
		;公共肉便器PLAY
		CASE 74
			RETURNF "トイレになる的"
		;触手召唤		未实装
		;CASE 80
		;	RETURNF "触手"
		;稍作休息
		CASE 90
			RETURNF "休息"
		;喝营养剂
		CASE 91
			RETURNF "药被飲まされる的"
		;进行治疗
		CASE 92
			RETURNF "变な治し方される的"
		;正常位侍奉/后背位侍奉/对面座位侍奉/背面座位侍奉
		CASE 95 TO 98
			RETURNF "性交させられる的"
		;肛交侍奉
		CASE 99
			RETURNF "后ろに入れさせられる的"
		;其他
		CASEELSE
			RETURNF STR:(CFLAG:MASTER:心理创伤)
	ENDSELECT
ENDIF
RETURNF STR:(ARG + 100)

;-------------------------------------------------
;関数名:EXP_INSUFF
;概　要:経験不足チェック関数
;引　数:ARG…行為者（TARGETかASSI的誰か）
;戻り値:経験不足等级。0那么経験十分
;備　考:式中関数
;経験不足的场合、戻り値但真（不足等级に应じて1～4直到）になる
;だいぶ面倒なつくりしてるけどいいやこ的ま直到…
;-------------------------------------------------
@EXP_INSUFF(ARG = -1)
#FUNCTION
#LOCALSIZE 2
;引数不正那么返回
SIF ARG < 0
	RETURNF 0

;経験判定パ回合(BIT和)
;(1p0:V経験, 1p1:A経験, 1p2:喷乳経験, 1p3:V性交, 1p4:A性交, 1p5:逆ﾚｲﾌﾟ, 1p6:Vﾌｨｽﾄ, 1p7:Aﾌｨｽﾄ)
LOCAL:0 = 0

;経験不足等级(BIT和)
;(0~3bit:V経験不足Lv, 4~7bit:A経験不足Lv, 8~11bit:逆ﾚｲﾌﾟ経験不足Lv)
;指数メモ(p0:V経験不足Lv, p4:A経験不足Lv, p8:逆ﾚｲﾌﾟ経験不足Lv)
LOCAL:1 = 0

;调教者根据PLAY被判定
IF ARG == TARGET
	;ACT的判定
	SELECTCASE TFLAG:ACT
		;Ａ経験チェック
		;肛门爱抚, 舔肛门, 肛门拉珠
		CASE 13, 14, 23
			LOCAL:0 |= 1p1
		;Ｖ性交経験チェック
		;正常位, 后背位, 骑乘位, 对面座位, 背面座位, 骑乘二穴責め, 对面二穴責め, 特别版牡丹乱舞, 谷间强渡
		CASE 30 TO 34, 306 TO 308, 311
			LOCAL:0 |= 1p3
		;Ａ性交経験チェック
		;肛交, 肛门二穴責め
		CASE 35, 309
			LOCAL:0 |= 1p4
		;性交経験チェック
		;正常位侍奉～逆强奸、肛交侍奉、Ｗ逆强奸
		CASE 95 TO 103, 310
			LOCAL:0 |= 1p5
		;Ｖ扩张経験チェック
		;拳交
		CASE 70
			LOCAL:0 |= 1p6
		;Ａ扩张経験チェック
		;肛门拳交
		CASE 71
			LOCAL:0 |= 1p7
		;Ｖ扩张経験チェック＋Ａ扩张経験チェック
		;两穴拳交
		CASE 72
			LOCAL:0 |= 1p6 | 1p7
		;道具装着时的みＶ経験チェック
		;震动棒
		CASE 21
			;派生(装着)
			SIF TFLAG:ACT派生 == 0
				LOCAL:0 |= 1p0
		;道具装着时的みＡ経験チェック
		;肛门震动棒, 灌肠器＋肛塞
		CASE 22, 68
			;派生(装着)
			SIF TFLAG:ACT派生 == 0
				LOCAL:0 |= 1p1
	ENDSELECT
	;追加行动的判定
	SELECTCASE TFLAG:追加行动
		;Ｖ経験チェック
		CASE 2
			LOCAL:0 |= 1p0
		;Ａ経験チェック
		CASE 3
			LOCAL:0 |= 1p1
	ENDSELECT
;助手根据PLAY被判定
ELSE
	;助手ACT的判定
	SELECTCASE NOWASSIACTNAME(ARG)
		CASE "逆强奸"
			;性交奉仕経験チェック
			LOCAL:0 |= 1p5
		CASE "肛门爱抚", "舔肛门", "肛门跳蛋", "肛门震动棒"
			;Ａ経験チェック
			LOCAL:0 |= 1p1
		CASE "性交"
			;Ｖ性交経験チェック
			LOCAL:0 |= 1p3
		CASE "手指插入", "震动棒"
			;Ｖ経験チェック
			LOCAL:0 |= 1p0
	ENDSELECT
	;コンビネーション的判定
	SELECTCASE TFLAG:ACT
		;Ａ性交経験チェック
		;骑乘二穴責め, 对面二穴責め
		CASE 306, 307
			LOCAL:0 |= 1p4
		;Ｖ性交経験チェック
		;肛门二穴責め
		CASE 309
			LOCAL:0 |= 1p3
		;性交経験チェック
		;Ｗ逆强奸
		CASE 310
			LOCAL:0 |= 1p5
	ENDSELECT
ENDIF

;1p0:Ｖ経験チェック
;奴隶的Ｖ経験被判定→Ｖ経験不足和して扱う(p0)
IF LOCAL:0 & 1p0
	IF EXP:MASTER:Ｖ経験 < 1
		LOCAL:1 |= 3p0
	ELSEIF EXP:MASTER:Ｖ経験 < 4
		LOCAL:1 |= 2p0
	ELSEIF EXP:MASTER:Ｖ経験 < 6
		LOCAL:1 |= 1p0
	ENDIF
ENDIF

;1p1:Ａ経験チェック
;奴隶的Ａ経験被判定→Ａ経験不足和して扱う(p4)
IF LOCAL:0 & 1p1
	IF EXP:MASTER:Ａ経験 < 1
		LOCAL:1 |= 4p4
	ELSEIF EXP:MASTER:Ａ経験 < 6
		LOCAL:1 |= 3p4
	ELSEIF EXP:MASTER:Ａ経験 < 11
		LOCAL:1 |= 2p4
	ELSEIF EXP:MASTER:Ａ経験 < 21
		LOCAL:1 |= 1p4
	ENDIF
ENDIF

;1p3:Ｖ性交経験チェック
;奴隶的Ｖ経験和性交経験被判定→Ｖ経験不足和して扱う(p0)
IF LOCAL:0 & 1p3
	IF EXP:MASTER:Ｖ経験 < 1
		LOCAL:1 |= 3p0
	ELSEIF EXP:MASTER:Ｖ経験 < 6 && EXP:MASTER:性交経験 < 5
		LOCAL:1 |= 2p0
	ELSEIF EXP:MASTER:Ｖ経験 < 11 && EXP:MASTER:性交経験 < 9
		LOCAL:1 |= 1p0
	ENDIF
ENDIF

;1p4:Ａ性交経験チェック
;奴隶的Ａ経験和性交経験被判定→Ａ経験不足和して扱う(p4)
IF LOCAL:0 & 1p4
	IF EXP:MASTER:Ａ経験 < 1
		LOCAL:1 |= 4p4
	ELSEIF EXP:MASTER:Ａ経験 < 6 && EXP:MASTER:性交経験 < 11
		LOCAL:1 |= 3p4
	ELSEIF EXP:MASTER:Ａ経験 < 13 && EXP:MASTER:性交経験 < 21
		LOCAL:1 |= 2p4
	ELSEIF EXP:MASTER:Ａ経験 < 26 && EXP:MASTER:性交経験 < 31
		LOCAL:1 |= 1p4
	ENDIF
ENDIF

;1p5:逆强奸経験チェック
;奴隶的性交経験和调教等级被判定→逆强奸経験不足和して扱う(p8)
IF LOCAL:0 & 1p5
	IF EXP:MASTER:性交経験 < 1 && CFLAG:MASTER:调教等级 < 3
		LOCAL:1 |= 4p8
	ELSEIF EXP:MASTER:性交経験 < 6 && CFLAG:MASTER:调教等级 < 5
		LOCAL:1 |= 3p8
	ELSEIF EXP:MASTER:性交経験 < 13 && CFLAG:MASTER:调教等级 < 8
		LOCAL:1 |= 2p8
	ELSEIF EXP:MASTER:性交経験 < 26 && CFLAG:MASTER:调教等级 < 11
		LOCAL:1 |= 1p8
	ENDIF
ENDIF

;1p6:Ｖ扩张経験チェック(拳交)
;奴隶的Ｖ経験和Ｖ扩张経験被判定→Ｖ経験不足和して扱う(p0)
IF LOCAL:0 & 1p6
	IF EXP:MASTER:Ｖ経験 < 20 || EXP:MASTER:Ｖ扩张経験 < 1
		LOCAL:1 |= 4p0
	ELSEIF EXP:MASTER:Ｖ経験 < 25 || EXP:MASTER:Ｖ扩张経験 < 2
		LOCAL:1 |= 3p0
	ELSEIF EXP:MASTER:Ｖ経験 < 30 || EXP:MASTER:Ｖ扩张経験 < 4
		LOCAL:1 |= 2p0
	ELSEIF EXP:MASTER:Ｖ扩张経験 < 6
		LOCAL:1 |= 1p0
	ENDIF
ENDIF

;1p7:Ａ扩张経験チェック(肛门拳交)
;奴隶的Ａ経験和Ａ扩张経験被判定→Ａ経験不足和して扱う(p4)
IF LOCAL:0 & 1p7
	IF EXP:MASTER:Ａ経験 < 20 || EXP:MASTER:Ａ扩张経験 < 1
		LOCAL:1 |= 4p4
	ELSEIF EXP:MASTER:Ａ経験 < 25 || EXP:MASTER:Ａ扩张経験 < 2
		LOCAL:1 |= 3p4
	ELSEIF EXP:MASTER:Ａ経験 < 30 || EXP:MASTER:Ａ扩张経験 < 4
		LOCAL:1 |= 2p4
	ELSEIF EXP:MASTER:Ａ扩张経験 < 6
		LOCAL:1 |= 1p4
	ENDIF
ENDIF

;経験不足等级被返す
RETURNF MAX(COUNTBIT(LOCAL:1, 0, 3), COUNTBIT(LOCAL:1, 4, 7), COUNTBIT(LOCAL:1, 8, 11))


;-------------------------------------------------
;関数名:LUBE_INSUFF
;概　要:润滑不足チェック関数
;引　数:ARG…真的场合、助手的PLAY被判定做
;戻り値:润滑不足等级。0那么润滑十分(最大3)
;備　考:式中関数
;経験不足的场合、戻り値但真（不足等级に应じて1～3直到）になる
;だいぶ面倒なつくりしてるけどいいやこ的ま直到…
;-------------------------------------------------
@LUBE_INSUFF(ARG)
#FUNCTION
#LOCALSIZE 1

;引数不正那么返回
SIF ARG < 0
	RETURNF 0
;润滑判定パ回合(0:判定无, 1:通常的判定, 2:扩张PLAY)
LOCAL = 0

;调教者根据PLAY被判定
IF !ARG
	;ACT的判定
	SELECTCASE TFLAG:ACT
		;常に润滑判定的あるPLAY
		;肛门爱抚, 貝合わせ/素股做, 进行乳交, 指插入
		;肛门拉珠
		;正常位, 后背位, 骑乘位, 对面座位, 背面座位, 肛交, 逆强奸
		CASE 13, 16, 17, 19, 23, 30 TO 36, 95 TO 103, 303 TO 311
			LOCAL = 1
		;道具装着时的み润滑判定的あるPLAY
		;震动棒, 肛门震动棒, 假阴茎
		CASE 21, 22, 26
			;派生(装着)
			SIF TFLAG:ACT派生 == 0
				LOCAL = 1
		;扩张PLAY(より多く的润滑但必要)
		;拳交, 肛门拳交, 两穴拳交
		CASE 70 TO 72
			LOCAL = 2
	ENDSELECT
	;追加行动的判定
	SELECTCASE TFLAG:追加行动
		CASE 2, 3
			LOCAL = 1
	ENDSELECT
;助手根据PLAY被判定
;实際的チェック是肛门拉珠水平しかしてない但、
;ここで是调教者的PLAY和同大人的チェック被做
;つまり雰囲気重視。异論是认める
ELSE
	;助手ACT的判定
	SELECTCASE NOWASSIACTNAME(ARG)
		CASE "逆强奸", "肛门爱抚", "肛门跳蛋", "肛门震动棒", "性交", "手指插入", "震动棒"
			LOCAL = 1
	ENDSELECT
	;コンビネーション的判定
	SELECTCASE TFLAG:ACT
		CASE 303 TO 310
			LOCAL = 1
	ENDSELECT
ENDIF

;润滑的参数和润滑液的残り效力被チェック
SELECTCASE LOCAL
	;通常的判定
	CASE 1
		IF PALAM:MASTER:润滑 < 100 && TEQUIP:润滑液 < 3
			RETURNF 3
		ELSEIF PALAM:MASTER:润滑 < 250 && TEQUIP:润滑液 < 2
			RETURNF 2
		ELSEIF PALAM:MASTER:润滑 < 500 && TEQUIP:润滑液 < 1
			RETURNF 1
		ENDIF
	;扩张PLAY
	CASE 2
		IF PALAM:MASTER:润滑 < 500 && TEQUIP:润滑液 < 3
			RETURNF 3
		ELSEIF PALAM:MASTER:润滑 < 1200 && TEQUIP:润滑液 < 2
			RETURNF 2
		ELSEIF PALAM:MASTER:润滑 < 2000 && TEQUIP:润滑液 < 1
			RETURNF 1
		ENDIF
ENDSELECT

;上で引っかからなかった场合
;润滑十分(また是判定的必要无)
RETURNF 0


;-------------------------------------------------
;関数名:IS_UNEXTRACTION
;概　要:中出し时的不拔出状态迟延判定用関数
;引　数:ARG:0…[省略可]不拔出对象的阴茎的持ち主被表す登录番号(现在MASTER的み)
;戻り値:中出しタイミングで的不拔出状态被表す数値(0~n)。1以上那么不拔出状态で的中出し
;備　考:式中関数
;中出し时に不拔出状态但ど的ような値であったか被返す
;もし口上で使えるタイミング但ある那么お引越しかも
;新規作成@revkoishi(14/06/07)
;-------------------------------------------------
@IS_UNEXTRACTION(ARG:0)
#FUNCTION
RETURNF _UNEXTRACTION(ARG:0)


;-------------------------------------------------
;関数名:_UNEXTRACTION
;概　要:中出し时的不拔出状态迟延判定用变数保持関数
;引　数:ARG:0…[省略可]不拔出对象的阴茎的持ち主被表す登录番号(现在MASTER的み)
;　　　 ARG:1…[省略可]模式。真(1)那么SET、伪(0)那么GET
;　　　 ARG:2…[省略可]SET模式的场合必須。代入做不拔出状态被表す数値(0~n)。1以上那么不拔出状态で的中出し
;戻り値:中出しタイミングで的不拔出状态被表す数値(0~n)。1以上那么不拔出状态で的中出し
;備　考:式中関数、内部関数
;新規作成@revkoishi(14/06/07)
;-------------------------------------------------
@_UNEXTRACTION(ARG:0 = -2, ARG:1, ARG:2)
#FUNCTION
ARG:0 = 0
LOCAL:(ARG:0) = ARG:1 ? ARG:2 # LOCAL:(ARG:0)
RETURNF LOCAL:(ARG:0)
