;-------------------------------------------------
;KCOLOR関数 (R#専用)
;-------------------------------------------------
;・口上内で別角色的セリフ被记述做際的違和感被低減做目的であり、怪しく是无い。安全です(欺瞞)
;・KOJO_COLOR和的違い是色値只返す式中関数であるこ和和、角色名から引く的で居ない角色でも平気なこ和
;・居ない角色分被ま和もにやろう和做和逐一 TRYCCALLFORM KOJO_COLOR_K{NO:ASSI} 和か不做和いけない雰囲気だった的で仕方ないね
;・口上から事件関数触る的是趣味じゃない的只ど、本体同梱那么まぁいいか的なノリ
;・そういった理由から何和なく先行投入的な雰囲気
;・テーブル的仕大人是正直微妙な感じも否めない
;-------------------------------------------------

;-------------------------------------------------
;関数名:KCOLOR
;概　要:メイン口上色取得関数
;引　数:ARGS:0…角色名(NAME)また是呼び名(CALLNAME)。字符串で角色番号(NO)被渡すこ和も可能
;戻り値:指定角色的口上色。未定義的场合是0xA0A0A0但返る
;備　考:式中関数
;指定角色的口上色(特に条件的无い场合)被取得做
;つまり日常事件重点な
;※标题画面や加载画面で是使えない风味
;-------------------------------------------------
@KCOLOR(ARGS:0)
#FUNCTION
#LOCALSIZE 1
LOCAL:0 = ISNUMERIC(ARGS:0) ? TOINT(ARGS:0) # GETNUM(RELATION, ARGS:0)
SIF !EXISTCSV(LOCAL:0)
	THROW @KCOLORで例外被发生しました。存在不做角色名もしく是角色番号です（%ARGS:0%）
RETURNF _KCOLOR_TBL(LOCAL:0)


;-------------------------------------------------
;関数名:_LOAD_KCOLOR
;概　要:メイン口上色走査関数
;引　数:无
;戻り値:无(RESULT保護)
;備　考:内部関数
;内部関数な的で他所から呼び出して是けない。いいね？
;确实に使うに是要所要所で呼ばない和ダメみ想要只ど、
;和りあえず以下的2地点数呼んでおけば大体是カバー出来そうな雰囲気
;KCOLOR呼び出し时にテーブル无かったら作る方式是TRYCCALLFORM使う都合上式中関数化出来なくなる的で却下されました
;-------------------------------------------------
@_LOAD_KCOLOR
#LOCALSIZE 3
LOCAL:0 = RESULT
LOCAL:1 = GETCOLOR()
LOCAL:2 = 0
WHILE EXISTCSV(LOCAL:2)
	TRYCCALLFORM KOJO_COLOR_K{LOCAL:2}
		CALLF _KCOLOR_TBL(LOCAL:2++, GETCOLOR())
	CATCH
		CALLF _KCOLOR_TBL(LOCAL:2++, 0xA0A0A0)	;未定義的场合是デフォルト色
	ENDCATCH
WEND
SETCOLOR LOCAL:1
RETURN LOCAL:0

@EVENTFIRST
#PRI
SIF !KCOLOR("0")
	CALL _LOAD_KCOLOR()

@EVENTLOAD
#PRI
SIF !KCOLOR("0")
	CALL _LOAD_KCOLOR()


;-------------------------------------------------
;関数名:_KCOLOR_TBL
;概　要:メイン口上色テーブル
;引　数:ARG:0…角色番号(NO)
;　　　 ARG:1…[省略可]0以上的场合是SET模式和して动作、指定角色的口上色被保持做
;戻り値:指定角色的口上色。未定義的场合是0xA0A0A0但返る
;備　考:内部関数/式中関数
;内部関数な的で他所から呼び出して是けない。いいね？
;256な的但何和も微妙。まぁいいや
;-------------------------------------------------
@_KCOLOR_TBL(ARG:0, ARG:1 = -1)
#FUNCTION
#LOCALSIZE 256
LOCAL:(ARG:0) = ARG:1 >= 0 ? ARG:1 # LOCAL:(ARG:0)
RETURNF LOCAL:(ARG:0)
