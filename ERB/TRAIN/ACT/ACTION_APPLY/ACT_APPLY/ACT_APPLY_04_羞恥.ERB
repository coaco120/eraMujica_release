;────────────────────────────────────
;040,自慰
;────────────────────────────────────
;反应前（Ｓ/Ｎ/Ｈ）[罰]（逃脱、中毒充足）
@ACTION_APPLY_40
;拒绝される可能性但ある的でここ是命令被出す和ころ直到处理し增加
;逃脱SOURCE増加、调教对象露出癖、自慰中毒、调教等级によって減少
SOURCE:MASTER:逃脱 = 1200 - GET_ABL(MASTER, "露出癖") * 8 - GET_ABL(MASTER, "自慰中毒") * 20 - CFLAG:MASTER:调教等级 * 20
;逃脱SOURCE但负的値被和らないよう调整
SOURCE:MASTER:逃脱 = SOURCE:MASTER:逃脱 < 0 ? 0 # SOURCE:MASTER:逃脱
;调教对象自慰中毒、技巧に应じて中毒充足SOURCE増加
SOURCE:MASTER:中毒充足 = GET_ABL(MASTER, "自慰中毒") * (GET_ABL(MASTER, "自慰中毒") + 10) * (GET_ABL(MASTER, "技巧") + 10) / 1000
;调教对象自慰中毒に应じて勃起度上升
TCVAR:MASTER:勃起度 += 100 + GET_ABL(MASTER, "自慰中毒") * 5
TFLAG:经过时间 += 3

;反应后（快Ｃ、快Ｖ、快Ａ、快Ｂ、性行动、露出、逃脱、中毒充足）
@ACTION_APPLY2_40
IF IS_COMGRONAME("消极的服从/淡然的服从/积极的服从/接受")
	;自慰的实行関数呼び出し
	CALL ONANIE_APPLY
	;调教对象的手被一本占有做
	TFLAG:手使用中++
;其他的场合(拒绝)
ELSE
	;REACT分类に要求に应じない被设置
	CALL SET_COMGRO("拒绝")
ENDIF

;────────────────────────────────────
;041,打开小穴
;────────────────────────────────────
;反应前（Ｓ/Ｎ/Ｈ）（快Ｃ、露出、逃脱）
@ACTION_APPLY_41
;下半身全部强制脱衣
TFLAG:强制脱衣 = 5
;灵巧的指尖素質、调教对象技巧に应じて快ＣSOURCE増加
SOURCE:MASTER:快Ｃ = 30 + (TALENT:灵巧的指尖 + 1) * GET_ABL(TARGET, "技巧")
;快ＣSOURCE、大镜使用に应じて露出SOURCE増加
SOURCE:MASTER:露出 = SOURCE:MASTER:快Ｃ / 3 + (TEQUIP:大镜 + 1) * 250
;露出SOURCEに应じて逃脱SOURCE増加、露出癖、调教对象调教等级によって減少
SOURCE:MASTER:逃脱 = SOURCE:MASTER:露出 - GET_ABL(MASTER, "露出癖") * 2 - CFLAG:MASTER:调教等级 * 5
;脏污但相互に移る
STAIN:MASTER:小穴 |= STAIN:手
STAIN:手 |= STAIN:MASTER:小穴
;逃脱SOURCE但负的値被和らないよう调整
SOURCE:MASTER:逃脱 = SOURCE:MASTER:逃脱 < 0 ? 0 # SOURCE:MASTER:逃脱
;顔面骑乘系解除
CALL FACESITTING_SET(0)
;调教者、调教对象自慰被中断
TEQUIP:自慰中 = 0
TFLAG:经过时间 += 1

;反应后（Ｓ/Ｎ/Ｈ）（快Ｃ、露出、逃脱）
@ACTION_APPLY2_41
;现在处理无

;────────────────────────────────────
;042,展示自慰
;────────────────────────────────────
;反应前（Ｓ/Ｎ/Ｈ/Ａ）（逃脱、欲望追加、快ｃ、快ｖ、快ｂ）
@ACTION_APPLY_42
;同じ确率で上＋下外套、上半身全部、下半身全部的どれか被自主脱衣
TFLAG:自主脱衣 = 3 + RAND:3
;调教者解放素質但存在すれば配饰－直到、全裸直到自主脱衣的どちらかに发展
SIF TALENT:解放
	TFLAG:自主脱衣 = 7 + RAND:2
;调教者压抑/羞涩素質但存在すれば上半身外套、下半身外套自主脱衣また是脱衣无直到軽減
SIF TALENT:压抑 || TALENT:羞涩
	TFLAG:自主脱衣 = RAND:3
;调教者着衣PLAY喜欢素質但存在すれば内衣只自主脱衣に固定
SIF TALENT:着衣PLAY喜欢
	TFLAG:自主脱衣 = 9
;调教者欲望、技巧、调教等级、调教者技巧、灵巧的指尖素質に应じて快ｃSOURCE増加
SOURCE:快Ｃ = 50 + GET_ABL(TARGET, "欲望") * GET_ABL(TARGET, "技巧") * GET_TRAINLV(TARGET) * 2 / 100 + GET_ABL(TARGET, "技巧") * 2 * (TALENT:TARGET:灵巧的指尖 + 1)
;调教者但非扶她那么、调教者欲望、技巧、调教等级、调教者技巧、灵巧的指尖素質に应じて快ｂSOURCE増加
SIF !TALENT:扶她
	SOURCE:快Ｂ = 50 + GET_ABL(TARGET, "欲望") * GET_ABL(TARGET, "技巧") * GET_TRAINLV(TARGET) * 2 / 100 + GET_ABL(TARGET, "技巧") * 2 * (TALENT:TARGET:灵巧的指尖 + 1)


;调教者但震动棒被持有していて、处女でも男でもなければ震动棒使用
;暂时オミット。復活侍奉和き是实行判定被见直してACT派生的方で处置
[SKIPSTART]
IF ITEM:震动棒 && !TALENT:处女 && !TALENT:扶她
	;调教者欲望、调教等级、道具使素質、技巧、道具强化に应じて快ｖSOURCE増加
	SOURCE:快Ｖ = 50 + GET_ABL(TARGET, "欲望") * (GET_TRAINLV(TARGET) + 1) * 5 / 10 + (TALENT:道具使 + 1) * GET_ABL(TARGET, "技巧") * (ITEM:9 + 1) * 3
	;代わりに快ｃSOURCE減少
	TIMES SOURCE:快Ｃ , 0.80
ENDIF
[SKIPEND]

;快x总計被取得しLOCALへ
LOCAL = SOURCE:快Ｃ + SOURCE:快Ｖ + SOURCE:快Ｂ
;调教对象调教等级和欲望被见て、どちらか但低ければLOCAL軽減
IF CFLAG:MASTER:调教等级 < 1 || ABL:MASTER:欲望 < 1
	TIMES LOCAL, 0.10
ELSEIF CFLAG:MASTER:调教等级 < 3 || ABL:MASTER:欲望 < 2
	TIMES LOCAL, 0.25
ELSEIF CFLAG:MASTER:调教等级 < 5 || ABL:MASTER:欲望 < 3
	TIMES LOCAL, 0.40
ELSEIF CFLAG:MASTER:调教等级 < 8 || ABL:MASTER:欲望 < 4
	TIMES LOCAL, 0.60
ELSEIF CFLAG:MASTER:调教等级 < 11 || ABL:MASTER:欲望 < 5
	TIMES LOCAL, 0.85
ENDIF
;LOCAL的1/5只调教者欲望但増加
CUP:MASTER:欲望 += LOCAL / 5
;LOCALに应じて逃脱SOURCE増加、调教对象露出癖、调教等级によって減少
SOURCE:MASTER:逃脱 = LOCAL - GET_ABL(MASTER, "露出癖") * 2 - CFLAG:MASTER:调教等级 * 5
;逃脱SOURCE但负的値被和らないよう调整
SOURCE:MASTER:逃脱 = SOURCE:MASTER:逃脱 < 0 ? 0 # SOURCE:MASTER:逃脱
;顔面骑乘系解除
CALL FACESITTING_SET(0)
;调教者欲望に应じて勃起度上升
TCVAR:MASTER:这回合的勃起 += 100 + GET_ABL(TARGET, "欲望")
TFLAG:经过时间 += 3

;反应后（Ｓ/Ｎ/Ｈ/Ａ）（逃脱、欲望追加、快ｃ、快ｖ、快ｂ）
@ACTION_APPLY2_42
;调教者自慰被开始
TEQUIP:自慰中 |= 2

;────────────────────────────────────
;043,羞耻PLAY（Ｓ/Ｎ/Ｈ）（露出）
;────────────────────────────────────
@ACTION_APPLY_43
;开始时
IF TFLAG:ACT派生 == 0
	;露出SOURCE増加
	SOURCE:MASTER:露出 = 200
	;大镜被据え付ける
	TEQUIP:大镜 = 1
;终了时
ELSE
	;大镜被片付ける
	TEQUIP:大镜 = 0
ENDIF
;顔面骑乘系解除
CALL FACESITTING_SET(0)
TFLAG:经过时间 += 2

;────────────────────────────────────
;044,野外PLAY（Ｈ/Ａ）[罰]（捆绑<=项圈的こ和、露出、逃脱、心理创伤）
;────────────────────────────────────
@ACTION_APPLY_44
;终了时
IF TFLAG:ACT派生 == 1
	;野外PLAY终了
	TEQUIP:野外PLAY = 0
;开始时
ELSE
	;反抗刻印に应じて捆绑SOURCE増加
	SOURCE:MASTER:捆绑 = 50 + MARK:反抗刻印 * 15
	;露出SOURCE増加
	SOURCE:MASTER:露出 = 300
	;捆绑SOURCEに应じて逃脱SOURCE増加、调教对象露出癖、调教等级によって減少
	SOURCE:MASTER:逃脱 = 150 + SOURCE:MASTER:捆绑 - GET_ABL(MASTER, "露出癖") * 3 - CFLAG:MASTER:调教等级 * 10
	;逃脱SOURCE但负的値被和らないよう调整
	SOURCE:MASTER:逃脱 = SOURCE:MASTER:逃脱 < 0 ? 0 # SOURCE:MASTER:逃脱
	;调教对象胆怯、无关心、压抑、解放、羞涩、无耻素質、逃脱SOURCEに应じて心理创伤SOURCE増減
	SOURCE:MASTER:心理创伤 = TALENT:MASTER:胆怯 * 150 - TALENT:MASTER:无关心 * 50 + TALENT:MASTER:压抑 * 150 - TALENT:MASTER:解放 * 50 + TALENT:MASTER:羞涩 * 250 - TALENT:MASTER:无耻 * 150 + SOURCE:MASTER:逃脱
	;逃脱SOURCE但0那么心理创伤SOURCEも0になる
	SOURCE:MASTER:心理创伤 = SOURCE:MASTER:逃脱 == 0 ? 0 # SOURCE:MASTER:心理创伤
	;心理创伤SOURCE但负的値被和らないよう调整
	SOURCE:MASTER:心理创伤 = SOURCE:MASTER:心理创伤 < 0 ? 0 # SOURCE:MASTER:心理创伤
	;心理创伤(刻印)的元但こ的调教だった场合、心理创伤SOURCE被1.5倍に做
	SIF CFLAG:MASTER:心理创伤 == 44
		TIMES SOURCE:MASTER:心理创伤 , 1.50
	;野外PLAY开始
	TEQUIP:野外PLAY = 1
ENDIF
;顔面骑乘系解除
CALL FACESITTING_SET(0)
;调教者、调教对象自慰被中断
TEQUIP:自慰中 = 0
TFLAG:经过时间 += 5

;────────────────────────────────────
;045,放尿
;────────────────────────────────────
;反应前（Ｎ/Ｈ/Ａ）[罰]（露出、逃脱、心理创伤）
@ACTION_APPLY_45
;露出SOURCE増加
SOURCE:MASTER:露出 = 350
;逃脱SOURCE増加、调教对象露出癖、调教等级によって減少
SOURCE:MASTER:逃脱 = 750 - GET_ABL(MASTER, "露出癖") * 15 - CFLAG:MASTER:调教等级 * 30
;逃脱SOURCE但负的値被和らないよう调整
SOURCE:MASTER:逃脱 = SOURCE:MASTER:逃脱 < 0 ? 0 # SOURCE:MASTER:逃脱
;调教对象胆怯、无关心、压抑、解放、羞涩、无耻素質、逃脱SOURCEに应じて心理创伤SOURCE増減
SOURCE:MASTER:心理创伤 = TALENT:MASTER:胆怯 * 150 - TALENT:MASTER:无关心 * 50 + TALENT:MASTER:压抑 * 150 - TALENT:MASTER:解放 * 50 + TALENT:MASTER:羞涩 * 250 - TALENT:MASTER:无耻 * 150 + SOURCE:MASTER:逃脱
;逃脱SOURCE但0那么心理创伤SOURCEも0になる
SOURCE:MASTER:心理创伤 = SOURCE:MASTER:逃脱 == 0 ? 0 # SOURCE:MASTER:心理创伤
;心理创伤SOURCE但负的値被和らないよう调整
SOURCE:MASTER:心理创伤 = SOURCE:MASTER:心理创伤 < 0 ? 0 # SOURCE:MASTER:心理创伤
	;心理创伤(刻印)的元但こ的调教だった场合、心理创伤SOURCE被1.5倍に做
	SIF CFLAG:MASTER:心理创伤 == 45
		TIMES SOURCE:MASTER:心理创伤 , 1.50
;调教对象自慰被中断
TEQUIP:自慰中 &= ~1
TFLAG:经过时间 += 2

;反应后（Ｎ/Ｈ/Ａ）[罰]（露出、逃脱、心理创伤）
@ACTION_APPLY2_45
IF IS_COMGRONAME("消极的服从/淡然的服从/积极的服从/接受")
	;尿意被失う
	BASE:MASTER:尿意 = 0
	;屈从SOURCE増加
	SOURCE:MASTER:屈从 += 200
	SOURCE:MASTER:达成 += 50 + LOCAL
	;调教对象Ｖ被占有做
	TFLAG:Ｖ使用中 = 2
;其他的场合(拒绝)
ELSE
	;REACT分类に要求に应じない被设置
	CALL SET_COMGRO("拒绝")
	;露出、逃脱、心理创伤、屈从SOURCE減少
	TIMES SOURCE:MASTER:露出 , 0.40
	TIMES SOURCE:MASTER:逃脱 , 0.50
	TIMES SOURCE:MASTER:心理创伤 , 0.10
	TIMES SOURCE:MASTER:屈从 , 0.20
ENDIF

;────────────────────────────────────
;046,COSPLAY（Ｓ/Ｎ/Ｈ/Ａ）（未实装）
;────────────────────────────────────
;@ACTION_APPLY_46
