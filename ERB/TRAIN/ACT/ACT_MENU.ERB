;────────────────────────────────────
;调教类別的判定 (会话0/爱抚1/道具2/性交3/羞耻4/奉仕5/施虐6/异常7/奴役8/性奉10)
;────────────────────────────────────
@ACT_MENU
#DIM LCOUNT

;变数被初期化
CALL SET_CRI_VAR
FOR LCOUNT, 0, 11
	;休息被是じく
	;奴役是处理不做
	SIF LCOUNT == 8 || LCOUNT == 9
		CONTINUE
	CALL SET_CRI_VAR, MENU(LCOUNT), 1
NEXT

LOCALS = ACT_MENU

CALL CHA_CRI_VAR, "筋書き"
CALLFORM %LOCALS%_STORY_T

CALL CHA_CRI_VAR, "状況判断"
CALLFORM %LOCALS%_ESTIMATE_T

CALL CHA_CRI_VAR, "调教者的素質"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "调教对象的素質"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "调教者但妊娠"
CALLFORM %LOCALS%_MATERNITY_T

CALL CHA_CRI_VAR, "调教者但处女"
CALLFORM %LOCALS%_MAIDEN_T

CALL CHA_CRI_VAR, "调教对象但处女"
CALLFORM %LOCALS%_MAIDEN_M

CALL CHA_CRI_VAR, "调教对象但童贞"
CALLFORM %LOCALS%_CHERRY_M

CALL CHA_CRI_VAR, "调教者的能力"
CALLFORM %LOCALS%_ABL_T

CALL CHA_CRI_VAR, "调教对象的能力"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "性別関係"
CALLFORM %LOCALS%_SEX

CALL CHA_CRI_VAR, @"本日的方针(%POLICY("今日")%)"
CALLFORM %LOCALS%_POLICY

CALL CHA_CRI_VAR, @"本次的方针(%POLICY("今")%)"
CALLFORM %LOCALS%_COURCE

CALL CHA_CRI_VAR, "调教对象的润滑"
CALLFORM %LOCALS%_MOIST_M

CALL CHA_CRI_VAR, "调教对象的欲望"
CALLFORM %LOCALS%_DESIRE_M

CALL CHA_CRI_VAR, "调教对象的屈服"
CALLFORM %LOCALS%_YIELD_M

CALL CHA_CRI_VAR, "调教对象的恭顺"
CALLFORM %LOCALS%_ALLEGIANCE_M

CALL CHA_CRI_VAR, "调教对象的射精ゲージ"
CALLFORM %LOCALS%_ECSTASY_M

IF TCVAR:MASTER:需要强制勃起
	CALL CHA_CRI_VAR, "需要强制勃起"
ELSE
	CALL CHA_CRI_VAR, "勃起度"
ENDIF
CALLFORM %LOCALS%_ERECTION_M

CALL CHA_CRI_VAR, "调教者的快感"
CALLFORM %LOCALS%_PREASURE_T

CALL CHA_CRI_VAR, "调教者的体力"
CALLFORM %LOCALS%_PHYSICAL_T

CALL CHA_CRI_VAR, "调教者的气力"
CALLFORM %LOCALS%_MENTAL_T

CALL CHA_CRI_VAR, "调教者的理性"
CALLFORM %LOCALS%_REASON_T

CALL CHA_CRI_VAR, "调教者的兴趣"
CALLFORM %LOCALS%_INTEREST_T

CALL CHA_CRI_VAR, "调教者的焦躁"
CALLFORM %LOCALS%_IRRITATION_T

CALL CHA_CRI_VAR, "调教者的好感度"
CALLFORM %LOCALS%_LIKE_T

CALL CHA_CRI_VAR, "调教对象的好感度"
CALLFORM %LOCALS%_LIKE_M

CALL CHA_CRI_VAR, @"调教者的状态(%CONDITION(TCVAR:状态变化)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, @"调教对象的状态(%CONDITION(TCVAR:MASTER:状态变化)%)"
CALLFORM %LOCALS%_CONDITION_M

CALL CHA_CRI_VAR, "调教对象的疲労"
CALLFORM %LOCALS%_TIRED_M

CALL CHA_CRI_VAR, "初手会话修正"
CALLFORM %LOCALS%_FIRST

CALL CHA_CRI_VAR, "大镜持有奖励"
CALLFORM %LOCALS%_MILLER

CALL CHA_CRI_VAR, "更加强硬"
CALLFORM %LOCALS%_PLEASE_HARD

CALL CHA_CRI_VAR, "请求宽恕"
CALLFORM %LOCALS%_PLEASE_HELP

CALL CHA_CRI_VAR, "気持よくして"
CALLFORM %LOCALS%_PLEASE_PREASURE

CALL CHA_CRI_VAR, "发狂"
CALLFORM %LOCALS%_VIOLENT

CALL CHA_CRI_VAR, "道具解除"
CALLFORM %LOCALS%_UNEQUIP

CALL CHA_CRI_VAR, "推到连续爱抚修正"
CALLFORM %LOCALS%_PETTING_CONTINUE

CALL CHA_CRI_VAR, "シックスナイン誘发"
CALLFORM %LOCALS%_69

CALL CHA_CRI_VAR, "调教者的服装"
CALLFORM %LOCALS%_CLOTH_T

CALL CHA_CRI_VAR, "调教对象的服装"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "媚药"
CALLFORM %LOCALS%_DRAG

CALL CHA_CRI_VAR, "震动棒"
CALLFORM %LOCALS%_VIBE

CALL CHA_CRI_VAR, "肛门震动棒"
CALLFORM %LOCALS%_AVIBE

CALL CHA_CRI_VAR, "肛门拉珠"
CALLFORM %LOCALS%_ABEADS

CALL CHA_CRI_VAR, "灌肠＋肛门肛塞"
CALLFORM %LOCALS%_APLUG

CALL CHA_CRI_VAR, "阴蒂夹"
CALLFORM %LOCALS%_CRICAP

CALL CHA_CRI_VAR, "飞机杯"
CALLFORM %LOCALS%_ONAHOLE

CALL CHA_CRI_VAR, "乳头夹"
CALLFORM %LOCALS%_NIPPLECAP

CALL CHA_CRI_VAR, "绳"
CALLFORM %LOCALS%_ROPE

CALL CHA_CRI_VAR, "眼罩"
CALLFORM %LOCALS%_EYEMASK

CALL CHA_CRI_VAR, "口球"
CALLFORM %LOCALS%_GAG

CALL CHA_CRI_VAR, "三角木马"
CALLFORM %LOCALS%_HORSE

CALL CHA_CRI_VAR, "野外PLAY"
CALLFORM %LOCALS%_OUT

CALL CHA_CRI_VAR, "大镜"
CALLFORM %LOCALS%_T_MILLER

CALL CHA_CRI_VAR, "道具解除制限"
CALLFORM %LOCALS%_KILL_UNEQUIP

CALL CHA_CRI_VAR, "调教者自慰中"
CALLFORM %LOCALS%_MASTURBATION_T

CALL CHA_CRI_VAR, "调教对象自慰中"
CALLFORM %LOCALS%_MASTURBATION_M

CALL CHA_CRI_VAR, "惩罚"
CALLFORM %LOCALS%_OSHIOKI

CALL CHA_CRI_VAR, "插入修正"
CALLFORM %LOCALS%_INSERT

CALL CHA_CRI_VAR, "焦虑中"
CALLFORM %LOCALS%_IRRITATE

CALL CHA_CRI_VAR, "Ａ性交回避"
CALLFORM %LOCALS%_AGUARD

CALL CHA_CRI_VAR, "Ａ性交体位制限"
CALLFORM %LOCALS%_ASEX_LIMITATION

CALL CHA_CRI_VAR, "口上修正"
CALLFORM %LOCALS%_KOJO_CORRECTION_T

CALL CHA_CRI_VAR, "实行判定"
CALLFORM %LOCALS%_ABLE

;可能的ACT系列数设置
;※兴奋値的修正に使用做的でここで計算
TFLAG:可能的ACT系列数 = 0
;可能的ACT系列数设置
FOR LCOUNT, 0, 11
	;休息被是じく
	;奴役是处理不做
	SIF LCOUNT == 8 || LCOUNT == 9
		CONTINUE
	;-98以上被可能和判断做(多分今是大丈夫だ和思うけど)@これみ
	SIF GET_CRI_VAR(MENU(LCOUNT) + "累计") > -99
		TFLAG:可能的ACT系列数++
NEXT

CALL CHA_CRI_VAR, "兴奋値"
CALLFORM %LOCALS%_HEAT

CALL CHA_CRI_VAR, "ACT修正値"
CALLFORM %LOCALS%_CORRECTION

;──────────────────────────────
;最终判定
;──────────────────────────────
CALL DEF_CRI_VAR("最大优先指定-爱抚/性奉/性交/道具/奉仕/羞耻/施虐/奴役/异常/会话")
CALLF GET_CRI_VAR("累计")
CALL ACT_MENU_RESULT()
CALL ANA_CRI_VAR()

;──────────────────────────────
;ACT分类判定
;引き分ける时的优先顺是爱抚1＞性奉10＞性交3＞道具2＞奉仕5＞羞耻4＞施虐6＞奴役8＞异常7＞会话0
;
;ARG = 0:基準値保存 1:分类呼び出し 2:ARGSで指定した分类被无效化
;──────────────────────────────
@ACT_MENU_RESULT(ARG = 0, ARGS)
#DIM LCOUNT
#DIM LTEMP, 2
#DIMS LTEMPS

;ARG但0那么
IF ARG == 0
	;結果被保存
	FOR LCOUNT, 0, 9
		LOCAL:LCOUNT = RESULT:LCOUNT
	NEXT
ENDIF
;分类名读み込み
;※奴役被处理做よう变成了ら追加做
SPLIT "会话/爱抚/道具/性交/羞耻/奉仕/施虐/异常/性奉", "/", LOCALS
;ARG但1那么
IF ARG == 1
	;优先顺读み込み
	LTEMPS = 爱抚/性奉/性交/道具/奉仕/羞耻/施虐/奴役/异常/会话
	;最优先分类被探す
	LTEMP = -99
	LTEMP:1 = -1
	FOR LCOUNT, 0, 9
		;数値但小さい、无效な场合是无視
		IF LOCAL:LCOUNT < LTEMP || LOCAL:LCOUNT <= -99
			CONTINUE
		;数値但同じ场合是优先顺に从って切り替え
		ELSEIF LOCAL:LCOUNT == LTEMP
			SIF STRFIND(LTEMPS, LOCALS:LCOUNT) > STRFIND(LTEMPS, LOCALS:(LTEMP:1))
				CONTINUE
		;もちろん数値但大きい场合も切り替え
		ENDIF
		;切り替え时分类ポインタ被动かす
		LTEMP = LOCAL:LCOUNT
		LTEMP:1 = LCOUNT
	NEXT
	;最优先分类但存在しなければ1被返す
	SIF LTEMP:1 == -1
		RETURN 1
	;最优先分类被本次的分类に指定做
	TFLAG:ACT分类 = MENUNUM(LOCALS:(LTEMP:1))
	RETURN 0
;ARG但2那么
ELSEIF ARG == 2
	;指定さ受到分类被探す
	LTEMP = FINDELEMENT(LOCALS, ARGS)
	;存在しなければ飛ばす
	SIF LTEMP == -1
		RETURN -1
	;存在すれば分类被判定对象から外す
	LOCAL:LTEMP = -99
	;本次的分类但指定さ受到も的那么ば、未选择に戻す
	SIF TFLAG:ACT分类 == MENUNUM(LOCALS:LTEMP)
		TFLAG:ACT分类 = -1
ENDIF

;-------------------------------------------------------------------------
;筋書き修正
;-------------------------------------------------------------------------
@ACT_MENU_STORY_T

[SKIPSTART]
;临时模式以外で是
IF FLAG:模式 != 9
	;性交、性奉にマイナス修正(20日经过で完全に解消)
	CALL ADD_CRI_VAR("性交", MIN(DAY / 2 - 10, 0))
	CALL ADD_CRI_VAR("性奉", MIN(DAY / 2 - 10, 0))
ENDIF
[SKIPEND]

;-------------------------------------------------------------------------
;状況判断
;-------------------------------------------------------------------------
@ACT_MENU_ESTIMATE_T
#DIM LCOUNT

FOR LCOUNT, 0, 11
	SIF LCOUNT == 9
		CONTINUE
	CALL ADD_CRI_VAR(MENU(LCOUNT), EST_TO_ACTM("取得", MENU(LCOUNT)))
NEXT

;-------------------------------------------------------------------------
;调教者的素質参照
;-------------------------------------------------------------------------
@ACT_MENU_TALENT_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;调教者但自尊心高/低
IF TALENT:LOCAL:自尊心高
	CALL ADD_CRI_VAR("奉仕",  9)
ELSEIF TALENT:LOCAL:自尊心低
	CALL ADD_CRI_VAR("奉仕", -9)
ENDIF

;调教者但无关心/好奇心
IF TALENT:LOCAL:无关心
	CALL ADD_CRI_VAR("会话", -15)
	CALL ADD_CRI_VAR("爱抚",  -6)
	CALL ADD_CRI_VAR("道具", , -9)
ELSEIF TALENT:LOCAL:好奇心
	CALL ADD_CRI_VAR("会话", 3, 18)
	CALL ADD_CRI_VAR("爱抚", 6)
	CALL ADD_CRI_VAR("道具", 3,  9)
	CALL ADD_CRI_VAR("性奉", 3,  6)
ENDIF

;调教者但压抑/解放
IF TALENT:LOCAL:压抑
	CALL ADD_CRI_VAR("道具",  -6)
	CALL ADD_CRI_VAR("性交",  -6)
	CALL ADD_CRI_VAR("羞耻", -12)
	CALL ADD_CRI_VAR("施虐",  -3)
	CALL ADD_CRI_VAR("异常",  -9)
	CALL ADD_CRI_VAR("性奉",  -3)
ELSEIF TALENT:LOCAL:解放
	CALL ADD_CRI_VAR("道具",   6)
	CALL ADD_CRI_VAR("性交",   6)
	CALL ADD_CRI_VAR("羞耻",  12)
	CALL ADD_CRI_VAR("施虐",   3)
	CALL ADD_CRI_VAR("异常",   9)
	CALL ADD_CRI_VAR("性奉",   3)
ENDIF

;调教者但羞涩/无耻
IF TALENT:LOCAL:羞涩
	CALL ADD_CRI_VAR("羞耻", -9)
	CALL ADD_CRI_VAR("性奉", -6)
ELSEIF TALENT:LOCAL:无耻
	CALL ADD_CRI_VAR("羞耻",  9)
	CALL ADD_CRI_VAR("性奉",  6)
ENDIF

;调教者但舌使
SIF TALENT:LOCAL:舌使
	CALL ADD_CRI_VAR("爱抚", 9)

;调教者但东方不败(针持有时)
SIF TALENT:LOCAL:东方不败 && ITEM:针
	CALL ADD_CRI_VAR("施虐", 9)

;调教者但猫舌
SIF TALENT:LOCAL:猫舌
	CALL ADD_CRI_VAR("爱抚", -9)

;调教者但灵巧な指
SIF TALENT:LOCAL:灵巧的指尖
	CALL ADD_CRI_VAR("爱抚", 9)

;调教者但腰使
IF TALENT:LOCAL:腰使
	CALL ADD_CRI_VAR("性交", 9)
	CALL ADD_CRI_VAR("性奉", 6)
ENDIF

;调教者但擅长捆绑(绳持有时)
SIF TALENT:LOCAL:擅长捆绑 && ITEM:绳
	CALL ADD_CRI_VAR("施虐", 9)

;调教者但道具使
SIF TALENT:LOCAL:道具使
	CALL ADD_CRI_VAR("道具", 9)

;调教者但自慰し容易
SIF TALENT:LOCAL:自慰し容易
	CALL ADD_CRI_VAR("羞耻", 9)

;调教者但献身的
SIF TALENT:LOCAL:献身的
	CALL ADD_CRI_VAR("奉仕", -9)

;调教者但被动
SIF TALENT:LOCAL:被动
	CALL ADD_CRI_VAR("奉仕", 9)

;调教者但承认快感/否定
IF TALENT:LOCAL:承认快感
	CALL ADD_CRI_VAR("性交",  9)
	CALL ADD_CRI_VAR("奉仕",  6)
	CALL ADD_CRI_VAR("性奉",  9)
ELSEIF TALENT:LOCAL:否定快感
	CALL ADD_CRI_VAR("性交", -9)
	CALL ADD_CRI_VAR("奉仕", -6)
	CALL ADD_CRI_VAR("性奉", -6)
ENDIF

;调教者但倒错的
IF TALENT:LOCAL:倒错的
	CALL ADD_CRI_VAR("施虐", 3)
	CALL ADD_CRI_VAR("异常", 3)
ENDIF

;调教者但好心眼
SIF TALENT:LOCAL:好心眼
	CALL ADD_CRI_VAR("施虐", -9)

;调教者但幼稚
IF TALENT:LOCAL:幼稚
	CALL ADD_CRI_VAR("爱抚", , 15)
	CALL ADD_CRI_VAR("羞耻", -9)
	CALL ADD_CRI_VAR("施虐", -3)
	CALL ADD_CRI_VAR("异常", , 12)
ENDIF

;调教者但疯狂
SIF TALENT:LOCAL:疯狂
	CALL ADD_CRI_VAR("异常", , 20)
	
;-------------------------------------------------------------------------
;调教对象的素質参照
;-------------------------------------------------------------------------
@ACT_MENU_TALENT_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG
;调教对象但威压感
IF TALENT:LOCAL:威压感
	CALL ADD_CRI_VAR("奉仕", -3)
	CALL ADD_CRI_VAR("施虐", -3)
ENDIF

;调教对象但魅惑
IF TALENT:LOCAL:魅惑
	CALL ADD_CRI_VAR("爱抚", , 2)
	CALL ADD_CRI_VAR("性交", , 4)
	CALL ADD_CRI_VAR("性奉", , 5)
ENDIF

;调教对象但亲近容易/难以接近
IF TALENT:LOCAL:亲近容易
	CALL ADD_CRI_VAR("会话",  3)
	CALL ADD_CRI_VAR("道具",   , -2)
	CALL ADD_CRI_VAR("奉仕",   ,  2)
	CALL ADD_CRI_VAR("性奉",   ,  2)
ELSEIF TALENT:LOCAL:难以接近
	CALL ADD_CRI_VAR("会话", -3)
	CALL ADD_CRI_VAR("道具",   ,  2)
	CALL ADD_CRI_VAR("奉仕",   , -2)
	CALL ADD_CRI_VAR("性奉",   , -2)
ENDIF

;-------------------------------------
;调教者但妊娠中
;-------------------------------------
@ACT_MENU_MATERNITY_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG
;妊娠中
IF TALENT:LOCAL:妊娠
	CALL ADD_CRI_VAR("施虐",  -5)
	CALL ADD_CRI_VAR("异常", -10)
	CALL ADD_CRI_VAR("性奉",  -5)
ENDIF

;-------------------------------------------------------------------------
;童贞・处女関係
;引数是调教者・调教对象
;调教对象但处女的时、无闇に性交不做仕大人です。童贞も同じ处理被行う但效果是やや低です。
;调教对象但处女で合意未取得かつ、调教对象但女人
;-------------------------------------------------------------------------
@ACT_MENU_MAIDEN_T, ARG = -1, ARG:1 = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1
;调教者但处女で、调教对象に阴茎但有る
IF TALENT:LOCAL:处女 && PENIS(LOCAL:1)
	;调教者但贞操观念/不在意贞操
	LOCAL:1 = CFLAG:LOCAL:好感 + CFLAG:LOCAL:M好感 / 2
	IF TALENT:LOCAL:贞操观念
		CALL ADD_CRI_VAR("性交", LINE_CALC(LOCAL:1, "0→-12,1200→0,LIMIT/-24～0"))
		CALL ADD_CRI_VAR("性奉", LINE_CALC(LOCAL:1, "0→-12,2400→0,LIMIT/-24～0"))
	ELSEIF TALENT:LOCAL:不在意贞操
		CALL ADD_CRI_VAR("性交", LINE_CALC(LOCAL:1, "0→-4,400→0,LIMIT/-8～0"))
		CALL ADD_CRI_VAR("性奉", LINE_CALC(LOCAL:1, "0→-4,800→0,LIMIT/-8～0"))
	ELSE
		CALL ADD_CRI_VAR("性交", LINE_CALC(LOCAL:1, "0→-8,800→0,LIMIT/-16～0"))
		CALL ADD_CRI_VAR("性奉", LINE_CALC(LOCAL:1, "0→-8,1600→0,LIMIT/-16～0"))
	ENDIF
	;调教者但胆怯
	IF TALENT:LOCAL:胆怯
		CALL ADD_CRI_VAR("性交", -4)
		CALL ADD_CRI_VAR("性奉", -2)
	ENDIF
	;调教者但不越过底线
	IF TALENT:LOCAL:不越过底线
		CALL ADD_CRI_VAR("性交", -4)
		CALL ADD_CRI_VAR("性奉", -2)
	ENDIF
ENDIF

;-----------------------------------------
;调教对象但处女
;-----------------------------------------
@ACT_MENU_MAIDEN_M, ARG = -1, ARG:1 = 1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1
IF TALENT:(LOCAL:1):处女 && !CFLAG:LOCAL:合意 && !TALENT:(LOCAL:1):扶她
	CALL ADD_CRI_VAR("性交", -5, -2)
	;调教者但胆怯/自制的/不越过底线/慎重
	SIF TALENT:LOCAL:胆怯 || TALENT:LOCAL:自制的 || TALENT:LOCAL:不越过底线 || TALENT:LOCAL:慎重
		CALL ADD_CRI_VAR("性交", -3)
	;调教者但刚毅/冲动的/急躁、また是调教者的状态但情欲/愤怒/无聊/疯狂的いずれか
	SIF TALENT:LOCAL:刚毅 || TALENT:LOCAL:冲动的 || TALENT:LOCAL:急躁 || TCVAR:状态变化 > 4
		CALL ADD_CRI_VAR("性交", , 6)
	;后ろ但使える状态那么ペナルティ被軽減(调教对象的Ａ経験3以上)
	SIF EXP:(LOCAL:1):Ａ経験 > 2
		CALL ADD_CRI_VAR("性交", 3)
	;调教者的调教Lv但1以上
	SIF CFLAG:LOCAL:调教等级 > 0
		CALL ADD_CRI_VAR("性交", GET_TRAINLV(LOCAL) / 3)
	;调教者へ的好感但1000超過
	SIF CFLAG:LOCAL:M好感 > 1000
		CALL ADD_CRI_VAR("性交", CFLAG:LOCAL:M好感 / 500)
ENDIF

@ACT_MENU_CHERRY_M, ARG = -1, ARG:1 = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1
;调教对象但童贞で合意未取得かつ、调教对象但男人また是扶她处女
IF TALENT:(LOCAL:1):童贞 && !CFLAG:合意 && (TALENT:(LOCAL:1):男人 || TALENT:(LOCAL:1):处女)
	CALL ADD_CRI_VAR("性交", -3, -1)
	;调教者但胆怯/自制的/不越过底线/慎重
	SIF TALENT:LOCAL:胆怯 || TALENT:LOCAL:自制的 || TALENT:LOCAL:不越过底线 || TALENT:LOCAL:慎重
		CALL ADD_CRI_VAR("性交", -2)
	;调教者但刚毅/冲动的/急躁、また是调教者的状态但情欲/愤怒/无聊/疯狂的いずれか
	SIF TALENT:LOCAL:刚毅 || TALENT:LOCAL:冲动的 || TALENT:LOCAL:急躁 || TCVAR:状态变化 > 4
		CALL ADD_CRI_VAR("性交", , 3)
	;后ろ但使える状态那么ペナルティ被軽減(调教对象的Ａ経験2以上)
	SIF EXP:(LOCAL:1):Ａ経験 > 1
		CALL ADD_CRI_VAR("性交", 2)
	;调教者的调教Lv但1以上
	SIF CFLAG:LOCAL:调教等级 > 0
		CALL ADD_CRI_VAR("性交", GET_TRAINLV(LOCAL) / 3)
	;调教者へ的好感但1000超過
	SIF CFLAG:LOCAL:M好感 > 1000
		CALL ADD_CRI_VAR("性交", CFLAG:LOCAL:M好感 / 500)
ENDIF

;-------------------------------------------------------------------------
;调教者的能力被参照
;-------------------------------------------------------------------------
@ACT_MENU_ABL_T, ARG = -1
#LOCALSIZE 1
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;既にある物被活かす时に使い、
;新規开拓时符号被逆にして使うこ和（今是いや）
[SKIPSTART]

;基本的な能力修正
FOR LCOUNT, 0, 11
	;调教种別から对应做ABL被割り出し、ABL修正値的1/5被加算
	CALL ADD_CRI_VAR(MENU(LCOUNT), GET_ABL(LOCAL, ABLNAME:(20 + LCOUNT)) * 2 / 10)
NEXT

;ＣＶ感觉的总计値但[0→0, 1→1, 4→2, 7→3]被加算(0～3)
CALL ADD_CRI_VAR("性交", LINE_CALC(ABL:LOCAL:Ｃ感觉 + ABL:LOCAL:Ｖ感觉, "1→1,7→3,LIMIT/0～3"))

;调教者的Ｖ感觉
CALL ADD_CRI_VAR("性奉", GET_ABL(LOCAL, "Ｖ感觉") / 20)

;调教者的露出癖
SIF ABL:LOCAL:露出癖
	CALL ADD_CRI_VAR("羞耻", GET_ABL(LOCAL, "露出癖") / 20)

[SKIPEND]

;-------------------------------------------------------------------------
;调教对象的能力被参照
;-------------------------------------------------------------------------
@ACT_MENU_ABL_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG

;调教对象的顺从和奉仕精神的总计値但[0→0, 1→1, 4→2, 7→3]被加算(0～3)
CALL ADD_CRI_VAR("奉仕", LINE_CALC(ABL:LOCAL:顺从 + ABL:LOCAL:奉仕精神, "1→1,7→3,LIMIT/0～3"))
;调教对象的顺从和奉仕精神的总计値但[1→0, 4→1, 7→2]被加算(0～2)
CALL ADD_CRI_VAR("性奉", LINE_CALC(ABL:LOCAL:顺从 + ABL:LOCAL:奉仕精神, "4→1,7→2,LIMIT/0～2"))

;调教对象的ＣＶ感觉的总计値但[0→0, 1→0～1, 4→0～2, 7→0～3]被加算(0～3)
CALL ADD_CRI_VAR("道具", , LINE_CALC(ABL:LOCAL:Ｃ感觉 + ABL:LOCAL:Ｖ感觉, "1→1,7→3,LIMIT/0～3"))
CALL ADD_CRI_VAR("性交", , LINE_CALC(ABL:LOCAL:Ｃ感觉 + ABL:LOCAL:Ｖ感觉, "1→1,7→3,LIMIT/0～3"))

;调教对象的Ａ感觉但[0→0～1, 1→0～2, 3→0～4, 5→0～6]被加算(0～6)
CALL ADD_CRI_VAR("道具", , LINE_CALC(ABL:LOCAL:Ａ感觉, "1→2,5→6,LIMIT/1～6"))
;调教对象的Ａ感觉但[0→0, 1→0～1, 3→0～3, 5→0～5]被加算(0～5)
CALL ADD_CRI_VAR("异常", , LINE_CALC(ABL:LOCAL:Ａ感觉, "1→1,5→5,LIMIT/0～5"))
;调教对象的Ａ感觉但[0→0, 3→1, 5→2]被加算(0～2)
CALL ADD_CRI_VAR("性交", LINE_CALC(ABL:LOCAL:Ａ感觉, "3→1,5→2,LIMIT/0～2"))

;调教对象的Ｂ感觉但[0→0, 3→0～1, 5→0～2]被加算(0～2)
CALL ADD_CRI_VAR("爱抚", , LINE_CALC(ABL:LOCAL:Ｂ感觉, "3→1,5→2,LIMIT/0～2"))
;调教对象的Ｂ感觉但[0→0, 1→0, 3→0～1]被加算(0～1)
CALL ADD_CRI_VAR("奉仕", , LINE_CALC(ABL:LOCAL:Ｂ感觉, "1→0,3→1,LIMIT/0～1"))

;调教对象的受虐相関能力(受虐气质/痛苦中毒/捆绑中毒的总计)
;※なぜか百合气质被见ていた的で受虐气质に变更
;但[0→0, 3→0～2, 6→0～4, 9→0～6, 12→0～8]被加算(0～8)
CALL ADD_CRI_VAR("施虐", , LINE_CALC(ABL:LOCAL:受虐气质 + ABL:LOCAL:痛苦中毒 + ABL:LOCAL:捆绑中毒, "0→0,12→8,LIMIT/0～8"))
;但[0→0, 3→0, 6→0～2, 9→0～4, 12→0～6]被加算(0～6)
CALL ADD_CRI_VAR("异常", , LINE_CALC(ABL:LOCAL:受虐气质 + ABL:LOCAL:痛苦中毒 + ABL:LOCAL:捆绑中毒, "3→0,12→6,LIMIT/0～6"))
;但[0→0, 9→0, 12→0～2]被加算(0～2)
CALL ADD_CRI_VAR("奴役", , LINE_CALC(ABL:LOCAL:受虐气质 + ABL:LOCAL:痛苦中毒 + ABL:LOCAL:捆绑中毒, "9→0,12→2,LIMIT/0～2"))

;-------------------------------------------------------------------------
;性別関係
;引数是调教者・调教对象
;同性で对应的能力や素質但ない场合是体的接触被避けるように
;-------------------------------------------------------------------------
@ACT_MENU_SEX, ARG = -1, ARG:1 = -1
LOCAL = ARG == -1 ? TARGET # ARG
LOCAL:1 = ARG == -1 ? MASTER # ARG:1

;调教者に双性恋但无く、男人同士
IF TALENT:LOCAL:男人 && TALENT:(LOCAL:1):男人 && !TALENT:LOCAL:双性恋
	;调教者的ＢＬ气质但[0→-2, 2→-1, 4→0]被加算(-2～0)
	CALL ADD_CRI_VAR("奉仕", LINE_CALC(ABL:LOCAL:ＢＬ气质, "0→-2,4→0,LIMIT/-2～0"))
	;调教者的ＢＬ气质但[0→-4, 1→-3, 2→-2, 3→-1, 4→0]被加算(-4～0)
	CALL ADD_CRI_VAR("爱抚", LINE_CALC(ABL:LOCAL:ＢＬ气质, "0→-4,4→0,LIMIT/-4～0"))
	CALL ADD_CRI_VAR("性交", LINE_CALC(ABL:LOCAL:ＢＬ气质, "0→-4,4→0,LIMIT/-4～0"))
ENDIF
;调教者に双性恋但无く、女人同士
IF !TALENT:LOCAL:男人 && !TALENT:(LOCAL:1):男人 && !TALENT:LOCAL:双性恋
	;调教者的百合气质但[0→-2, 2→-1, 4→0]被加算(-2～0)
	CALL ADD_CRI_VAR("奉仕", LINE_CALC(ABL:LOCAL:百合气质, "0→-2,4→0,LIMIT/-2～0"))
	;调教者的百合气质但[0→-4, 1→-3, 2→-2, 3→-1, 4→0]被加算(-4～0)
	CALL ADD_CRI_VAR("爱抚", LINE_CALC(ABL:LOCAL:百合气质, "0→-4,4→0,LIMIT/-4～0"))
	CALL ADD_CRI_VAR("性交", LINE_CALC(ABL:LOCAL:百合气质, "0→-4,4→0,LIMIT/-4～0"))
	CALL ADD_CRI_VAR("性奉", LINE_CALC(ABL:LOCAL:百合气质, "0→-4,4→0,LIMIT/-4～0"))
	
	;调教者的百合中毒但[0→0, 1→1, 3→2, 5→3]被加算(0～3)
	CALL ADD_CRI_VAR("爱抚", LINE_CALC(ABL:LOCAL:百合中毒, "1→1,5→3,LIMIT/0～3"))
	CALL ADD_CRI_VAR("性交", LINE_CALC(ABL:LOCAL:百合中毒, "1→1,5→3,LIMIT/0～3"))
	CALL ADD_CRI_VAR("性奉", LINE_CALC(ABL:LOCAL:百合中毒, "1→1,5→3,LIMIT/0～3"))
	CALL ADD_CRI_VAR("奉仕", LINE_CALC(ABL:LOCAL:百合中毒, "1→1,5→3,LIMIT/0～3"))
ENDIF

;调教者但恐男かつ、调教对象但男人
IF TALENT:LOCAL:恐男 && TALENT:(LOCAL:1):男人
	CALL ADD_CRI_VAR("会话",  2)
	CALL ADD_CRI_VAR("爱抚", -3)
	CALL ADD_CRI_VAR("性交", -4)
	CALL ADD_CRI_VAR("奉仕", -2)
	CALL ADD_CRI_VAR("施虐",  1)
	CALL ADD_CRI_VAR("性奉", -4)
ENDIF

[SKIPSTART]
/////////////////////////////////////
本日的方针
	0=通常         (平时原样)
	1=肛门       (欺负肛门)
	2=惩罚     (惩罚)
	3=强硬       (激烈)
	4=调情 (撒娇)
	5=异常         (直到坏掉)
	6=野外         (想要外出)
	7=做小孩       (想要怀孕)
	8=休息         (要求休息)
	9=抱住       (被抱住)
	10=ご奉仕      (想要侍奉)
	11=宠爱  (宠爱)
/////////////////////////////////////
[SKIPEND]
@ACT_MENU_POLICY
SELECTCASE POLICY("今日")
	CASE "肛门"
		;调教者但扶她であるか假阴茎持有的场合是性交系被誘发做ように
		IF (CFLAG:M好感 + CFLAG:好感 >= 10000) && (TALENT:TARGET:扶她 || ITEM:假阴茎)
			CALL ADD_CRI_VAR("会话", -10)
			CALL ADD_CRI_VAR("爱抚",  -8)
			CALL ADD_CRI_VAR("道具", -10)
			CALL ADD_CRI_VAR("性交",  10)
			CALL ADD_CRI_VAR("羞耻", -10)
			CALL ADD_CRI_VAR("奉仕",  -6)
			CALL ADD_CRI_VAR("施虐", -10)
			CALL ADD_CRI_VAR("异常", -10)
			CALL ADD_CRI_VAR("性奉", -10)
		;其他的场合
		ELSE
			CALL ADD_CRI_VAR("爱抚",  10)
			CALL ADD_CRI_VAR("道具",  10)
			CALL ADD_CRI_VAR("施虐", ITEM:灌肠套装 ? 10 # -10)
			CALL ADD_CRI_VAR("性奉", -10)
		ENDIF
	CASE "惩罚"
		;惩罚时惩罚标志但立つ的み
		;何らか的手入れ被してもいい的かも知れない
	CASE "强硬"
		CALL ADD_CRI_VAR("道具",   5)
		CALL ADD_CRI_VAR("奉仕",   8)
		CALL ADD_CRI_VAR("施虐",  10)
	CASE "调情"
		;调教对象但女かつ假阴茎持有的场合是性奉系被誘发做ように
		IF (CFLAG:M好感 + CFLAG:好感 >= 10000) && (ITEM:假阴茎 && !PENIS(MASTER))
			CALL ADD_CRI_VAR("会话",  -7)
			CALL ADD_CRI_VAR("爱抚",  -8)
			CALL ADD_CRI_VAR("道具", -10)
			CALL ADD_CRI_VAR("性交",  -5)
			CALL ADD_CRI_VAR("羞耻", -10)
			CALL ADD_CRI_VAR("奉仕",  -3)
			CALL ADD_CRI_VAR("施虐", -10)
			CALL ADD_CRI_VAR("异常", -10)
			CALL ADD_CRI_VAR("性奉",  10)
		ELSE
			CALL ADD_CRI_VAR("爱抚",   8)
			CALL ADD_CRI_VAR("道具", -10)
			CALL ADD_CRI_VAR("性交",  -5)
			CALL ADD_CRI_VAR("羞耻", -10)
			CALL ADD_CRI_VAR("奉仕",  10)
			CALL ADD_CRI_VAR("施虐", -10)
			CALL ADD_CRI_VAR("异常", -10)
			CALL ADD_CRI_VAR("性奉",  -5)
		ENDIF
	CASE "异常"
		CALL ADD_CRI_VAR("施虐",  10)
		CALL ADD_CRI_VAR("异常",  10, 10)
		CALL ADD_CRI_VAR("奴役",  10)
	CASE "做小孩"
		CALL ADD_CRI_VAR("会话",  -8)
		CALL ADD_CRI_VAR("爱抚",  -8)
		CALL ADD_CRI_VAR("道具", -10)
		CALL ADD_CRI_VAR("性交", -10)
		CALL ADD_CRI_VAR("羞耻", -10)
		CALL ADD_CRI_VAR("奉仕",  -4)
		CALL ADD_CRI_VAR("施虐", -10)
		CALL ADD_CRI_VAR("异常", -10)
		CALL ADD_CRI_VAR("性奉",  10)
	CASE "抱住"
		CALL ADD_CRI_VAR("会话", -10)
		CALL ADD_CRI_VAR("爱抚",  -9)
		CALL ADD_CRI_VAR("道具", -10)
		CALL ADD_CRI_VAR("性交",  10)
		CALL ADD_CRI_VAR("羞耻", -10)
		CALL ADD_CRI_VAR("奉仕",  -2)
		CALL ADD_CRI_VAR("施虐", -10)
		CALL ADD_CRI_VAR("异常", -10)
		CALL ADD_CRI_VAR("性奉", -10)
	CASE "ご奉仕"
		CALL ADD_CRI_VAR("会话", -10)
		CALL ADD_CRI_VAR("爱抚", -10)
		CALL ADD_CRI_VAR("道具", -10)
		CALL ADD_CRI_VAR("性交", -10)
		CALL ADD_CRI_VAR("羞耻", -10)
		CALL ADD_CRI_VAR("奉仕",  10)
		CALL ADD_CRI_VAR("施虐", -10)
		CALL ADD_CRI_VAR("异常", -10)
		CALL ADD_CRI_VAR("性奉",  -5)
	CASE "宠爱"
		CALL ADD_CRI_VAR("会话",   5)
		CALL ADD_CRI_VAR("爱抚",  10)
		CALL ADD_CRI_VAR("道具", -10)
		CALL ADD_CRI_VAR("羞耻", -10)
		CALL ADD_CRI_VAR("奉仕", -10)
		CALL ADD_CRI_VAR("施虐", -10)
		CALL ADD_CRI_VAR("异常", -10)
		CALL ADD_CRI_VAR("性奉", -10)
		;调教对象但男人不是场合、条件被满たしていれば性交系に+修正
		SIF !TALENT:MASTER:男人 && (CFLAG:M好感 + CFLAG:好感 >= 10000)
			CALL ADD_CRI_VAR("性交", 5)
	CASEELSE
		;none
ENDSELECT

;------------------------------------------
;本次的调教方针
;------------------------------------------
@ACT_MENU_COURCE

SELECTCASE POLICY("今")
	CASE "温和"
		CALL ADD_CRI_VAR("会话",   7)
		CALL ADD_CRI_VAR("爱抚",   5)
		CALL ADD_CRI_VAR("性交",  -3)
		CALL ADD_CRI_VAR("施虐", -10)
		CALL ADD_CRI_VAR("异常", -10)
		CALL ADD_CRI_VAR("性奉",  -3)
	CASE "普通"
		CALL ADD_CRI_VAR("施虐",  -5)
		CALL ADD_CRI_VAR("异常",  -5)
	CASE "强硬"
		CALL ADD_CRI_VAR("会话",  -5)
		CALL ADD_CRI_VAR("爱抚",  -5)
		CALL ADD_CRI_VAR("奉仕",  -3)
	CASE "异常"
		CALL ADD_CRI_VAR("会话",  -5)
		CALL ADD_CRI_VAR("爱抚",  -5)
		CALL ADD_CRI_VAR("奉仕",  -3)
		CALL ADD_CRI_VAR("施虐",  10)
		CALL ADD_CRI_VAR("异常",  10, 10)
		CALL ADD_CRI_VAR("奴役",  10)
;	CASE "SM"
;		CALL ADD_CRI_VAR("会话", -10)
;		CALL ADD_CRI_VAR("爱抚", -10)
;		SIF !TALENT:MASTER:男人
;			CALL ADD_CRI_VAR("性交", -20)
;		CALL ADD_CRI_VAR("奉仕", , -10)
;		CALL ADD_CRI_VAR("施虐", 15)
;		CALL ADD_CRI_VAR("异常", 10)
;		CALL ADD_CRI_VAR("性奉", -20)
ENDSELECT

;-------------------------------------------------------------------------
;PALAM群
;ここ是单独に
;-------------------------------------------------------------------------
;------------------------------------------
;润滑
;------------------------------------------
@ACT_MENU_MOIST_M, ARG = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG

;调教对象的润滑PALAM但[0→-6, 1000→-4, 2000→-2, 3000→0]被加算(-6～0)
LOCAL:1 = LINE_CALC(PALAM:LOCAL:润滑, "0→-6,3000→0,LIMIT/-6～0")
;调教者但慎重那么より減算
IF TALENT:慎重
	LOCAL:1 = LOCAL:1 * 8 / 5
;调教者但急躁那么軽減
ELSEIF TALENT:急躁
	LOCAL:1 = LOCAL:1 * 2 / 5
ENDIF
;道具是性交よりも減算量但小さい
CALL ADD_CRI_VAR("性交", LOCAL:1)
CALL ADD_CRI_VAR("道具", LOCAL:1 * 2 / 5)

;----------------------------------
;欲望
;----------------------------------
@ACT_MENU_DESIRE_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;调教对象的欲望PALAM但[0→3～6, 1000→2～4, 2000→1～2, 3000→0, 4000→-2, 5000→-4]被加算(-4～6)
CALL ADD_CRI_VAR("爱抚", LINE_CALC(PALAM:LOCAL:欲望, "0→3,3000→0,5000→-4,LIMIT/-4～3"), LINE_CALC(PALAM:LOCAL:欲望, "0→3,3000→0,LIMIT/0～3"))
;调教对象的欲望PALAM但[0→-2, 1000→-1, 2000→2, 3000→4, 4000→6, 5000→4]被加算(-2～6)
CALL ADD_CRI_VAR("性交", LINE_CALC(PALAM:LOCAL:欲望, "0→-2,2000→0,2000→2,4000→6,5000→4,LIMIT/-2～6"))
CALL ADD_CRI_VAR("性奉", LINE_CALC(PALAM:LOCAL:欲望, "0→-2,2000→0,2000→2,4000→6,5000→4,LIMIT/-2～6"))
;调教对象的欲望PALAM但[0→1, 1000→2, 2000→3, 3000→4, 4000→3, 5000→2]被加算(1～4)
CALL ADD_CRI_VAR("道具", LINE_CALC(PALAM:LOCAL:欲望, "0→1,3000→4,5000→2,LIMIT/1～4"))

;----------------------------------
;屈服
;----------------------------------
@ACT_MENU_YIELD_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;调教对象的屈服PALAM但[4000→-2～-1, 5000→-4～-2, 6000→-5～-2, 7000→-7～-3, 8000→-8～-3]被加算(-8～0)
CALL ADD_CRI_VAR("施虐", LINE_CALC(PALAM:LOCAL:屈服, "4000→-1,8000→-3,LIMIT/-3～0"), LINE_CALC(PALAM:LOCAL:屈服, "4000→-1,8000→-5,LIMIT/-5～0"))
;调教对象的屈服PALAM但[4000→-2～0, 5000→-4～0, 6000→-6～0, 7000→-8～0, 8000→-10～0]被加算(-10～0)
CALL ADD_CRI_VAR("奴役", , LINE_CALC(PALAM:LOCAL:屈服, "4000→-2,8000→-10,LIMIT/-10～0"))

;----------------------------------
;恭顺
;----------------------------------
@ACT_MENU_ALLEGIANCE_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;调教对象的恭顺PALAM但[4000→0, 8000→3]被加算(0～3)
CALL ADD_CRI_VAR("爱抚", LINE_CALC(PALAM:LOCAL:恭顺, "4000→0,8000→3,LIMIT/0～3"))
;调教对象的恭顺PALAM但[4000→1, 5000→2, 6000→3, 7000→4, 8000→5]被加算(0～6)
CALL ADD_CRI_VAR("性交", LINE_CALC(PALAM:LOCAL:恭顺, "4000→1,8000→5,LIMIT/0～6"))
CALL ADD_CRI_VAR("奉仕", LINE_CALC(PALAM:LOCAL:恭顺, "4000→1,8000→5,LIMIT/0～6"))
CALL ADD_CRI_VAR("性奉", LINE_CALC(PALAM:LOCAL:恭顺, "4000→1,8000→5,LIMIT/0～6"))
;调教对象的恭顺PALAM但[4000→0, 5000→-1, 6000→-2, 7000→-3, 8000→-4]被加算(-4～0)
CALL ADD_CRI_VAR("施虐", LINE_CALC(PALAM:LOCAL:恭顺, "4000→0,8000→-4,LIMIT/-4～0"))

;----------------------------------
;调教对象的射精ゲージ
;----------------------------------
@ACT_MENU_ECSTASY_M, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG
;调教对象的射精BASE但[0%→-3, 100%→0]被加算(-3～0)
SIF PENIS(LOCAL) && !TEQUIP:性交奉仕中
	CALL ADD_CRI_VAR("性奉", LINE_CALC(10 * BASE:LOCAL:射精 / MAXBASE:LOCAL:射精, "0→-3,10→0,LIMIT/-3～0"))

;----------------------------------
;勃起度
;
;需要强制勃起模式时性奉的ペナルティ无
;に一应してみる
;---------------------------------
@ACT_MENU_ERECTION_M
;勃起度(需要强制勃起模式)
IF TCVAR:MASTER:需要强制勃起
	;调教对象的勃起度但[400→5,1000→0]被加算(0～5)
	CALL ADD_CRI_VAR("爱抚", LINE_CALC(TCVAR:MASTER:勃起度, "400→5,1000→0,LIMIT/0～5"))
ELSE
	;调教对象的勃起度但[400→-2,1000→-1]被加算(-3～0)
	CALL ADD_CRI_VAR("性奉", LINE_CALC(TCVAR:MASTER:勃起度, "400→-2,1000→-1,LIMIT/-3～0"))
ENDIF

;-------------------------------------------------------------------------
;调教者的ベース被参照
;-------------------------------------------------------------------------
;----------------------------------
;调教者的快感(快ｃｖａｂ的总计)
;---------------------------------
@ACT_MENU_PREASURE_T, ARG = -1
#LOCALSIZE 2
LOCAL = ARG == -1 ? TARGET # ARG

;调教者快感的总计被計算
LOCAL:1 = PALAM:快Ｃ + PALAM:快Ｖ + PALAM:快Ａ + PALAM:快Ｂ
;总计但[0→0, 600→1, 1200→2, 1800→3, 2400→4]被加算(0～4)
CALL ADD_CRI_VAR("奉仕", LINE_CALC(LOCAL:1, "0→0,2400→4,LIMIT/0～4"))
CALL ADD_CRI_VAR("性奉", LINE_CALC(LOCAL:1, "0→0,2400→4,LIMIT/0～4"))
;总计但[600→0, 1200→1, 1800→2, 2400→3]被加算(0～4)
CALL ADD_CRI_VAR("性交", LINE_CALC(LOCAL:1, "600→0,2400→3,LIMIT/0～4"))
;总计但[1200→0, 1800→-1, 2400→-2]被加算(-2～0)
CALL ADD_CRI_VAR("爱抚", LINE_CALC(LOCAL:1, "1200→0,2400→-2,LIMIT/-2～0"))
CALL ADD_CRI_VAR("道具", LINE_CALC(LOCAL:1, "1200→0,2400→-2,LIMIT/-2～0"))

;-------------------------------------
;调教者的体力
;-------------------------------------
@ACT_MENU_PHYSICAL_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;调教者的体力但[0→4, 500→2, 1000→0, 1500→-2]被加算(-2～4)
CALL ADD_CRI_VAR("会话", LINE_CALC(BASE:LOCAL:体力, "0→4,1500→-2,LIMIT/-2～4"))
;调教者的体力但[0→2, 500→1, 1000→0]被加算(0～2)
CALL ADD_CRI_VAR("奉仕", LINE_CALC(BASE:LOCAL:体力, "0→2,1000→0,LIMIT/0～2"))
;调教者的体力但[0→-2, 500→-1, 1000→0]被加算(-2～0)
CALL ADD_CRI_VAR("爱抚", LINE_CALC(BASE:LOCAL:体力, "0→-2,1000→0,LIMIT/-2～0"))
CALL ADD_CRI_VAR("施虐", LINE_CALC(BASE:LOCAL:体力, "0→-2,1000→0,LIMIT/-2～0"))
;调教者的体力但[0→-3, 500→-2, 1000→-1, 1500→0]被加算(-3～0)
CALL ADD_CRI_VAR("性交", LINE_CALC(BASE:LOCAL:体力, "0→-3,1500→0,LIMIT/-3～0"))

;-------------------------------------
;调教者的气力
;-------------------------------------
@ACT_MENU_MENTAL_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;调教者的气力但[0→3, 250→2, 500→1, 1000→0]被加算(0～3)
CALL ADD_CRI_VAR("奉仕", LINE_CALC(BASE:LOCAL:气力, "0→3,500→1,1000→0,LIMIT/0～3"))
;调教者的气力但[0→2, 250→1, 500→0, 1000→-1]被加算(-1～2)
CALL ADD_CRI_VAR("奴役", LINE_CALC(BASE:LOCAL:气力, "0→2,500→0,1000→-1,LIMIT/-1～2"))
;调教者的气力但[0→-2, 250→-1, 500→0, 1000→2]被加算(-2～2)
CALL ADD_CRI_VAR("爱抚", LINE_CALC(BASE:LOCAL:气力, "0→-2,1000→2,LIMIT/-2～2"))
CALL ADD_CRI_VAR("性交", LINE_CALC(BASE:LOCAL:气力, "0→-2,1000→2,LIMIT/-2～2"))
;调教者的气力但[0→-2, 250→-1, 500→0, 1000→1]被加算(-2～1)
CALL ADD_CRI_VAR("道具", LINE_CALC(BASE:LOCAL:气力, "0→-2,500→0,1000→1,LIMIT/-2～1"))
;调教者的气力但[0→-2, 250→-1, 500→0]被加算(-2～0)
CALL ADD_CRI_VAR("羞耻", LINE_CALC(BASE:LOCAL:气力, "0→-2,500→0,LIMIT/-2～0"))
CALL ADD_CRI_VAR("性奉", LINE_CALC(BASE:LOCAL:气力, "0→-2,500→0,LIMIT/-2～0"))
;调教者的气力但[0→-3, 250→-2, 500→-1, 1000→1]被加算(-3～1)
CALL ADD_CRI_VAR("施虐", LINE_CALC(BASE:LOCAL:气力, "0→-3,1000→1,LIMIT/-3～1"))
CALL ADD_CRI_VAR("异常", LINE_CALC(BASE:LOCAL:气力, "0→-3,1000→1,LIMIT/-3～1"))

;-------------------------------------
;调教者的理性
;-------------------------------------
@ACT_MENU_REASON_T, ARG = -1
#LOCALSIZE 1
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;调教者的理性但[0→0～4, 250→0～2, 500→0]被加算(0～4)
FOR LCOUNT, 0, 11
	CALL ADD_CRI_VAR, MENU(LCOUNT), , LINE_CALC(BASE:LOCAL:理性, "0→4,500→0,LIMIT/0～4")
NEXT
;调教者的理性但750被超過していれば-4～0被加算
SIF BASE:LOCAL:理性 > 750
	CALL ADD_CRI_VAR("异常", , -4)

;-------------------------------------
;调教者的兴趣
;-------------------------------------
@ACT_MENU_INTEREST_T, ARG = -1
#LOCALSIZE 2
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;调教者的兴趣被取得做
LOCAL:1 = BASE:LOCAL:兴趣
FOR LCOUNT, 0, 11
	;これら是[0→3, 250→1, 500→0, 750→-2, 1000→-3](-3～3)
	SIF MENU(LCOUNT) == "奴役"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→3,1000→-3,LIMIT/-3～3"))
	;これら是[0→-3, 250→-2, 500→0, 750→1, 1000→3](-3～3)
	SIF GROUPMATCH(MENU(LCOUNT), "会话", "爱抚", "性交")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→-3,1000→3,LIMIT/-3～3"))
	;これら是[0→-2, 250→-1, 500→0, 750→1, 1000→2](-2～2)
	SIF GROUPMATCH(MENU(LCOUNT), "道具", "奉仕")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→-2,1000→2,LIMIT/-2～2"))
	;これら是[0→-3, 250→-2, 500→-1, 750→0, 1000→1](-3～1)
	SIF MENU(LCOUNT) == "性奉"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→-3,1000→1,LIMIT/-3～1"))
NEXT

;-------------------------------------
;调教者的焦躁
;-------------------------------------
@ACT_MENU_IRRITATION_T, ARG = -1
#LOCALSIZE 2
#DIM LCOUNT
LOCAL = ARG == -1 ? TARGET # ARG

;调教者的焦躁被取得做
LOCAL:1 = BASE:LOCAL:焦躁
FOR LCOUNT, 0, 11
	;これら是[200→2, 400→3, 600→4, 800→5](2～5)
	SIF MENU(LCOUNT) == "施虐"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "200→2,800→5,LIMIT/2～5"))
	;これら是[0→1, 200→2, 400→3, 600→4, 800→5](1～5)
	SIF MENU(LCOUNT) == "奴役"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→1,800→5,LIMIT/1～5"))
	;これら是[400→0, 600→-2, 800→-3](-3～0)
	SIF GROUPMATCH(MENU(LCOUNT), "道具", "性交")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "400→0,800→-3,LIMIT/-3～0"))
	;これら是[200→0, 400→-1, 600→-3, 800→-5](-5～0)
	SIF GROUPMATCH(MENU(LCOUNT), "会话", "爱抚")
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "400→-1,800→-5,LIMIT/-5～0"))
	;これら是[0→0, 200→-1, 400→-2, 600→-3, 800→-4](-4～0)
	SIF MENU(LCOUNT) == "性奉"
		CALL ADD_CRI_VAR(MENU(LCOUNT), LINE_CALC(LOCAL:1, "0→0,800→-4,LIMIT/-4～0"))
NEXT

;-------------------------------------
;调教者的好感
;-------------------------------------
@ACT_MENU_LIKE_T

;调教者的好感但[-2000→-6, 2000→0]被加算(-6～0)
CALL ADD_CRI_VAR("性奉", LINE_CALC(CFLAG:好感, "-2000→-6,2000→0,LIMIT/-6～0"))
;调教者的好感但[-2000→-5, 2000→0]被加算(-5～0)
CALL ADD_CRI_VAR("性交", LINE_CALC(CFLAG:好感, "-2000→-5,2000→0,LIMIT/-5～0"))
;调教者的好感但[-2000→-4, 2000→4]被加算(-4～4)
CALL ADD_CRI_VAR("爱抚", LINE_CALC(CFLAG:好感, "-2000→-4,2000→4,LIMIT/-4～4"))

;-------------------------------------
;调教对象的好感
;主人公から调教者へ的好感是调教者自身但保持している的に注意
;-------------------------------------
@ACT_MENU_LIKE_M

;调教对象的好感但[-2000→-10, 2000→0]被加算(-10～0)
CALL ADD_CRI_VAR("性奉", LINE_CALC(CFLAG:M好感, "-2000→-10,2000→0,LIMIT/-10～0"))
;调教对象的好感但[-2000→-12, 2000→0]被加算(-12～0)
CALL ADD_CRI_VAR("性交", LINE_CALC(CFLAG:M好感, "-2000→-12,2000→0,LIMIT/-12～0"))
;调教对象的好感但[-2000→-4, 2000→4]被加算(-4～4)
CALL ADD_CRI_VAR("奉仕", LINE_CALC(CFLAG:M好感, "-2000→-4,2000→4,LIMIT/-4～4"))

;-------------------------------------
;调教者的状态
;-------------------------------------
@ACT_MENU_CONDITION_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;调教者的状态(0=通常/1=疲惫/2=衰弱/3=无气力/4=朦胧/5=情欲/6=愤怒/7=无聊/8=郁闷)
SELECTCASE TCVAR:状态变化
	;疲惫・衰弱
	CASE 1, 2
		;调教者的疲れ
		CALL ADD_CRI_VAR("会话",   TCVAR:状态变化 * 2 + TFLAG:调教者疲惫度)
		CALL ADD_CRI_VAR("性交", -(TCVAR:状态变化 * 3 + TFLAG:调教者疲惫度))
		CALL ADD_CRI_VAR("施虐", -(TCVAR:状态变化 * 2 + TFLAG:调教者疲惫度))
		CALL ADD_CRI_VAR("性奉", -(TCVAR:状态变化 * 3 + TFLAG:调教者疲惫度))
	;无气力
	CASE 3
		CALL ADD_CRI_VAR("爱抚", -3)
		CALL ADD_CRI_VAR("道具", -1)
		CALL ADD_CRI_VAR("性交", -5)
		CALL ADD_CRI_VAR("奉仕",  5)
		CALL ADD_CRI_VAR("施虐", -3)
		CALL ADD_CRI_VAR("异常", -1)
		CALL ADD_CRI_VAR("奴役",  3)
	;朦胧
	CASE 4
		CALL ADD_CRI_VAR("爱抚", -3)
		CALL ADD_CRI_VAR("道具", -7)
		CALL ADD_CRI_VAR("性交", -3)
		CALL ADD_CRI_VAR("羞耻",   , 6)
		CALL ADD_CRI_VAR("奉仕",   , 3)
		CALL ADD_CRI_VAR("施虐",   , 2)
		CALL ADD_CRI_VAR("异常",   , 2)
		CALL ADD_CRI_VAR("性奉",   , 2)
	;情欲
	CASE 5
		CALL ADD_CRI_VAR("爱抚",  2)
		CALL ADD_CRI_VAR("道具",  1)
		CALL ADD_CRI_VAR("性交", TALENT:冲动的 ? 7 # 4, 5)
		CALL ADD_CRI_VAR("羞耻", -3)
		CALL ADD_CRI_VAR("奉仕",  2, 3)
		CALL ADD_CRI_VAR("施虐",   , 2)
		CALL ADD_CRI_VAR("异常",   , 2)
		CALL ADD_CRI_VAR("性奉", TALENT:冲动的 ? 7 # 4, 5)
	;愤怒
	CASE 6
		;TALENT:冲动的 = 冲动的　TALENT:自制的 = 自制的
		CALL ADD_CRI_VAR("施虐", , 7 + TALENT:冲动的 * 5 - TALENT:自制的 * 3)
	;无聊
	CASE 7
		CALL ADD_CRI_VAR("会话", -5)
		CALL ADD_CRI_VAR("异常",   , 5)
		CALL ADD_CRI_VAR("奴役",   , 5)
	;郁闷
	CASE 8
		CALL ADD_CRI_VAR("爱抚", ,  5)
		CALL ADD_CRI_VAR("道具", ,  5)
		CALL ADD_CRI_VAR("性交", ,  5)
		CALL ADD_CRI_VAR("羞耻", ,  5)
		CALL ADD_CRI_VAR("奉仕", ,  5)
		CALL ADD_CRI_VAR("施虐", ,  8)
		CALL ADD_CRI_VAR("异常", , 11)
		CALL ADD_CRI_VAR("奴役", ,  8)
		CALL ADD_CRI_VAR("性奉", ,  5)
ENDSELECT

;-------------------------------------
;调教对象的状态
;欲望している调教对象被みて调教者但どう思うか
;つまり素質的参照先是调教者
;-------------------------------------
@ACT_MENU_CONDITION_M, ARG = -1
#DIM DYNAMIC TGT
TGT = ARG == -1 ? TARGET # ARG

;调教对象的状态但情欲
IF IS_CONDITION(TARGET, "情欲")
	;调教者但反抗的/自制的/否定快感/虐待狂/坏心眼的うち２つ以上持有
	IF TRUECHECK(TALENT:TGT:反抗的, TALENT:TGT:自制的, TALENT:TGT:否定快感, TALENT:TGT:虐待狂, TALENT:TGT:坏心眼) > 1
		CALL ADD_CRI_VAR("羞耻", 2)
		CALL ADD_CRI_VAR("奉仕", 1)
		CALL ADD_CRI_VAR("性奉", 4)
	ELSE
		CALL ADD_CRI_VAR("性交", 3)
		CALL ADD_CRI_VAR("性奉", 4)
	ENDIF
ENDIF

;-------------------------------------
;调教对象的疲労
;TFLAG:疲惫度 = 疲労度
;性奉但ない的是違和感
;-------------------------------------
@ACT_MENU_TIRED_M

;调教对象的疲惫标志(疲れ)
IF TFLAG:疲惫度
	CALL ADD_CRI_VAR("性交", -TFLAG:疲惫度 / 2)
	CALL ADD_CRI_VAR("奉仕", -TFLAG:疲惫度 / 2)
	CALL ADD_CRI_VAR("施虐", -TFLAG:疲惫度)
	CALL ADD_CRI_VAR("异常", -TFLAG:疲惫度 / 2)
	CALL ADD_CRI_VAR("奴役", -TFLAG:疲惫度 / 2)
ENDIF

;-------------------------------------
;初手会话修正
;-------------------------------------
@ACT_MENU_FIRST

;初用手高确率会话
SIF !TFLAG:经过时间
	CALL ADD_CRI_VAR("会话", 10)

;-------------------------------------
;大镜奖励
;-------------------------------------
@ACT_MENU_MILLER
SIF ITEM:大镜
	CALL ADD_CRI_VAR("羞耻", , 3)

;──────────────────────────────
;状況や上次的指令根据变动
;直前的调教对象的反应
;-------------------------------------
;更加强硬
;-------------------------------------
@ACT_MENU_PLEASE_HARD

IF IS_NOWCOMNAME("更加强硬")
	CALL ADD_CRI_VAR("爱抚", -2)
	CALL ADD_CRI_VAR("性交", -1)
	CALL ADD_CRI_VAR("奉仕", -1)
	CALL ADD_CRI_VAR("施虐",  4)
	CALL ADD_CRI_VAR("异常",  2)
ENDIF

;-------------------------------------
;请求宽恕
;虐待狂OR坏心眼
;-------------------------------------
@ACT_MENU_PLEASE_HELP

IF IS_NOWCOMNAME("请求宽恕")
	;调教对象但郁闷以外
	IF TCVAR:状态变化 < 8
		CALL ADD_CRI_VAR("施虐", -5, (TALENT:虐待狂 || TALENT:坏心眼) ? 4 # 0)
		CALL ADD_CRI_VAR("异常", -5, (TALENT:虐待狂 || TALENT:坏心眼) ? 4 # 0)
	ENDIF
ENDIF

;-------------------------------------
;気持よくして
;-------------------------------------
@ACT_MENU_PLEASE_PREASURE

IF IS_NOWCOMNAME("保持心情舒畅")
	CALL ADD_CRI_VAR("爱抚", 2)
	CALL ADD_CRI_VAR("道具", 3)
	CALL ADD_CRI_VAR("性交", 2)
	CALL ADD_CRI_VAR("性奉", 3)
ENDIF

;-------------------------------------
;发狂
;-------------------------------------
@ACT_MENU_VIOLENT

IF IS_NOWCOMNAME("发狂")
	CALL ADD_CRI_VAR("爱抚", -2)
	CALL ADD_CRI_VAR("性交", -5)
	CALL ADD_CRI_VAR("施虐",  5)
	CALL ADD_CRI_VAR("奴役",  5)
	CALL ADD_CRI_VAR("性奉", -6)
ENDIF

;-------------------------------------
;道具解除
;-------------------------------------
@ACT_MENU_UNEQUIP

IF IS_NOWCOMNAME("移除道具")
	CALL ADD_CRI_VAR("施虐", 10)
ENDIF

;-------------------------------------
;推到时的连续爱抚修正
;-------------------------------------
@ACT_MENU_PETTING_CONTINUE

;推到中で、前的回合的菜单但爱抚
IF TEQUIP:推到中 && TFLAG:前ACT分类 == 1
	CALL ADD_CRI_VAR("爱抚", 20)
ENDIF

;-------------------------------------
;シックスナイン誘发
;-------------------------------------
@ACT_MENU_69

;シックスナインになる傾向(TFLAG:前ACT分类 = 前的回合的调教菜单)
SELECTCASE TFLAG:前ACT分类
	CASE 1
		CALL ADD_CRI_VAR("奉仕", 1, 2)
	CASE 5
		CALL ADD_CRI_VAR("爱抚", 1, 2)
ENDSELECT

;--------------------------------------
;调教者的服装
;-------------------------------------
@ACT_MENU_CLOTH_T, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? TARGET # ARG

;服装
;LOCAL:3（性交）、LOCAL:10（性交奉仕）に影响
;调教者但下半身外套被着ている
IF TEQUIP:LOCAL:下半身外套
	CALL ADD_CRI_VAR("性交", -2)
	CALL ADD_CRI_VAR("性奉", -2)
ENDIF
;调教者但上半身外套被着ている
IF TEQUIP:LOCAL:上半身外套
	CALL ADD_CRI_VAR("性交", -1)
	CALL ADD_CRI_VAR("性奉", -1)
ENDIF
;调教者但全身外套被着ている
IF TEQUIP:LOCAL:全身外套
	CALL ADD_CRI_VAR("性交", -3)
	CALL ADD_CRI_VAR("性奉", -3)
ENDIF

;--------------------------------------
;调教对象的服装
;对象的服装是2部位で定義されてた
;そ的うち一部位被注释アウト
;-------------------------------------
@ACT_MENU_CLOTH_M, ARG = -1, ARG:1 = -1
#LOCALSIZE 2
LOCAL = ARG   == -1 ? TARGET # ARG
LOCAL = ARG:1 == -1 ? MASTER # ARG:1

;;调教对象但下半身外套被着ている
;IF TEQUIP:(LOCAL:1):下半身外套
;	;调教者に穿墙但ない
;	SIF !TALENT:LOCAL:穿墙
;		CALL ADD_CRI_VAR("性交", -2)
;	CALL ADD_CRI_VAR("性奉", -2)
;ENDIF
;调教对象但上半身外套被着ている
;IF TEQUIP:(LOCAL:1):上半身外套
;	;调教者に穿墙但ない
;	SIF !TALENT:LOCAL:穿墙
;		CALL ADD_CRI_VAR("性交", -1)
;	CALL ADD_CRI_VAR("性奉", -1)
;ENDIF
;调教对象但全身外套被着ている
;IF TEQUIP:(LOCAL:1):全身外套
;	;调教者に穿墙但ない
;	SIF !TALENT:LOCAL:穿墙
;		CALL ADD_CRI_VAR("性交", -3)
;	CALL ADD_CRI_VAR("性奉", -3)
;ENDIF
;外套
;调教对象的下半身外套で妨害され时き
IF CLOTHESTYPE(1, 1) == 2
	CALL ADD_CRI_VAR("会话", 1, 4)
	;调教者に穿墙但ない
	SIF !TALENT:LOCAL:穿墙
		CALL ADD_CRI_VAR("爱抚", , -2)
	CALL ADD_CRI_VAR("道具", -2, -3)
	;调教者に穿墙但ない
	SIF !TALENT:LOCAL:穿墙
		CALL ADD_CRI_VAR("性交", -1, -5)
	CALL ADD_CRI_VAR("羞耻", , -7)
	CALL ADD_CRI_VAR("性奉", -1, -5)
;调教对象的下半身外套但あ时き
ELSEIF CLOTHESTYPE(1, 1)
	CALL ADD_CRI_VAR("会话", , 3)
	;调教者に穿墙但ない
	SIF !TALENT:LOCAL:穿墙
		CALL ADD_CRI_VAR("爱抚", , -1)
	CALL ADD_CRI_VAR("道具", -1, -2)
	;调教者に穿墙但ない
	SIF !TALENT:LOCAL:穿墙
		CALL ADD_CRI_VAR("性交", , -1)
	CALL ADD_CRI_VAR("性奉", , -1)
ENDIF
;调教对象的上半身外套但あ时き
IF CLOTHESTYPE(1, 3)
	CALL ADD_CRI_VAR("会话", , 2)
	;调教者に穿墙但ない
	SIF !TALENT:LOCAL:穿墙
		CALL ADD_CRI_VAR("爱抚", , -2)
	CALL ADD_CRI_VAR("道具", , -2)
	CALL ADD_CRI_VAR("羞耻", , -4)
ENDIF

;内衣
;调教对象的下半身内衣但あ时き
IF CLOTHESTYPE(1, 0)
	CALL ADD_CRI_VAR("会话", , 1)
	;调教者に穿墙但ない
	SIF !TALENT:LOCAL:穿墙
		CALL ADD_CRI_VAR("爱抚", , -1)
	CALL ADD_CRI_VAR("道具", -1, -1)
	;调教者に穿墙但ない
	SIF !TALENT:LOCAL:穿墙
		CALL ADD_CRI_VAR("性交", , -4)
	CALL ADD_CRI_VAR("性奉", , -4)
ENDIF
;调教对象的上半身内衣但あ时き
IF CLOTHESTYPE(1, 2)
	CALL ADD_CRI_VAR("会话", , 2)
	;调教者に穿墙但ない
	SIF !TALENT:LOCAL:穿墙
		CALL ADD_CRI_VAR("爱抚", , -2)
	CALL ADD_CRI_VAR("道具", , -4)
	CALL ADD_CRI_VAR("羞耻", , -2)
ENDIF

;----------------------------------------------------------
;TEQUIP相关
;---------------------------------------------------------
;-------------------------------------
;媚药
;-------------------------------------
@ACT_MENU_DRAG

IF TEQUIP:媚药
	CALL ADD_CRI_VAR("会话", , 4)
	CALL ADD_CRI_VAR("爱抚", TEQUIP:媚药 / 2)
	CALL ADD_CRI_VAR("道具", TEQUIP:媚药 / 2)
	CALL ADD_CRI_VAR("性交", TEQUIP:媚药 / 2)
	CALL ADD_CRI_VAR("羞耻", TEQUIP:媚药 / 3)
	CALL ADD_CRI_VAR("性奉", TEQUIP:媚药 / 2)
ENDIF

;-------------------------------------
;震动棒
;助手的A感觉但高和、性交した但る？
;-------------------------------------
@ACT_MENU_VIBE

;震动棒
IF TEQUIP:震动棒
	CALL ADD_CRI_VAR("爱抚", ,  3)
	CALL ADD_CRI_VAR("道具", ,  6)
	CALL ADD_CRI_VAR("性交", , -9)
	SIF ASSI >= 0
		CALL ADD_CRI_VAR("性交", GET_ABL(ASSI, "Ａ感觉"))
	CALL ADD_CRI_VAR("羞耻", 7)
	CALL ADD_CRI_VAR("施虐", , 2)
ENDIF

;-------------------------------------
;A震动棒
;-------------------------------------
@ACT_MENU_AVIBE

;肛门震动棒
IF TEQUIP:肛门震动棒
	CALL ADD_CRI_VAR("爱抚", , 3)
	CALL ADD_CRI_VAR("道具", , 6)
	;震动棒但ない场合做爱了但る
	CALL ADD_CRI_VAR("性交", TEQUIP:震动棒 ? 0 # 8)
	CALL ADD_CRI_VAR("羞耻", 8)
	CALL ADD_CRI_VAR("施虐", , 5)
	CALL ADD_CRI_VAR("异常", , 4)
ENDIF

;-------------------------------------
;肛门拉珠
;-------------------------------------
@ACT_MENU_ABEADS

;肛门拉珠
IF TEQUIP:肛门拉珠
	CALL ADD_CRI_VAR("爱抚", , 5)
	CALL ADD_CRI_VAR("道具", , 8)
	CALL ADD_CRI_VAR("性交", TEQUIP:震动棒 ? 0 # 2)
	CALL ADD_CRI_VAR("羞耻", 5)
	CALL ADD_CRI_VAR("施虐", , 3)
	CALL ADD_CRI_VAR("异常", , 1)
ENDIF

;-------------------------------------
;肛门肛塞
;-------------------------------------
@ACT_MENU_APLUG

;灌肠＋肛门肛塞
IF TEQUIP:灌肠器＋肛塞
	CALL ADD_CRI_VAR("会话", , 5)
	CALL ADD_CRI_VAR("爱抚", , 3)
	CALL ADD_CRI_VAR("道具", , 3)
	CALL ADD_CRI_VAR("性交", , TEQUIP:震动棒 ? 0 # 1)
	CALL ADD_CRI_VAR("羞耻", 8)
	CALL ADD_CRI_VAR("施虐", , 7)
	CALL ADD_CRI_VAR("异常", , 7)
ENDIF

;-------------------------------------
;阴蒂夹
;-------------------------------------
@ACT_MENU_CRICAP

;阴蒂夹
IF TEQUIP:阴蒂夹
	CALL ADD_CRI_VAR("爱抚", , 2)
	CALL ADD_CRI_VAR("道具", , 2)
	CALL ADD_CRI_VAR("羞耻", 4)
ENDIF

;-------------------------------------
;飞机杯
;-------------------------------------
@ACT_MENU_ONAHOLE

IF TEQUIP:飞机杯
	CALL ADD_CRI_VAR("爱抚", , 2)
	CALL ADD_CRI_VAR("道具", , 2)
	CALL ADD_CRI_VAR("羞耻", 4)
	CALL ADD_CRI_VAR("异常", 1)
ENDIF

;-------------------------------------
;乳头夹
;-------------------------------------
@ACT_MENU_NIPPLECAP

IF TEQUIP:乳头夹
	CALL ADD_CRI_VAR("爱抚", , 2)
	CALL ADD_CRI_VAR("道具", , 2)
	CALL ADD_CRI_VAR("羞耻", 4 + TALENT:MASTER:男人)
	CALL ADD_CRI_VAR("异常", , TALENT:MASTER:男人 ? 5 # 0)
ENDIF

;-------------------------------------
;绳
;-------------------------------------
@ACT_MENU_ROPE

IF TEQUIP:绳
	CALL ADD_CRI_VAR("会话",  3)
	CALL ADD_CRI_VAR("性交", , -1)
	CALL ADD_CRI_VAR("奉仕", -7)
	CALL ADD_CRI_VAR("施虐",  3)
	CALL ADD_CRI_VAR("异常",  , 2)
	CALL ADD_CRI_VAR("性奉", -3)
ENDIF

;-------------------------------------
;眼罩
;-------------------------------------
@ACT_MENU_EYEMASK

IF TEQUIP:眼罩
	CALL ADD_CRI_VAR("会话",   , 4)
	CALL ADD_CRI_VAR("羞耻",  3, 4)
	CALL ADD_CRI_VAR("奉仕", -5)
	CALL ADD_CRI_VAR("施虐",  1)
	CALL ADD_CRI_VAR("异常",   , 3)
ENDIF

;-------------------------------------
;口球
;-------------------------------------
@ACT_MENU_GAG

IF TEQUIP:口球
	CALL ADD_CRI_VAR("会话", -10)
	CALL ADD_CRI_VAR("奉仕",  -9)
	CALL ADD_CRI_VAR("施虐",   2)
	CALL ADD_CRI_VAR("异常", , 4)
ENDIF

;-------------------------------------
;三角木马
;-------------------------------------
@ACT_MENU_HORSE

IF TEQUIP:三角木马
	CALL ADD_CRI_VAR("会话",   , 6)
	CALL ADD_CRI_VAR("爱抚", -6)
	CALL ADD_CRI_VAR("道具", -6)
	CALL ADD_CRI_VAR("羞耻", -3)
	CALL ADD_CRI_VAR("施虐",  7, 2)
	CALL ADD_CRI_VAR("异常",  3, 7)
ENDIF

;-------------------------------------
;野外PLAY
;-------------------------------------
@ACT_MENU_OUT

SIF TEQUIP:野外PLAY
	CALL ADD_CRI_VAR("羞耻", , 6)

;-------------------------------------
;大镜
;-------------------------------------
@ACT_MENU_T_MILLER

SIF TEQUIP:大镜
	CALL ADD_CRI_VAR("羞耻", , 8)

;-------------------------------------
;道具解除制限
;体力但ある的にフルアーマーしてる那么道具菜单被選ばないように(道具解除になるから)
;ちょっ和处理被变えてみるテスト。拙かったらごめんなさい。
;・男人那么Ｖ系无しでもフルアーマー和みなすように
;-------------------------------------
@ACT_MENU_KILL_UNEQUIP, ARG = -1
#LOCALSIZE 1
LOCAL = ARG == -1 ? MASTER # ARG

IF (BASE:LOCAL:体力 + BASE:LOCAL:气力) > 500
	SIF (TALENT:LOCAL:男人 || TEQUIP:震动棒 || TEQUIP:假阴茎) && TEQUIP:肛门震动棒 && (TEQUIP:阴蒂夹 || TEQUIP:飞机杯) && TEQUIP:乳头夹
		CALL ADD_CRI_VAR("道具", -15)
ENDIF

;-------------------------------------
;调教者自慰中
;-------------------------------------
@ACT_MENU_MASTURBATION_T

;调教者自慰中
IF GETBIT(TEQUIP:自慰中, 1)
	CALL ADD_CRI_VAR("会话",  , 4)
	CALL ADD_CRI_VAR("道具",  , 4)
	CALL ADD_CRI_VAR("性交",  , 4)
	CALL ADD_CRI_VAR("羞耻", 2, 4)
	CALL ADD_CRI_VAR("奉仕", 2, 4)
	CALL ADD_CRI_VAR("性奉",  , 4)
ENDIF

;-------------------------------------
;调教对象自慰中
;-------------------------------------
@ACT_MENU_MASTURBATION_M

;自慰中
IF GETBIT(TEQUIP:自慰中, 0)
	CALL ADD_CRI_VAR("会话", 2, 4)
	CALL ADD_CRI_VAR("道具", 2, 4)
	CALL ADD_CRI_VAR("性交", ,  4)
	CALL ADD_CRI_VAR("羞耻", 2, 4)
	CALL ADD_CRI_VAR("奉仕",  , 4)
	CALL ADD_CRI_VAR("性奉",  -10)
ENDIF

;-------------------------------------
;惩罚中
;-------------------------------------
@ACT_MENU_OSHIOKI

;惩罚标志
IF TFLAG:惩罚标志
	CALL ADD_CRI_VAR("施虐", , 4)
	CALL ADD_CRI_VAR("异常", , 2)
	CALL ADD_CRI_VAR("奴役", , 1)
	CALL ADD_CRI_VAR("奉仕", -999)
ENDIF

;──────────────────────────────
;实行不可能的判定
;──────────────────────────────
;-------------------------------------
;插入
;和りあえず、现行的插入续行仕大人(pa0903版时点)にあわせる(COMABLEで续行不做指令被蹴りだす)
;和いうかこ的仕大人但ある场合、下手に許可されないもんに落和す和指令全滅で错误になる是ず
;
;插入续行制限可で实行可能的指令但ないも的是こ的时点数蹴る
;修正是性交(性奉) > 爱抚 > 会话・施虐
;愤怒修正是こ的仕大人だ和解除に向かいやすくなるわけでもない的で置い和く
;怒时で插入解除和かそういう別事件被挟んでやったほう但よさげ？
;假阳具时指令禁止根据措置但动かない的で、追加で续行させやすく做ために修正被与える
;-------------------------------------
@ACT_MENU_INSERT

;性交/性交奉仕中
IF TEQUIP:性交中 || TEQUIP:性交奉仕中
	;假阴茎插入
	IF TEQUIP:性交中 && TEQUIP:调教者假阴茎
		CALL ADD_CRI_VAR("性交", 5)
	;假阴茎插入以外
	ELSE
		CALL ADD_CRI_VAR("道具", -999)
		CALL ADD_CRI_VAR("羞耻", -999)
		CALL ADD_CRI_VAR("奉仕", -999)
		CALL ADD_CRI_VAR("施虐", , 2)
		CALL ADD_CRI_VAR("异常", -999)
		CALL ADD_CRI_VAR("奴役", -999)
	ENDIF
	CALL ADD_CRI_VAR("会话", , 2)
	CALL ADD_CRI_VAR("爱抚", , 4)
	;性交中
	IF TEQUIP:性交中
		CALL ADD_CRI_VAR("性交", 5, 5)
		SIF !TEQUIP:调教者假阴茎
			CALL ADD_CRI_VAR("性奉", -999)
	;性交奉仕中
	ELSE
		CALL ADD_CRI_VAR("性奉", 5, 5)
		CALL ADD_CRI_VAR("性交", -999)
	ENDIF
ENDIF

;----------------------------------------------------------
;焦虑中、Ｃ刺激被中断不做ように
;----------------------------------------------------------
@ACT_MENU_IRRITATE

;焦虑中
IF TCVAR:MASTER:焦虑度
	;焦虑终了的瞬间是それに集中。もしく是性交奉仕中で是ない场合
	;(性交奉仕に関して是插入修正的内容で问题ないんじゃないかな)
	IF TCVAR:MASTER:焦虑度 == 1 || !TEQUIP:性交奉仕中
		;今后再考的必要有りか
		CALL ADD_CRI_VAR("会话", -999)
		CALL ADD_CRI_VAR("道具", -999)
		CALL ADD_CRI_VAR("性交", -999)
		CALL ADD_CRI_VAR("羞耻", -999)
		CALL ADD_CRI_VAR("奉仕", -999)
		CALL ADD_CRI_VAR("施虐", -999)
		CALL ADD_CRI_VAR("异常", -999)
		CALL ADD_CRI_VAR("奴役", -999)
	ENDIF
ENDIF

;----------------------------------------------------------
;A性交回避
;----------------------------------------------------------
@ACT_MENU_AGUARD

;调教方针但异常的场合、50%的确率で判定被行う
IF IS_POLICY("今", "异常") && RAND:2
	;ＡＳ可だ但、慣れてない的でガード做。ただし50%(?!)
	SIF EXP:MASTER:Ａ経験 < 2 && !TEQUIP:肛门震动棒 && !TEQUIP:肛门拉珠 && !TEQUIP:灌肠器＋肛塞
		CALL ADD_CRI_VAR("性交", -999)
ENDIF

;----------------------------------------------------------
;肛交中、温和方针で他的体位に变更不做ように
;----------------------------------------------------------
@ACT_MENU_ASEX_LIMITATION

IF IS_POLICY("今", "温和")
	SIF TEQUIP:性交中 == 6
		CALL ADD_CRI_VAR("性交", -999)
	SIF TEQUIP:性交奉仕中 == 6
		CALL ADD_CRI_VAR("性奉", -999)
ENDIF

;----------------------------------------------------------
;实行判定
;----------------------------------------------------------
@ACT_MENU_ABLE

;不可能判定
FOR LOCAL, 0, 11
	CALL ACTM_ABLE(LOCAL)
	SIF !RESULT
		CALL DIM_CRI_VAR(MENU(LOCAL), -999)
NEXT

;-------------------------------------
;兴奋値
;-------------------------------------
@ACT_MENU_HEAT
#LOCALSIZE 1
#DIM LCOUNT

FOR LCOUNT, 0, 11
	SIF LCOUNT == 9
		CONTINUE
	LOCAL = TFLAG:(@"%MENU(LCOUNT)%的兴奋") * LIMIT(TFLAG:可能的ACT系列数 - 1, 0, 4) / 4
	CALL ADD_CRI_VAR(MENU(LCOUNT), -LOCAL)
NEXT

;-------------------------------------
;ACT修正値
;-------------------------------------
@ACT_MENU_CORRECTION
#DIM LCOUNT

FOR LCOUNT, 0, 11
	;修正値被加算
	CALL ACT_M_RES_MENU(LCOUNT)
	CALL ADD_CRI_VAR(MENU(LCOUNT), RESULT)
NEXT
