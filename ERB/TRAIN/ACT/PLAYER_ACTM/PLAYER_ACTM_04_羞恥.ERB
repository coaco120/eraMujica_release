;────────────────────────────────────
;羞耻 (自慰40/张开41/看到42/大镜43/野外44/放尿45/COS(未实装)46/放置73)
;────────────────────────────────────
@ACT_M4(ARG = 0)
;1以上那么判定被行わない
#DIM LCOUNT, 1

;関数初期化
CALL ACT_M_INIT(4)

;处理开始

LOCALS = ACT_ACTM4

CALL CHA_CRI_VAR, "基準値"
CALLFORM %LOCALS%_REFERENCE

CALL CHA_CRI_VAR, "调教者的素質"
CALLFORM %LOCALS%_TALENT_T

CALL CHA_CRI_VAR, "调教对象的素質"
CALLFORM %LOCALS%_TALENT_M

CALL CHA_CRI_VAR, "调教对象的能力"
CALLFORM %LOCALS%_ABL_M

CALL CHA_CRI_VAR, "调教者的能力"
CALLFORM %LOCALS%_ABL_T

CALL CHA_CRI_VAR, "调教对象的欲望"
CALLFORM %LOCALS%_DESIRE_M

CALL CHA_CRI_VAR, "调教者的罪恶感"
CALLFORM %LOCALS%_SIN_T

CALL CHA_CRI_VAR, "调教者的ALI"
CALLFORM %LOCALS%_ALI_T

CALL CHA_CRI_VAR, "调教对象的体力"
CALLFORM %LOCALS%_PHYSICAL_M

CALL CHA_CRI_VAR, "调教对象的气力"
CALLFORM %LOCALS%_MENTAL_M

CALL CHA_CRI_VAR, "调教者的兴趣"
CALLFORM %LOCALS%_INTEREST_T

CALL CHA_CRI_VAR, "调教者的满足"
CALLFORM %LOCALS%_SATISFACTION_T

CALL CHA_CRI_VAR, @"调教者的状态(%CONDITION(TCVAR:状态变化)%)"
CALLFORM %LOCALS%_CONDITION_T

CALL CHA_CRI_VAR, @"调教对象的状态(%CONDITION(TCVAR:MASTER:状态变化)%)"
CALLFORM %LOCALS%_CONDITION_M

CALL CHA_CRI_VAR, "媚药"
CALLFORM %LOCALS%_DRAG

CALL CHA_CRI_VAR, "利尿剂"
CALLFORM %LOCALS%_DIURETIC

CALL CHA_CRI_VAR, "装着道具"
CALLFORM %LOCALS%_TOOLS

CALL CHA_CRI_VAR, "绳"
CALLFORM %LOCALS%_ROPE

CALL CHA_CRI_VAR, "眼罩"
CALLFORM %LOCALS%_EYEMASK

CALL CHA_CRI_VAR, "口球"
CALLFORM %LOCALS%_GAG

CALL CHA_CRI_VAR, "三角木马"
CALLFORM %LOCALS%_HORSE

CALL CHA_CRI_VAR, "野外PLAY"
CALLFORM %LOCALS%_OUT

CALL CHA_CRI_VAR, "大镜"
CALLFORM %LOCALS%_T_MILLER

CALL CHA_CRI_VAR, "道具被是ずした"
CALLFORM %LOCALS%_TAKE_OFF_TOOLS

CALL CHA_CRI_VAR, "调教者自慰中"
CALLFORM %LOCALS%_MASTURBATION_T

CALL CHA_CRI_VAR, "调教对象自慰中"
CALLFORM %LOCALS%_MASTURBATION_M

CALL CHA_CRI_VAR, "调教对象的衣服"
CALLFORM %LOCALS%_CLOTH_M

CALL CHA_CRI_VAR, "更加强硬"
CALLFORM %LOCALS%_PLEASE_HARD

CALL CHA_CRI_VAR, "请求宽恕"
CALLFORM %LOCALS%_PLEASE_HELP

CALL CHA_CRI_VAR, "连续行动修正"
CALLFORM %LOCALS%_PREV

CALL CHA_CRI_VAR, "口上修正値"
CALL ACT_ACTM_KOJO_CORRECTION_T, 4

CALL CHA_CRI_VAR, "ACT修正値"
CALLFORM %LOCALS%_CORRECTION(ARG)

;ARG但1以上那么处理中断
SIF ARG
	RETURN RESULT

CALL CHA_CRI_VAR, "实行判定"
CALLFORM %LOCALS%_ABLE

;────────────────────────────────────
;最终判定
;────────────────────────────────────
CALL DEF_CRI_VAR("最大前者")
LOCAL = GET_CRI_VAR("決定")
IF LOCAL == -1
	SIF FLAG:DEBUG
		PRINTL (羞耻类ACT中不存在可执行的ACT)
	TFLAG:ACT = -1
	CALL ACT_MENU_RESULT(2, "羞耻")
ELSE
	TFLAG:ACT = GET_ACTNUM(RESULTS)
ENDIF
CALL ANA_CRI_VAR()

;-------------------------------------------------------------------------
;基準値
;-------------------------------------------------------------------------
@ACT_ACTM4_REFERENCE

;要らないかも和思った的で試しに无效化@これみ
[SKIPSTART]
CALL ADD_CRI_VAR("自慰", 10)
CALL ADD_CRI_VAR("打开小穴", 10)
CALL ADD_CRI_VAR("展示自慰", 8)
CALL ADD_CRI_VAR("羞耻PLAY", 8)
CALL ADD_CRI_VAR("野外PLAY", 0)
CALL ADD_CRI_VAR("放尿", 5)
CALL ADD_CRI_VAR("COSPLAY", 5)
CALL ADD_CRI_VAR("放置PLAY", 0)
[SKIPEND]

;-------------------------------------------------------------------------
;调教者的素質参照
;-------------------------------------------------------------------------
@ACT_ACTM4_TALENT_T

;调教者但自尊心高/低
IF TALENT:自尊心高
	CALL ADD_CRI_VAR("展示自慰", GETBIT(TEQUIP:自慰中, 1) ? 0 # -5)
ELSEIF TALENT:自尊心低
	CALL ADD_CRI_VAR("展示自慰", GETBIT(TEQUIP:自慰中, 1) ? 0 # 5)
ENDIF

;调教者但自制的/冲动的
IF TALENT:自制的
	CALL ADD_CRI_VAR("展示自慰", GETBIT(TEQUIP:自慰中, 1) ? 0 # -3)
	CALL ADD_CRI_VAR("野外PLAY", -3)
ELSEIF TALENT:冲动的
	CALL ADD_CRI_VAR("展示自慰", GETBIT(TEQUIP:自慰中, 1) ? 0 # 3)
	CALL ADD_CRI_VAR("野外PLAY", 3)
ENDIF

;调教者但无关心/好奇心
IF TALENT:无关心
	CALL ADD_CRI_VAR("自慰", GETBIT(TEQUIP:自慰中, 0) ? 0 # -2)
	CALL ADD_CRI_VAR("打开小穴", -3)
	CALL ADD_CRI_VAR("放置PLAY", 3, 4)
ELSEIF TALENT:好奇心
	CALL ADD_CRI_VAR("自慰", GETBIT(TEQUIP:自慰中, 0) ? 0 # 1, GETBIT(TEQUIP:自慰中, 0) ? 0 # 3)
	CALL ADD_CRI_VAR("打开小穴", 2, 3)
ENDIF

;调教者但不越过底线
SIF TALENT:不越过底线 && !TEQUIP:野外PLAY
	CALL ADD_CRI_VAR("野外PLAY", -5)

;调教者但渴望关注かつ、调教者但自慰していない
SIF TALENT:渴望关注 && !GETBIT(TEQUIP:自慰中, 1)
	CALL ADD_CRI_VAR("展示自慰", 5, 5)

;调教者但压抑/解放
IF TALENT:压抑
	CALL ADD_CRI_VAR("展示自慰", GETBIT(TEQUIP:自慰中, 1) ? 0 # -3)
	CALL ADD_CRI_VAR("野外PLAY", TEQUIP:野外PLAY ? 0 # -5)
	CALL ADD_CRI_VAR("放尿", -5)
	CALL ADD_CRI_VAR("COSPLAY", -5)
ELSEIF TALENT:解放
	CALL ADD_CRI_VAR("展示自慰", , GETBIT(TEQUIP:自慰中, 1) ? 0 # 5)
	CALL ADD_CRI_VAR("野外PLAY", , TEQUIP:野外PLAY ? 0 # 7)
	CALL ADD_CRI_VAR("放尿", , 7)
	CALL ADD_CRI_VAR("COSPLAY", , 7)
ENDIF

;调教者但羞涩/无耻
IF TALENT:羞涩
	CALL ADD_CRI_VAR("展示自慰", GETBIT(TEQUIP:自慰中, 1) ? 0 # -7)
	CALL ADD_CRI_VAR("羞耻PLAY", TEQUIP:大镜 ? 0 # -5)
ELSEIF TALENT:无耻
	CALL ADD_CRI_VAR("展示自慰", , GETBIT(TEQUIP:自慰中, 1) ? 0 # 5)
	CALL ADD_CRI_VAR("羞耻PLAY", TEQUIP:大镜 ? 0 # 5)
ENDIF

;调教者但易湿
SIF TALENT:易湿
	CALL ADD_CRI_VAR("打开小穴", , 2)

;调教者但自慰し容易かつ、调教者但自慰していない
SIF TALENT:自慰し容易 && !GETBIT(TEQUIP:自慰中, 1)
	CALL ADD_CRI_VAR("展示自慰", 3, 1)

;调教者但脏污无視、污臭钝感/敏感
IF TALENT:脏污无視 || TALENT:污臭钝感
	CALL ADD_CRI_VAR("放尿", 1, 4)
ELSEIF TALENT:污臭敏感
	CALL ADD_CRI_VAR("放尿", 4)
ENDIF

;调教者但承认快感/否定
IF TALENT:承认快感
	CALL ADD_CRI_VAR("展示自慰", GETBIT(TEQUIP:自慰中, 1) ? 0 # 3)
ELSEIF TALENT:否定快感
	CALL ADD_CRI_VAR("展示自慰", , GETBIT(TEQUIP:自慰中, 1) ? 0 # -6)
ENDIF

;调教者但恐男かつ、调教对象但男人
SIF TALENT:恐男 && TALENT:MASTER:男人
	CALL ADD_CRI_VAR("自慰", -5)

;调教者但虐待狂
IF TALENT:虐待狂
	CALL ADD_CRI_VAR("野外PLAY", , TEQUIP:野外PLAY ? 0 # 4)
	CALL ADD_CRI_VAR("放尿", 3, 3)
ENDIF

;调教者但慎重かつ、野外PLAY中不是
SIF TALENT:慎重 && !TEQUIP:野外PLAY
	CALL ADD_CRI_VAR("野外PLAY", -3)

;调教者但坏心眼
IF TALENT:坏心眼
	CALL ADD_CRI_VAR("羞耻PLAY", TEQUIP:大镜 ? 0 # 3)
	CALL ADD_CRI_VAR("野外PLAY", TEQUIP:野外PLAY ? 0 # 1)
	CALL ADD_CRI_VAR("放尿", 3)
	CALL ADD_CRI_VAR("放置PLAY", 3)
ENDIF

;调教者但幼稚
SIF TALENT:幼稚
	CALL ADD_CRI_VAR("放尿", , 4)

;调教者但魅惑かつ、调教对象但自慰していない
SIF TALENT:魅惑 && !GETBIT(TEQUIP:自慰中, 0)
	CALL ADD_CRI_VAR("自慰", , 6)

;-------------------------------------------------------------------------
;调教对象的素質参照
;-------------------------------------------------------------------------
@ACT_ACTM4_TALENT_M

;调教对象但自慰し容易かつ、调教对象但自慰していない
SIF TALENT:MASTER:自慰し容易 && !GETBIT(TEQUIP:自慰中, 0)
	CALL ADD_CRI_VAR("自慰", 3, 1)

;────────────────────────────────────
;能力、参数根据变动
;────────────────────────────────────
;-------------------------------------------------------------------------
;调教对象的能力被参照
;-------------------------------------------------------------------------
@ACT_ACTM4_ABL_M

;调教对象的露出癖。调教但進んだ状态でより强硬な菜单被选择做傾向
;但[0→2, 5→-5]被加算(0～7)
CALL ADD_CRI_VAR("打开小穴", LINE_CALC(ABL:MASTER:露出癖, "0→2,5→-5,LIMIT/-5～2"))
;但[2→0, 5→2～8]被加算(0～8)
CALL ADD_CRI_VAR("放尿", LINE_CALC(ABL:MASTER:露出癖, "2→0,5→2,LIMIT/0～2"), LINE_CALC(ABL:MASTER:露出癖, "2→0,5→6,LIMIT/0～6"))
;但[1→0, 5→0～9]被加算(0～9)
CALL ADD_CRI_VAR("COSPLAY", , LINE_CALC(ABL:MASTER:露出癖, "1→0,5→9,LIMIT/0～9"))
;但[2→0, 5→0～7]被加算(0～7)
CALL ADD_CRI_VAR("放置PLAY", , LINE_CALC(ABL:MASTER:露出癖, "2→0,5→7,LIMIT/0～7"))
;但[0→0, 2→0～4, 4→0]被加算(0～4)
CALL ADD_CRI_VAR("自慰", , LINE_CALC(ABL:MASTER:露出癖, "0→0,2→4,4→0,LIMIT/0～4"))
;但[0→0, 2→2, 4→0]被加算(0～2)
CALL ADD_CRI_VAR("展示自慰", LINE_CALC(ABL:MASTER:露出癖, "0→0,2→2,4→0,LIMIT/0～2"))
;※キャンセルに修正是付かない
;但[0→0/0, 4→0/3～5, 8→0/0]被加算(0～5)
SIF !TEQUIP:大镜
	CALL ADD_CRI_VAR("羞耻PLAY", LINE_CALC(ABL:MASTER:露出癖, "0→0,4→3,8→0,LIMIT/0～3"), LINE_CALC(ABL:MASTER:露出癖, "0→0,4→2,8→0,LIMIT/0～2"))
;但[2→0/0, 5→0/4～8]被加算(0～8)
SIF !TEQUIP:野外PLAY
	CALL ADD_CRI_VAR("野外PLAY", LINE_CALC(ABL:MASTER:露出癖, "2→0,5→4,LIMIT/0～4"), LINE_CALC(ABL:MASTER:露出癖, "2→0,5→4,LIMIT/0～4"))

;-------------------------------------------------------------------------
;调教者的能力被参照
;-------------------------------------------------------------------------
@ACT_ACTM4_ABL_T

;调教者的异常但[0→0, 5→3～5]被加算(0～5)
CALL ADD_CRI_VAR("放置PLAY", LINE_CALC(ABL:异常, "0→0,5→3,LIMIT/0～3"), LINE_CALC(ABL:异常, "0→0,5→2,LIMIT/0～2"))

;-------------------------------------------------------------------------
;PALAM群
;ここ是单独に
;-------------------------------------------------------------------------
;----------------------------------
;欲望
;----------------------------------
@ACT_ACTM4_DESIRE_M

;暫定的处理。调教者但坏心眼那么、欲望した调教对象被…
IF TALENT:坏心眼
	;调教对象但自慰中
	IF GETBIT(TEQUIP:自慰中, 0)
		;调教者的欲望但[3000→0, 7500→5]被加算(0～5)
		CALL ADD_CRI_VAR("自慰", LINE_CALC(PALAM:MASTER:欲望, "3000→0,7500→5,LIMIT/0～5"))
	;调教对象是自慰していない
	ELSE
		;调教者的欲望但[3000→0/2, 7500→5/7]被加算(0～7)(调教者自慰によって増加)
		CALL ADD_CRI_VAR("放置PLAY", LINE_CALC(PALAM:MASTER:欲望, "3000→0,7500→5,LIMIT/0～5") + GETBIT(TEQUIP:自慰中, 1))
	ENDIF
ENDIF

;-------------------------------------
;调教者的罪恶感
;-------------------------------------
@ACT_ACTM4_SIN_T

;罪恶感但[0→0, 100→-5～-2]被加算(-5～0)
CALL ADD_CRI_VAR("放尿", LINE_CALC(CFLAG:罪恶感, "0→0,100→-2,LIMIT/-2～0"), LINE_CALC(CFLAG:罪恶感, "0→0,100→-3,LIMIT/-3～0"))
;罪恶感但[0→0, 100→-7～-4]被加算(-7～0)
CALL ADD_CRI_VAR("放置PLAY", LINE_CALC(CFLAG:罪恶感, "0→0,100→-4,LIMIT/-4～0"), LINE_CALC(CFLAG:罪恶感, "0→0,100→-3,LIMIT/-3～0"))
;※キャンセルに修正是付かない
;罪恶感但[0→0, 100→0/-5～-2]被加算(-5～0)
SIF !TEQUIP:野外PLAY
	CALL ADD_CRI_VAR("野外PLAY", LINE_CALC(CFLAG:罪恶感, "0→0,100→-2,LIMIT/-2～0"), LINE_CALC(CFLAG:罪恶感, "0→0,100→-3,LIMIT/-3～0"))

;-------------------------------------
;调教者的ALI
;-------------------------------------
@ACT_ACTM4_ALI_T

;定位但[0→0, 50→0/3]被加算(0～3)(调教对象自慰中是修正无)
SIF !GETBIT(TEQUIP:自慰中, 0)
	CALL ADD_CRI_VAR("自慰", LINE_CALC(CFLAG:定位, "0→0,50→3,LIMIT/0～3"))
;定位但[0→0, 50→0/3]被加算(0～3)
CALL ADD_CRI_VAR("打开小穴", LINE_CALC(CFLAG:定位, "0→0,50→3,LIMIT/0～3"))
;定位但[-50→3, 50→-3]被加算(-3～3)
CALL ADD_CRI_VAR("放置PLAY", LINE_CALC(CFLAG:定位, "-50→3,50→-3,LIMIT/-3～3"))
;定位但[-50→3, 0→0]被加算(0～3)(キャンセルに是修正无)
SIF !TEQUIP:野外PLAY
	CALL ADD_CRI_VAR("野外PLAY", LINE_CALC(CFLAG:定位, "-50→3,0→0,LIMIT/0～3"))
;定位但[-50→3, 0→0]被加算(0～3)
CALL ADD_CRI_VAR("放尿", LINE_CALC(CFLAG:定位, "-50→3,0→0,LIMIT/0～3"))

;────────────────────────────────────
;ゲージや状态根据变动
;────────────────────────────────────
;-------------------------------------
;调教对象的体力
;-------------------------------------
@ACT_ACTM4_PHYSICAL_M

;调教者的体力但[0→-1, 500→0]被加算(-1～0)
CALL ADD_CRI_VAR("羞耻PLAY", LINE_CALC(BASE:体力, "0→-1,500→0,LIMIT/-1～0"))
;调教者的体力但[0→3/-3, 1500→0/0]被加算(-3～3)(调教者自慰中是正)
LOCAL = GETBIT(TEQUIP:自慰中, 1) ? 1 # -1
CALL ADD_CRI_VAR("展示自慰", LOCAL * LINE_CALC(BASE:体力, "0→3,1500→0,LIMIT/0～3"))
;调教者的体力但[0→3/-3, 1500→0/0]被加算(-3～3)(实行中是正)
LOCAL = TEQUIP:野外PLAY ? 1 # -1
CALL ADD_CRI_VAR("野外PLAY", LOCAL * LINE_CALC(BASE:体力, "0→3,1500→0,LIMIT/0～3"))

;-------------------------------------
;调教对象的气力
;-------------------------------------
@ACT_ACTM4_MENTAL_M

;调教者的气力但[0→-2, 500→0]被加算(-2～0)
CALL ADD_CRI_VAR("打开小穴", LINE_CALC(BASE:气力, "0→-2,500→0,LIMIT/-2～0"))
;调教者的气力但[0→2, 500→0]被加算(0～2)
CALL ADD_CRI_VAR("放置PLAY", LINE_CALC(BASE:气力, "0→2,500→0,LIMIT/0～2"))
;调教者的气力但[0→5/-5, 500→0/0]被加算(-5～5)(调教者自慰中是正)
LOCAL = GETBIT(TEQUIP:自慰中, 1) ? 1 # -1
CALL ADD_CRI_VAR("展示自慰", LOCAL * LINE_CALC(BASE:气力, "0→5,750→0,LIMIT/0～5"))
;调教者的气力但[0→3/-3, 500→0/0]被加算(-3～3)(实行中是正)
LOCAL = TEQUIP:野外PLAY ? 1 # -1
CALL ADD_CRI_VAR("野外PLAY", LOCAL * LINE_CALC(BASE:气力, "0→3,500→0,LIMIT/0～3"))

;-------------------------------------
;调教者的兴趣
;-------------------------------------
@ACT_ACTM4_INTEREST_T

;调教者的兴趣但[500→1～2, 750→3～6]被加算(-2～6)
CALL ADD_CRI_VAR("打开小穴", LINE_CALC(BASE:兴趣, "500→1,750→3,LIMIT/-1～3"), LINE_CALC(BASE:兴趣, "500→1,750→3,LIMIT/-1～3"))
;调教者的兴趣但500未满那么
IF BASE:兴趣 < 500
	CALL ADD_CRI_VAR("COSPLAY", , 4)
	CALL ADD_CRI_VAR("放置PLAY", 3)
ENDIF

;-------------------------------------
;调教者的满足
;-------------------------------------
@ACT_ACTM4_SATISFACTION_T

;调教者的满足但[500→3/-3, 750→5/-5]被加算(-5～5)(实行中是正)
LOCAL = TEQUIP:野外PLAY ? 1 # -1
CALL ADD_CRI_VAR("野外PLAY", LOCAL * LINE_CALC(BASE:满足, "500→3,750→5,LIMIT/0～5"))

;-------------------------------------
;调教者的状态
;-------------------------------------
@ACT_ACTM4_CONDITION_T

;调教者的状态(0=通常/1=疲惫/2=衰弱/3=无气力/4=朦胧/5=情欲/6=愤怒/7=无聊/8=郁闷)
SELECTCASE TCVAR:状态变化
	CASE 1 TO 3
		CALL ADD_CRI_VAR("展示自慰", GETBIT(TEQUIP:自慰中, 1) ? 5 # -5)
		CALL ADD_CRI_VAR("野外PLAY", TEQUIP:野外PLAY ? 5 # -5)
	CASE 7
		CALL ADD_CRI_VAR("自慰", GETBIT(TEQUIP:自慰中, 0) ? 5 # 0)
		CALL ADD_CRI_VAR("COSPLAY", TEQUIP:MASTER:COSPLAY ? 5 # 0)
		CALL ADD_CRI_VAR("放置PLAY", GETBIT(TEQUIP:自慰中, 0) ? 0 # 5)
	CASE 8
		CALL ADD_CRI_VAR("放尿", , 6)
ENDSELECT

;-------------------------------------
;调教对象的状态
;-------------------------------------
@ACT_ACTM4_CONDITION_M

;调教对象的状态但疲惫/衰弱/无气力
IF 1 <= TCVAR:MASTER:状态变化 && TCVAR:MASTER:状态变化 <= 3
	CALL ADD_CRI_VAR("自慰", (GETBIT(TEQUIP:自慰中, 0) ? 1 # -1) * (TFLAG:疲惫度 * 2 + TCVAR:MASTER:状态变化 + 1))
	CALL ADD_CRI_VAR("羞耻PLAY", (TEQUIP:大镜 && TCVAR:MASTER:状态变化 == 3 ? 1 # -1) * (TFLAG:疲惫度 + 3))
	CALL ADD_CRI_VAR("野外PLAY", (TEQUIP:野外PLAY ? 1 # -1) * (TFLAG:疲惫度 * 2 + TCVAR:MASTER:状态变化), (TEQUIP:野外PLAY ? 1 # -1) * 5)
ENDIF

;────────────────────────────────────
;上次的行动や状況根据变动
;────────────────────────────────────
;-------------------------------------
;媚药
;-------------------------------------
@ACT_ACTM4_DRAG
SIF TEQUIP:媚药
	CALL ADD_CRI_VAR("放置PLAY", TALENT:坏心眼 * 3 - TALENT:好心眼 * 3, 4)

;-------------------------------------
;利尿剂
;-------------------------------------
@ACT_ACTM4_DIURETIC
SIF TEQUIP:利尿剂
	CALL ADD_CRI_VAR("放尿", 3, 4)

;-------------------------------------
;装着道具类
;-------------------------------------
@ACT_ACTM4_TOOLS

;震动棒/Ａ震动棒/阴蒂夹/飞机杯/乳头夹装着中で、调教对象自慰中不是
SIF (TEQUIP:震动棒 || TEQUIP:肛门震动棒 || TEQUIP:阴蒂夹 || TEQUIP:飞机杯 || TEQUIP:乳头夹) && !GETBIT(TEQUIP:自慰中, 0)
	CALL ADD_CRI_VAR("自慰", 3 + TEQUIP:震动棒 + TEQUIP:肛门震动棒 + TEQUIP:阴蒂夹 + TEQUIP:飞机杯 + TEQUIP:乳头夹)

;-------------------------------------
;绳
;-------------------------------------
@ACT_ACTM4_ROPE
SIF TEQUIP:绳
	CALL ADD_CRI_VAR("放置PLAY", 5)

;-------------------------------------
;眼罩
;-------------------------------------
@ACT_ACTM4_EYEMASK
IF TEQUIP:眼罩
	CALL ADD_CRI_VAR("羞耻PLAY", TEQUIP:大镜 ? 0 # -10)
	CALL ADD_CRI_VAR("放置PLAY", 3)
ENDIF

;-------------------------------------
;口球
;-------------------------------------
@ACT_ACTM4_GAG
SIF TEQUIP:口球
	CALL ADD_CRI_VAR("放置PLAY", , 3)

;-------------------------------------
;三角木马
;-------------------------------------
@ACT_ACTM4_HORSE
SIF TEQUIP:三角木马
	CALL ADD_CRI_VAR("放置PLAY", , 7)

;-------------------------------------
;野外PLAY
;-------------------------------------
@ACT_ACTM4_OUT
SIF TEQUIP:野外PLAY
	CALL MUL_CRI_VAR("放置PLAY", 150)

;-------------------------------------
;大镜
;-------------------------------------
@ACT_ACTM4_T_MILLER
IF TEQUIP:大镜
	CALL ADD_CRI_VAR("自慰", GETBIT(TEQUIP:自慰中, 0) ? 0 # 3)
	CALL ADD_CRI_VAR("放尿", , 4)
ENDIF

;-------------------------------------
;要求されて道具被是ずした
;-------------------------------------
@ACT_ACTM4_TAKE_OFF_TOOLS

;キャンセルに应じて装着被躊躇
SIF !TEQUIP:野外PLAY
	CALL ADD_CRI_VAR("野外PLAY", -CANCEL_TEQUIP_COUNT("野外PLAY") / 20)
SIF !TEQUIP:大镜
	CALL ADD_CRI_VAR("羞耻PLAY", -CANCEL_TEQUIP_COUNT("大镜") / 20)

;-------------------------------------
;调教者自慰中
;-------------------------------------
@ACT_ACTM4_MASTURBATION_T

;调教者的み
IF TEQUIP:自慰中 == 2
	CALL ADD_CRI_VAR("自慰", 5)
	CALL ADD_CRI_VAR("放置PLAY", , 10)
ENDIF

;-------------------------------------
;调教对象自慰中
;-------------------------------------
@ACT_ACTM4_MASTURBATION_M

;调教对象的み
IF TEQUIP:自慰中 == 1
	CALL ADD_CRI_VAR("打开小穴", -5)
	;调教者的下半身外套で妨害されない和き
	SIF CLOTHESTYPE(0, 1) != 2
		CALL ADD_CRI_VAR("展示自慰", 5)
	CALL ADD_CRI_VAR("放尿", -3)
ENDIF

;-------------------------------------
;调教对象的衣服
;-------------------------------------
@ACT_ACTM4_CLOTH_M

;调教对象的下半身外套で妨害され时き
IF CLOTHESTYPE(1, 1) == 2
	CALL ADD_CRI_VAR("自慰", -5)
	CALL ADD_CRI_VAR("羞耻PLAY", TEQUIP:大镜 ? 0 # -5)
	CALL ADD_CRI_VAR("放尿", -10)
ENDIF

;-------------------------------------
;更加强硬
;-------------------------------------
@ACT_ACTM4_PLEASE_HARD
IF IS_NOWCOMNAME("更加强硬")
	CALL ADD_CRI_VAR("羞耻PLAY", TEQUIP:大镜 ? -5 # 5)
	CALL ADD_CRI_VAR("野外PLAY", TEQUIP:野外PLAY ? -5 # 5)
ENDIF

;-------------------------------------
;请求宽恕
;-------------------------------------
@ACT_ACTM4_PLEASE_HELP
;許し被乞ったかつ、调教者的状态但郁闷以外
IF IS_NOWCOMNAME("请求宽恕") && TCVAR:状态变化 < 8
	CALL ADD_CRI_VAR("羞耻PLAY", TEQUIP:大镜 ? 5 # -5)
	CALL ADD_CRI_VAR("野外PLAY", TEQUIP:野外PLAY ? 5 # -5)
ENDIF

;────────────────────────────────────
;同じ行动连续实行的确率被ダウンし增加
;────────────────────────────────────
@ACT_ACTM4_PREV
SELECTCASE TFLAG:前ACT
	CASE 40 TO 47, 73
		CALL ADD_CRI_VAR(GET_ACTNAME(TFLAG:前ACT), -3 * TFLAG:同一行动奖励, -4 * TFLAG:同一行动奖励)
ENDSELECT

;────────────────────────────────────
;修正値計算
;────────────────────────────────────
@ACT_ACTM4_CORRECTION(ARG)
#DIM LCOUNT

;ARG但0以外那么修正値被計算し、以后的处理被行わない
IF ARG
	;73に関して是ここで計算不做
	FOR LCOUNT, 40, 47
		CALL ACT_M_RES_CALC(LCOUNT, GET_CRI_VAR(GET_ACTNAME(LCOUNT) + "累计"))
	NEXT
ELSE
	;修正値被判定値に加算做
	CALL ACT_ACTM_CORRECTION(4)
ENDIF

;────────────────────────────────────
;其他变动や实行不可能的判定
;────────────────────────────────────
@ACT_ACTM4_ABLE
#DIM LCOUNT

;不可能判定
CALL ACT_ACTM_ABLE(4)
